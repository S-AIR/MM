```abap
*&---------------------------------------------------------------------*
*& Include          ZC103MMR0002F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form get_pr_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_data .
  " 구매요청 데이터, 마스터 데이터 가져오기
  CLEAR : gt_prbody, gt_pobody.

*-- 구매요청 헤더 + 아이템 데이터 가져오기
  CLEAR : gt_prbody, gt_prback.
  PERFORM get_pr_data.
  PERFORM get_po_data.

ENDFORM.
FORM get_pr_data.

  SELECT a~prid plnid strid mrpid pr_status estkz
       request_date receive_date b~matid quantity
       b~unit b~price currency
       creator create_time create_date
       approval approve_date approve_time
       a~erdat AS h_erdat a~erzet AS h_erzet a~ernam AS h_ernam
  INTO CORRESPONDING FIELDS OF TABLE gt_prback
  FROM zc103mmt0007 AS a
 INNER JOIN zc103mmt0008 AS b
    ON a~prid EQ b~prid
 WHERE pr_status EQ 'B'
   AND a~prid IN so_prid
   AND a~request_date IN so_reqdt
   AND a~receive_date IN so_recdt.

  gt_prbody = CORRESPONDING #( gt_prback ).

ENDFORM.
FORM get_po_data.
*-- 구매오더 헤더 + 아이템 데이터 가져오기
  CLEAR : gt_pobody, gt_poback.

  SELECT a~poid prid plnid strid mrpid order_date receive_date
         total_price a~currency order_status creator approval
         matid bpid quantity unit price
    INTO CORRESPONDING FIELDS OF TABLE gt_poback
    FROM zc103mmt0011 AS a
   INNER JOIN zc103mmt0012 AS b
      ON a~poid EQ b~poid
   WHERE a~poid IN so_poid
     AND order_date IN so_orddt.

  gt_pobody = CORRESPONDING #( gt_poback ).
ENDFORM.
FORM get_master_data.

  DATA : lv_cnt TYPE i.

  SELECT SINGLE COUNT(*)
    INTO lv_cnt
    FROM ZC103MMt0011
   GROUP BY poid.

  MESSAGE s008 WITH lv_cnt.

  " 직원 마스터 테이블 조회
  SELECT empno dptcode empname
    INTO CORRESPONDING FIELDS OF TABLE gt_emp
    FROM zc103fit0011.

  " 플랜트 마스터 테이블 조회
  SELECT plnid name ptype
    INTO CORRESPONDING FIELDS OF TABLE gt_plant
    FROM zc103mmt0003.

  " 저장위치 마스터 테이블 조회
  SELECT strid plnid name stype
    INTO CORRESPONDING FIELDS OF TABLE gt_stlo
    FROM zc103mmt0004.

  " 자재 마스터 테이블 조회
  SELECT matid matname mattype matgrp base_unit
    INTO CORRESPONDING FIELDS OF TABLE gt_mara
    FROM zc103mmt0001.

  " 구매정보레코드 데이터를 가져온다.
  SELECT pirid matid a~bpid b~name AS bpname lifab lifbi leadtime unit price  a~currency
    INTO CORRESPONDING FIELDS OF TABLE gt_pir_back
    FROM zc103mmt0005 AS a
   INNER JOIN zc103mmt0002 AS b
      ON a~bpid EQ b~bpid.

  " 벤더 마스터 정보를 가져온다
  SELECT bpid name
    INTO CORRESPONDING FIELDS OF TABLE gt_vendor
    FROM zc103mmt0002.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pr_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_data .
  " 구매요청, 구매오더에 세팅할 데이터 세팅하는 서브루틴
  " 아이콘, 직원이름, 플랜트명, 스토리지로케이션명, 스타일필드 세팅

  DATA : lt_emp_value LIKE TABLE OF gs_emp_value.

  PERFORM set_pr_data.
  PERFORM set_po_data.
  PERFORM set_pir_data.

  " 서치헬프값 세팅
  lt_emp_value = CORRESPONDING #( gt_emp ).
  gt_emp_value = VALUE #( FOR ls_emp_value IN lt_emp_value
                          WHERE ( dptcode = 'MM' ) ( ls_emp_value ) ).

ENDFORM.
FORM set_pr_data.
  DATA : lv_tabix TYPE sy-tabix,        " 반복 인덱스
         lt_dd07v TYPE TABLE OF dd07v,  " 도메인 값
         ls_dd07v TYPE dd07v.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_PRSTATUS'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.


  " 구매요청에 직원이름, 플랜트명, 저장위치명 세팅
  CLEAR : gs_prbody.
  LOOP AT gt_prbody INTO gs_prbody.

    lv_tabix = sy-tabix.

    " 아이콘 세팅
    gs_prbody-icon = icon_led_green.

    CLEAR : gs_emp, gs_plant, gs_stlo, ls_dd07v, gs_mara.

    " 직원 마스터 테이블에서 값 읽어 세팅하기 (생성자)
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_prbody-creator.
    gs_prbody-creator_name = gs_emp-empname.

    " 결재자 정보 읽어서 세팅하기
    CLEAR gs_emp.
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_prbody-approval.
    gs_prbody-approve_name = gs_emp-empname.

    " 자재 이름 세팅
    READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_prbody-matid.
    gs_prbody-matname = gs_mara-matname.

    " 플랜트명 세팅
    READ TABLE gt_plant INTO gs_plant WITH KEY plnid = gs_prbody-plnid.
    gs_prbody-pname = gs_plant-name.

    " 저장위치명 세팅
    READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_prbody-strid.
    gs_prbody-sname = gs_stlo-name.

    " 구매요청상태 도메인 값 세팅
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_prbody-pr_status.
    gs_prbody-status_text = ls_dd07v-ddtext.


    " 스타일필드 세팅
    gs_prbody-celltab = VALUE #(
      ( fieldname = 'MAKE_ORDER' style = cl_gui_alv_grid=>mc_style_button ) ).
    gs_prbody-make_order = '오더전환'.

    " 정보 인터널테이블에 반영
    MODIFY gt_prbody FROM gs_prbody INDEX lv_tabix TRANSPORTING icon creator_name approve_name matname
                                                                pname sname status_text celltab make_order.

  ENDLOOP.



ENDFORM.
FORM set_po_data.
  DATA : lv_tabix TYPE sy-tabix,        " 반복 인덱스
         lt_dd07v TYPE TABLE OF dd07v,  " 도메인 값
         ls_dd07v TYPE dd07v.


  " 구매오더 세팅
  CLEAR : gs_pobody, lv_tabix, lt_dd07v, ls_dd07v.

  " 구매오더상태 도메인 값 가져오기
  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_ORDSTATUS'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  LOOP AT gt_pobody INTO gs_pobody.

    lv_tabix = sy-tabix.

    " 아이콘 세팅
    CASE gs_pobody-order_status.
      WHEN 'A'.
        gs_pobody-icon = icon_led_yellow.
      WHEN OTHERS.
        gs_pobody-icon = icon_led_green.
    ENDCASE.

    CLEAR : gs_emp, gs_plant, gs_stlo, gs_vendor.

    " 직원 마스터 테이블에서 값 읽어 세팅하기 (생성자)
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_pobody-creator.
    gs_pobody-creator_name = gs_emp-empname.

    " 결재자 정보 읽어서 세팅하기
    CLEAR gs_emp.
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_pobody-approval.
    gs_pobody-approve_name = gs_emp-empname.

    " 플랜트명 세팅
    READ TABLE gt_plant INTO gs_plant WITH KEY plnid = gs_pobody-plnid.
    gs_pobody-pname = gs_plant-name.

    " 저장위치명 세팅
    READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_pobody-strid.
    gs_pobody-sname = gs_stlo-name.

    " 자재명 세팅
    READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_pobody-matid.
    gs_pobody-matname = gs_mara-matname.

    " 회사이름 세팅
    READ TABLE gt_vendor INTO gs_vendor WITH KEY bpid = gs_pobody-bpid.
    gs_pobody-bpname = gs_vendor-name.

    " 스타일 필드 세팅 (승인 대기 상태일 때만)
    IF gs_pobody-order_status EQ 'A'.
      gs_pobody-approve = '결재하기'.
      gs_pobody-celltab = VALUE #( ( fieldname = 'APPROVE' style = cl_gui_alv_grid=>mc_style_button ) ).
    ENDIF.

    " 구매요청상태 도메인 값 세팅
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_pobody-order_status.
    gs_pobody-status_text = ls_dd07v-ddtext.

    " 정보 인터널테이블에 반영
    MODIFY gt_pobody FROM gs_pobody INDEX lv_tabix TRANSPORTING icon creator_name approve_name status_text
                                                                pname sname bpname approve celltab matname.

  ENDLOOP.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen .

  IF go_container IS INITIAL.

    CLEAR : gt_tfcat, gt_bfcat.
    PERFORM set_pfcat.     " 구매요청 (위쪽) 필드 카탈로그 세팅
    PERFORM set_bfcat.     " 구매오더 (아래쪽) 필드 카탈로그 세팅
    PERFORM set_layout.    " 레이아웃, variant, sort 설정
    PERFORM create_object. " 오브젝트 생성
    PERFORM exclude_toolbar.
    PERFORM register_event.    " 이벤트 등록(문서 초기화, 문서 리스트)

    " 이벤트 핸들러 등록 (top_of_page)
    SET HANDLER : lcl_event_handler=>top_of_page  FOR go_top_grid,        " top-of-page 이벤트
                  lcl_event_handler=>edit_toolbar FOR go_top_grid,        " 툴바 편집 이벤트
                  lcl_event_handler=>edit_toolbar FOR go_bottom_grid,
                  lcl_event_handler=>handle_btn_click FOR go_top_grid,    " 오더전환 버튼 이벤트
                  lcl_event_handler=>handle_btn_click FOR go_bottom_grid, " 오더 결재 버튼 이벤트
                  lcl_event_handler=>user_command     FOR go_bottom_grid.

    CALL METHOD go_top_grid->list_processing_events
      EXPORTING
        i_event_name = 'TOP_OF_PAGE'
        i_dyndoc_id  = go_dyndoc_id.

    " 구매요청 ALV 출력
    CALL METHOD go_top_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_tvari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_tlayo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_prbody
        it_fieldcatalog      = gt_tfcat
        it_sort              = gt_sort.

    " 구매오더 ALV 출력
    CALL METHOD go_bottom_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_bvari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_blayo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_pobody
        it_fieldcatalog      = gt_bfcat
        it_sort              = gt_bsort.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pfcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pfcat .
  " 구매요청 필드카탈로그 세팅
  gt_tfcat = VALUE #(
              ( fieldname = 'ICON'         coltext = '상태' icon = abap_true just = 'C' )
    ( key = 'X' fieldname = 'PRID'            ref_table = 'ZC103MMT0007' just = 'C'  )
              ( fieldname = 'PLNID'           ref_table = 'ZC103MMT0007' just = 'C'  )
              ( fieldname = 'STRID'           ref_table = 'ZC103MMT0007' just = 'C'  )
              ( fieldname = 'PNAME'           ref_table = 'ZC103MMT0003' coltext = '플랜트명'  emphasize = abap_true )
              ( fieldname = 'SNAME'           ref_table = 'ZC103MMT0004' coltext = '저장위치명' emphasize = abap_true )
              ( fieldname = 'ESTKZ'           ref_table = 'ZC103MMT0007' just = 'C' )
              ( fieldname = 'REQUEST_DATE'    ref_table = 'ZC103MMT0007' coltext = '구매요청일' just = 'C' emphasize = abap_true )
              ( fieldname = 'RECEIVE_DATE'    ref_table = 'ZC103MMT0007' coltext = '납품요청일' just = 'C' emphasize = abap_true )
              ( fieldname = 'MATID'           ref_table = 'ZC103MMT0008' just = 'C'  )
              ( fieldname = 'MATNAME'         ref_table = 'ZC103MMT0001' emphasize = abap_true )
              ( fieldname = 'QUANTITY'        ref_table = 'ZC103MMT0008' qfieldname = 'UNIT' coltext = '수량'
                do_sum = abap_true emphasize = abap_true )
              ( fieldname = 'UNIT'            ref_table = 'ZC103MMT0008' coltext = '단위' just = 'C' )
              ( fieldname = 'PRICE'           ref_table = 'ZC103MMT0008' cfieldname = 'CURRENCY' coltext = '금액'
                do_sum = abap_true emphasize = abap_true )
              ( fieldname = 'CURRENCY'        ref_table = 'ZC103MMT0008' coltext = '통화' )
              ( fieldname = 'CREATOR'         ref_table = 'ZC103MMT0003' coltext = '생성자 사번' just = 'C' )
              ( fieldname = 'CREATOR_NAME'    ref_table = 'ZC103FIT0011' coltext = '생성자 이름' just = 'C' )
              ( fieldname = 'APPROVAL'        ref_table = 'ZC103MMT0004' coltext = '승인자 사번' just = 'C' )
              ( fieldname = 'APPROVE_NAME'   ref_table = 'ZC103FIT0011' coltext = '승인자 이름' just = 'C' )
              ( fieldname = 'STATUS_TEXT'  coltext = '구매요청상태' just = 'C'  emphasize = abap_true )
              ( fieldname = 'MAKE_ORDER'  coltext = '오더전환' just = 'C' ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_bfcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_bfcat .
  " 구매오더 필드카탈로그 세팅
  gt_bfcat = VALUE #(
             (   fieldname = 'ICON'         coltext = '상태' icon = abap_true just = 'C' )
    ( key = 'X'	 fieldname = 'POID'            ref_table = 'ZC103MMT0011' just = 'C' )
             (   fieldname = 'PRID'            ref_table = 'ZC103MMT0011' just = 'C' )
             (   fieldname = 'PLNID'           ref_table = 'ZC103MMT0011' just = 'C' )
             (   fieldname = 'STRID'           ref_table = 'ZC103MMT0011' just = 'C' )
             (   fieldname = 'PNAME'           ref_table = 'ZC103MMT0003' coltext = '플랜트명'  emphasize = abap_true )
             (   fieldname = 'SNAME'           ref_table = 'ZC103MMT0004' coltext = '저장위치명' emphasize = abap_true )
             (   fieldname = 'BPID'            ref_table = 'ZC103MMT0012' just = 'C' )
             (   fieldname = 'BPNAME'          ref_table = 'ZC103MMT0002' coltext = '공급업체이름' emphasize = abap_true  )
             (   fieldname = 'ORDER_DATE'      ref_table = 'ZC103MMT0011' coltext = '납품요청일' just = 'C' emphasize = abap_true )
             (   fieldname = 'RECEIVE_DATE'    ref_table = 'ZC103MMT0011' coltext = '납품일'  just = 'C' emphasize = abap_true )
             (   fieldname = 'MATID'           ref_table = 'ZC103MMT0012' just = 'C' )
             (   fieldname = 'MATNAME'         ref_table = 'ZC103MMT0001' just = 'C' )
             (   fieldname = 'QUANTITY'        ref_table = 'ZC103MMT0012' coltext = '수량'
                 do_sum = abap_true  qfieldname = 'UNIT' emphasize = abap_true  )
             (   fieldname = 'UNIT'            ref_table = 'ZC103MMT0012' coltext = '단위' just = 'C' )
             (   fieldname = 'PRICE'           ref_table = 'ZC103MMT0011' coltext = '금액' just = 'R'
                 do_sum = abap_true cfieldname = 'CURRENCY' emphasize = abap_true  )
             (   fieldname = 'CURRENCY'        ref_table = 'ZC103MMT0011' coltext = '통화' just = 'C' )
             (   fieldname = 'CREATOR'         ref_table = 'ZC103MMT0003' coltext = '생성자 사번' just = 'C' )
             (   fieldname = 'CREATOR_NAME'    ref_table = 'ZC103FIT0011' coltext = '생성자 이름' just = 'C' )
             (   fieldname = 'APPROVAL'        ref_table = 'ZC103MMT0004' coltext = '승인자 사번' just = 'C' )
             (   fieldname = 'APPROVE_NAME'    ref_table = 'ZC103FIT0011' coltext = '승인자 이름' just = 'C' )
             (   fieldname = 'STATUS_TEXT'  coltext = '구매오더상태' just = 'C' emphasize = abap_true  )
             (   fieldname = 'APPROVE'  coltext = '결재' just = 'C') ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .
  " layout, variant 설정

  gs_tlayo = VALUE #( zebra = 'X' cwidth_opt = 'A' sel_mode = 'D' totals_bef = abap_true stylefname = 'CELLTAB' ).
  gs_blayo = VALUE #( zebra = 'X' cwidth_opt = 'A' sel_mode = 'D' totals_bef = abap_true stylefname = 'CELLTAB' ).
  gs_tvari = VALUE #( report = sy-repid handle = 'ALV1' ).
  gs_bvari = VALUE #( report = sy-repid handle = 'ALV2' ).

  " sort 설정
  gt_sort = VALUE #(
    ( fieldname = 'PRID' up = abap_true subtot = abap_true  group = abap_true ) ).
  gt_bsort = VALUE #(
    ( fieldname = 'POID' up = abap_true subtot = abap_true  group = abap_true ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_dyndoc_id
    EXPORTING
      style = 'ALV_GRID'.

  CREATE OBJECT go_topof_cont
    EXPORTING
      side      = cl_gui_docking_container=>dock_at_top
      extension = 80.


  " 메인 컨테이너 생성
  CREATE OBJECT go_container
    EXPORTING
      side      = go_container->dock_at_left
      extension = 5000.

  " 스플리터 컨테이너 생성
  CREATE OBJECT go_split_cont
    EXPORTING
      parent  = go_container
      rows    = 2
      columns = 1.

  " 구매요청 컨테이너
  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_top_cont.

  " 구매오더 컨테이너
  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 2
      column    = 1
    RECEIVING
      container = go_bottom_cont.

  " 구매요청 ALV
  CREATE OBJECT go_top_grid
    EXPORTING
      i_parent = go_top_cont.

  " 구매오더 ALB
  CREATE OBJECT go_bottom_grid
    EXPORTING
      i_parent = go_bottom_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event .

  CALL METHOD go_dyndoc_id->initialize_document " 문서 초기화
    EXPORTING
      background_color = cl_dd_area=>col_textarea.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_top_of_page
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_top_of_page .

  DATA : lr_dd_table  TYPE REF TO cl_dd_table_element,
         col_field    TYPE REF TO cl_dd_area,
         col_value    TYPE REF TO cl_dd_area,
         lv_low_text  TYPE sdydo_text_element,
         lv_high_text TYPE sdydo_text_element,
         lv_text      TYPE sdydo_text_element.

  CALL METHOD go_dyndoc_id->add_table
    EXPORTING
      no_of_columns = 2
      border        = '0'
    IMPORTING
      table         = lr_dd_table.

  CALL METHOD lr_dd_table->add_column
    IMPORTING
      column = col_field.

  CALL METHOD lr_dd_table->add_column
    IMPORTING
      column = col_value.

  CALL METHOD col_value->add_text
    EXPORTING
      text         = lv_text
      sap_emphasis = cl_dd_document=>heading
      sap_color    = cl_dd_document=>list_negative_inv.

  " 구매요청번호 텍스트
  IF so_prid IS NOT INITIAL.
    lv_text = |{ so_prid-low }|.
  ELSE.
    lv_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '구매요청번호' lv_text.

  " 구매요청일 텍스트 만들기
  IF so_reqdt IS NOT INITIAL.
    lv_low_text  = |{ so_reqdt-low+0(4) }년 { so_reqdt-low+4(2) }월 { so_reqdt-low+6(2) }일|.
    lv_high_text = |{ so_reqdt-high+0(4) }년 { so_reqdt-high+4(2) }월 { so_reqdt-high+6(2) }일|.
    IF so_reqdt-high IS INITIAL.
      lv_text = |{ lv_low_text }|.
    ELSE.
      lv_text = |{ lv_low_text } ~ { lv_high_text }|.
    ENDIF.
  ELSE.
    lv_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '구매요청일' lv_text.

  " 납품요청일 텍스트 만들기
  IF so_recdt IS NOT INITIAL.
    lv_low_text  = |{ so_recdt-low+0(4) }년 { so_recdt-low+4(2) }월 { so_recdt-low+6(2) }일|.
    lv_high_text = |{ so_recdt-high+0(4) }년 { so_recdt-high+4(2) }월 { so_recdt-high+6(2) }일|.
    IF so_recdt-high IS INITIAL.
      lv_text = |{ lv_low_text }|.
    ELSE.
      lv_text = |{ lv_low_text } ~ { lv_high_text }|.
    ENDIF.
  ELSE.
    lv_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '납품요청일' lv_text.

  PERFORM count_pr_po.

  " 구매오더번호
  IF so_poid IS NOT INITIAL.
    lv_text = |{ so_poid-low }|.
  ELSE.
    lv_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '구매오더번호' lv_text.

  " 구매오더일 텍스트
  IF so_orddt IS NOT INITIAL.
    lv_low_text  = |{ so_orddt-low+0(4) }년 { so_orddt-low+4(2) }월 { so_orddt-low+6(2) }일|.
    lv_high_text = |{ so_orddt-high+0(4) }년 { so_orddt-high+4(2) }월 { so_orddt-high+6(2) }일|.
    IF so_orddt-high IS INITIAL.
      lv_text = |{ lv_low_text }|.
    ELSE.
      lv_text = |{ lv_low_text } ~ { lv_high_text }|.
    ENDIF.
  ELSE.
    lv_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '오더요청일' lv_text.

  PERFORM set_top_of_page.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form add_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LR_DD_TABLE
*&      --> COL_FIELD
*&      --> COL_VALUE
*&      --> P_
*&      --> LV_TEXT
*&---------------------------------------------------------------------*
FORM add_row  USING    pr_dd_table  TYPE REF TO cl_dd_table_element
                       pv_col_field TYPE REF TO cl_dd_area
                       pv_col_value TYPE REF TO cl_dd_area
                       pv_field
                       pv_text.

  DATA : lv_text TYPE sdydo_text_element.

  " col_field 세팅
  lv_text = pv_field.
  CALL METHOD pv_col_field->add_text
    EXPORTING
      text         = lv_text
      sap_emphasis = cl_dd_document=>strong
      sap_color    = cl_dd_document=>list_heading_inv.

  CALL METHOD pv_col_field->add_gap EXPORTING width = 3.

  " col_value 세팅
  lv_text = pv_text.
  CALL METHOD pv_col_value->add_text
    EXPORTING
      text         = lv_text
      sap_emphasis = cl_dd_document=>heading
      sap_color    = cl_dd_document=>list_negative_inv.

  CALL METHOD pv_col_value->add_gap EXPORTING width = 3.
  CALL METHOD pr_dd_table->new_row.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_top_of_page
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_top_of_page .

  IF go_html_cntrl IS INITIAL.

    CREATE OBJECT go_html_cntrl
      EXPORTING
        parent = go_topof_cont.

  ENDIF.

  CALL METHOD go_dyndoc_id->merge_document.
  go_dyndoc_id->html_control = go_html_cntrl.

  CALL METHOD go_dyndoc_id->display_document
    EXPORTING
      reuse_control = abap_true
      parent        = go_topof_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> E_INTERACTIVE
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar  USING    po_object  TYPE REF TO cl_alv_event_toolbar_set
                                   pv_interactive
                                   po_sender  TYPE REF TO cl_gui_alv_grid.

  " 툴바 버튼을 수정하는 이벤트

  DATA : lv_napr(32),
         lv_aprv(32),
         lv_tota(32).

  lv_napr = | 승인대기 : { gv_pocnt }건|.
  lv_aprv = | 승인 : { gv_poacnt }건|.
  lv_tota = | 전체 : { gv_pocnt + gv_poacnt }건|.

  CASE po_sender.
    WHEN go_top_grid.
      po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = 3 ) ).
    WHEN go_bottom_grid.
      po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar
        ( function = 'NAPR' icon = icon_led_yellow text = lv_napr )
        ( function = 'APRV' icon = icon_led_green  text = lv_aprv )
        ( function = 'TOTA' icon = icon_overview   text = lv_tota ) ).
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_btn_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> ES_ROW_NO
*&---------------------------------------------------------------------*
FORM handle_btn_click  USING ps_row_no TYPE lvc_s_roid
                             po_sender TYPE REF TO cl_gui_alv_grid.
  CASE po_sender.
    WHEN go_top_grid.

      " 오더 전화 버튼을 눌렀을 때 팝업창을 띄워 오더 전환을 하도록 한다
      " 벤더 선택, 오더 생성자 정보 입력, 오더 전환 완료

      " 선택된 행 정보 읽어오기
      READ TABLE gt_prbody INTO gs_sprbody INDEX ps_row_no-row_id.
      CASE gs_sprbody-estkz.
        WHEN 'A'. gv_estkz = 'MRP'.
        WHEN 'B'. gv_estkz = '직접 요청'.
        WHEN 'C'. gv_estkz = 'CBP'.
      ENDCASE.

      CASE gs_sprbody-pr_status.
        WHEN 'A'. gv_pr_status = '승인 대기'.
        WHEN 'B'. gv_pr_status = '승인'.
        WHEN 'C'. gv_pr_status = '오더 전환'.
      ENDCASE.

      PERFORM set_pop_loop_data.

      " 사원정보입력칸 초기화
      CLEAR : gv_creator, gv_creator_name, gv_dptcode, gt_pir.

      " 팝업창 호출
      CALL SCREEN 110 STARTING AT 10 10.

      IF go_pop_prid_cont IS BOUND.

        " ALV Free
        CALL METHOD : go_pop_prid_grid->free, go_pop_prid_cont->free,
                      go_pop_vs_grid->free, go_pop_vs_cont->free.

        FREE : go_pop_prid_grid, go_pop_prid_cont,
               go_pop_vs_grid, go_pop_vs_cont.

      ENDIF.

    WHEN go_pop_vs_grid.
      PERFORM pop_vs_btn_click USING ps_row_no.
    WHEN go_bottom_grid.
      " 구매오더 결재
      PERFORM approve_po USING ps_row_no.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_pop_prid_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_pop_prid_screen .
  " 구매오더전환 팝업창에서 구매요청 아이템을 보여주는 ALV 화면 띄우기

  IF go_pop_prid_cont IS INITIAL.

    PERFORM set_ppfcat.
    PERFORM set_pplayo.
    PERFORM create_pp_object.
    SET HANDLER : lcl_event_handler=>hotspot_click FOR go_pop_prid_grid.
    CALL METHOD go_pop_prid_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_ppvari
        i_save               = 'A'
        i_default            = abap_true
        is_layout            = gs_pplayo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_sprit
        it_fieldcatalog      = gt_ppfcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_ppfcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_ppfcat .

  gt_ppfcat = VALUE #(
    ( key = 'X' fieldname = 'MATID'        ref_table = 'ZC103MMT0008' hotspot = abap_true )
              ( fieldname = 'VS_COMPLETE'  coltext = '선정상태' just = 'C' emphasize = abap_true )
              ( fieldname = 'VS_STATUS'    coltext = '선정상태' just = 'C' )
              ( fieldname = 'MATNAME'      ref_table = 'ZC103MMT0001' )
              ( fieldname = 'QUANTITY'     ref_table = 'ZC103MMT0008' qfieldname = 'UNIT' coltext = '수량' )
              ( fieldname = 'UNIT'         ref_table = 'ZC103MMT0008' coltext = '단위')
              ( fieldname = 'PRICE'        ref_table = 'ZC103MMT0008' cfieldname = 'CURRENCY' coltext = '금액' do_sum = abap_true )
              ( fieldname = 'CURRENCY'     ref_table = 'ZC103MMT0008' coltext = '통화' )
              ( fieldname = 'BPID'         coltext = '회사코드' emphasize = abap_true )
              ( fieldname = 'BPNAME'       coltext = '회사명' )
  ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pplayo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pplayo .

  gs_pplayo = VALUE #( cwidth_opt = 'A' zebra = abap_true stylefname = 'cell_tab' totals_bef = abap_true
                       grid_title = '구매요청아이템' ).
  gs_ppvari = VALUE #( report = sy-repid handle = 'ALV2' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_pp_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_pp_object .

  CREATE OBJECT go_pop_prid_cont
    EXPORTING
      container_name = 'POP_PRIT_CONT'.

  CREATE OBJECT go_pop_prid_grid
    EXPORTING
      i_parent = go_pop_prid_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_pir_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pir_data .

  DATA : lv_tabix TYPE sy-tabix,
         lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v.

  DATA : lt_vscore TYPE TABLE OF zc103mmt0019,
         ls_vscore TYPE zc103mmt0019.

  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE lt_vscore
    FROM zc103mmt0019.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_MATGRP'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  SORT gt_pir BY pscore DESCENDING.

  " 구매정보데이터 세팅 (자재이름, 자재타입)
  LOOP AT gt_pir INTO gs_pir.

    CLEAR : gs_mara.
    lv_tabix = sy-tabix.

    " 자재이름, 자재타입 세팅
    READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_pir-matid.
    gs_pir-matname = gs_mara-matname.
    gs_pir-matgrp  = gs_mara-matgrp.
    gs_pir-unit    = gs_mara-base_unit.

    " 추후 값 수정 필요
    CLEAR ls_vscore.
    READ TABLE lt_vscore INTO ls_vscore WITH KEY bpid = gs_pir-bpid.
    gs_pir = VALUE #( BASE gs_pir
                      pscore = ls_vscore-price_score
                      qscore = ls_vscore-quality_score
                      rscore = ls_vscore-receive_score ).

    " 자재그룹이름
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_pir-matgrp.
    gs_pir-mgrpnm = ls_dd07v-ddtext.

    " 스타일세팅
    gs_pir-celltab = VALUE #( ( fieldname = 'SELECT' style = cl_gui_alv_grid=>mc_style_button ) ).
    gs_pir-select = '선택'.

    gs_pir-currency = 'KRW'.

    MODIFY gt_pir FROM gs_pir INDEX lv_tabix TRANSPORTING matname pscore qscore rscore mgrpnm
                                                          celltab select unit currency.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_pop_vs_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_pop_vs_screen .

  IF go_pop_vs_cont IS INITIAL.

    PERFORM set_pvfcat.
    PERFORM set_pvlayo.
    PERFORM create_pvobject.
    SET HANDLER : lcl_event_handler=>handle_btn_click FOR go_pop_vs_grid.
    CALL METHOD go_pop_vs_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_pvvari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_pvlayo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_pir
        it_fieldcatalog      = gt_pvfcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pvfcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pvfcat .

  gt_pvfcat = VALUE #(
    ( key = 'X' fieldname = 'PIRID'     ref_table = 'ZC103MMT0005' )
              ( fieldname = 'SELECT'    coltext = '선택'  col_pos = 2 )
              ( fieldname = 'MATID'     ref_table = 'ZC103MMT0005' just = 'C' emphasize = abap_true )
              ( fieldname = 'MATNAME'   ref_table = 'ZC103MMT0001' just = 'C' )
              ( fieldname = 'MGRPNM'    just = 'C'  coltext = '자재그룹' )
              ( fieldname = 'BPID'      ref_table = 'ZC103MMT0005' just = 'C' emphasize = abap_true )
              ( fieldname = 'BPNAME'    coltext = '납품처명' just = 'C' )
              ( fieldname = 'LIFAB'     ref_table = 'ZC103MMT0005' just = 'C' coltext = '납품시작일' )
              ( fieldname = 'LIFBI'     ref_table = 'ZC103MMT0005' just = 'C' coltext = '납품종료일' )
              ( fieldname = 'LEADTIME'  ref_table = 'ZC103MMT0005' just = 'C' )
              ( fieldname = 'UNIT'      ref_table = 'ZC103MMT0005' just = 'C' coltext = '단위' )
              ( fieldname = 'PRICE'     ref_table = 'ZC103MMT0005' just = 'C' coltext = '가격' cfieldname = 'CURRENCY' emphasize = abap_true )
              ( fieldname = 'CURRENCY'  ref_table = 'ZC103MMT0005' just = 'C' coltext = '통화' )
              ( fieldname = 'QSCORE'    coltext = '품질점수' just = 'C' )
              ( fieldname = 'PSCORE'    coltext = '가격점수' just = 'C' )
              ( fieldname = 'RSCORE'    coltext = '납기점수' just = 'C' ) ).



ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pvlayo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pvlayo .

  gs_pvlayo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'B' stylefname = 'CELLTAB' grid_title = '벤더선택' ).
  gs_pvvari = VALUE #( report = sy-repid handle = 'PVALV1' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_pvobject
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_pvobject .

  CREATE OBJECT go_pop_vs_cont
    EXPORTING
      container_name = 'POP_VS_CONT'.

  CREATE OBJECT go_pop_vs_grid
    EXPORTING
      i_parent = go_pop_vs_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pop_loop_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pop_loop_data .
  " 팝업창에 출력할 ITAB 필터링
  DATA : lt_body  LIKE TABLE OF gs_prbody,
         lv_tabix TYPE sy-tabix.
  lt_body = VALUE #( FOR ls_body IN gt_prbody WHERE ( prid EQ gs_sprbody-prid ) ( ls_body ) ).
  gt_sprit = CORRESPONDING #( lt_body ).

  " 아이템 데이터 세팅
  CLEAR gs_sprit.
  LOOP AT gt_sprit INTO gs_sprit.

    lv_tabix = sy-tabix.
    gs_sprit = VALUE #( BASE gs_sprit vs_status = '선택대기' vs_complete = icon_led_yellow ).
    MODIFY gt_sprit FROM gs_sprit INDEX lv_tabix TRANSPORTING vs_status vs_complete.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW_ID
*&      --> E_COLUMN_ID
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_hotspot_click  USING    ps_row_id    TYPE lvc_s_row
                                    ps_column_id TYPE lvc_s_col
                                    po_sender    TYPE REF TO cl_gui_alv_grid.

  " 핫스팟 이벤트
  " 클릭한 자재 번호에 맞는 벤더 목록이 보임
  CLEAR gs_sprit.
  READ TABLE gt_sprit INTO gs_sprit INDEX ps_row_id-index.
  gt_pir = VALUE #( FOR ls_pir IN gt_pir_back WHERE ( matid EQ  gs_sprit-matid ) ( ls_pir ) ).
  PERFORM set_pir_data.

  PERFORM refresh_table USING go_pop_vs_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_vs_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_table USING po_alv_grid TYPE REF TO cl_gui_alv_grid.

  DATA ls_stable TYPE lvc_s_stbl.
  ls_stable = VALUE #( row = abap_true col = abap_true ).

  CALL METHOD po_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form pop_vs_btn_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PS_ROW_NO
*&---------------------------------------------------------------------*
FORM pop_vs_btn_click USING ps_row_no TYPE lvc_s_roid.
  " 벤더 선택시 좌측의 구매요청아이템 목록에 벤더번호와 이름을 세팅한다
  " 선택 완료된 자재의 상태를 바꾸고 테이블을 리프레시 한다.
  CLEAR : gs_pir, gs_vendor, gs_sprit.
  READ TABLE gt_pir INTO gs_pir INDEX ps_row_no-row_id.
  READ TABLE gt_vendor INTO gs_vendor WITH KEY bpid = gs_pir-bpid.

  DATA : lv_tabix TYPE sy-tabix.

  " 구매오더 아이템 itab에 회사코드, 회사명, 상태 세팅
  LOOP AT gt_sprit INTO gs_sprit.

    lv_tabix = sy-tabix.

    gs_sprit = VALUE #( BASE gs_sprit
                             bpid = gs_vendor-bpid
                             bpname = gs_vendor-name
                             vs_status = '선택완료'
                             vs_complete = icon_led_green ).

    MODIFY gt_sprit FROM gs_sprit INDEX lv_tabix TRANSPORTING bpid bpname vs_status vs_complete.

  ENDLOOP.

  PERFORM refresh_table USING go_pop_prid_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_order_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM make_order_data .
  "오더 데이터로 전환시키는 기능
*-- 전환 전 검증
  " 1. 벤더 전부 선택되었는지 확인
  " 2. 사원 번호가 입력 되었는지 확인

  DATA : lv_answer(1).
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = '오더로 전환'
    IMPORTING
      ev_answer = lv_answer.

  " 저장하는지 확인
  CHECK lv_answer EQ '1'.

  " 사원 번호 체크
  IF gv_creator IS INITIAL.
    MESSAGE i017 DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  " 사원번호가 올바른 사원번호인지 체크
  CLEAR : gs_emp.
  READ TABLE gt_emp INTO gs_emp WITH KEY empno = gv_creator.
  IF gs_emp IS INITIAL.

    MESSAGE i068 DISPLAY LIKE 'E' WITH TEXT-t05.
    RETURN.

  ENDIF.

  " 벤더 전부 선택 확인
  LOOP AT gt_sprit INTO gs_sprit.

    IF gs_sprit-bpid IS INITIAL.
      MESSAGE i032 DISPLAY LIKE 'E' WITH TEXT-t04.
      RETURN.
    ENDIF.

  ENDLOOP.

*-- 구매요청 데이터를 오더로 전환하고 DB에 저장한다
  PERFORM save_po_data.

*-- 구매요청 ALV와 구매오더 ALV를 갱신한다.
  PERFORM get_data. " 데이터를 다시 조회함
  PERFORM set_data. " 데이터 세팅
  PERFORM count_pr_po. " 개수 세기
  PERFORM register_event. " 해당 서브루틴에서 top of page 리프레시함
  PERFORM refresh_table USING go_top_grid.    " 구매요청 리프레시
  PERFORM refresh_table USING go_bottom_grid. " 구매오더 리프레시


  " 팝업창 종료
  LEAVE TO SCREEN 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form popup_to_confirm
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LV_ANSWER
*&---------------------------------------------------------------------*
FORM popup_to_confirm  CHANGING pv_answer.

  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar      = '구매오더전환'
      text_question = '구매오더로 전환하시겠습니까?'
      text_button_1 = '저장하기'
    IMPORTING
      answer        = pv_answer.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_po_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_po_data .
  " 구매오더 헤더, 아이템 정보를 DB에 저장한다.

  " 저장용 데이터
  DATA : ls_pohd        TYPE zc103mmt0011,
         lt_poit        TYPE TABLE OF zc103mmt0012,
         ls_poit        TYPE zc103mmt0012,
         lv_tabix       TYPE sy-tabix,
         lv_poid(8),  " 저장할 구매오더 번호
         ls_prhead      TYPE zc103mmt0007,
         lv_total_price TYPE i.

  CALL FUNCTION 'ZC103MMFG0002'
    EXPORTING
      iv_object = 'ZC103MM02'
      iv_prefix = 'PO'
      iv_nro_no = '01'
    IMPORTING
      ev_id     = lv_poid.

  " 총 금액 계산
  LOOP AT gt_sprit INTO DATA(ls_sprit).
    lv_total_price += ls_sprit-price.
  ENDLOOP.

  " 선택된 구매요청 헤더 데이터를 저장용 데이터에 담는다
  ls_pohd = CORRESPONDING #( gs_sprbody ).
  ls_pohd = VALUE #( BASE ls_pohd
                     poid = lv_poid
                     erdat = sy-datum create_date = sy-datum order_date = sy-datum "( 자재 주문 일자)
                     erzet = sy-uzeit create_time = sy-uzeit
                     ernam = sy-uname
                     total_price = lv_total_price
                     order_status = 'A'
                     receive_date = '' " 구매요청의 receive_date와는 다른 항목이므로 초기화 필요 (자재 수령 일자임)
                     creator = gv_creator
                     approval = '' approve_date = '' approve_time = '' ). " 초기화

  " 아이템 데이터를 저장용 데이터에 담는다
  LOOP AT gt_sprit INTO gs_sprit.

    CLEAR ls_poit.
    ls_poit = CORRESPONDING #( gs_sprit ).
    ls_poit = VALUE #( BASE ls_poit
                        poid = lv_poid
                        erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

    APPEND ls_poit TO lt_poit.

  ENDLOOP.

*-- 구매요청 데이터 가져와서 상태 변경 (승인 -> 오더 전환 완료)
  CLEAR : gs_prbody.
  READ TABLE gt_prbody INTO gs_prbody WITH KEY prid = ls_pohd-prid.
  ls_prhead = CORRESPONDING #( gs_prbody ).
  ls_prhead = VALUE #( BASE ls_prhead
                            erdat = gs_prbody-h_erdat erzet = gs_prbody-h_erzet
                            ernam = gs_prbody-h_ernam
                            aedat = sy-datum aezet = sy-uzeit aenam = sy-uname
                            pr_status = 'C' ).

  " 데이터 저장
  INSERT zc103mmt0011 FROM ls_pohd. " 헤더
  INSERT zc103mmt0012 FROM TABLE lt_poit.
  " 구매요청헤더 업데이트
  MODIFY zc103mmt0007 FROM ls_prhead.

  IF sy-subrc EQ 0.
    COMMIT WORK.
    MESSAGE s018 WITH TEXT-t06.
  ELSE.
    ROLLBACK WORK.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form approve_po
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM approve_po USING ps_row_no TYPE lvc_s_roid .
  " 구매오더를 결재한다
  " 결재자 정보 입력
  " 구매오더상태 변경
  " 구매요청상태 변경 (승인완료 -> 오더전환완료)
  DATA : lv_empno  TYPE zc103fit0011-empno,
         ls_header TYPE zc103mmt0011, " 수정할 구매오더 헤더
         ls_prhead TYPE zc103mmt0007.

  CALL FUNCTION 'ZC103PMFG0001'
    IMPORTING
      empno = lv_empno.

  " 결재 버튼을 누른 행의 데이터를 가져온다
  CLEAR : gs_pobody.
  READ TABLE gt_pobody INTO gs_pobody INDEX ps_row_no-row_id.
  ls_header = CORRESPONDING #( gs_pobody ).
  ls_header = VALUE #( BASE ls_header
                       approval = lv_empno
                       aedat = sy-datum aezet = sy-uzeit aenam = sy-uname
                       order_status = 'B'
                       approve_date = sy-datum approve_time = sy-uzeit ).

  " 업데이트
  MODIFY zc103mmt0011 FROM ls_header. " 구매오더 헤더 업데이트
  MODIFY zc103mmt0007 FROM ls_prhead. " 구매요청 헤더 업데이트
  IF sy-subrc EQ 0.
    COMMIT WORK.
    MESSAGE s009.
  ELSE.
    ROLLBACK WORK.
  ENDIF.

  " 하단 ALV 새로고침
  PERFORM get_po_data.
  PERFORM set_po_data.
  PERFORM count_pr_po.
  PERFORM refresh_table USING go_bottom_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_all_temp
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_all_temp .
  " 전체 요청, 오더 데이터를 삭제하는 임시 버튼

  " 구매요청테이블
  DELETE FROM zc103mmt0007.
  DELETE FROM zc103mmt0008.

  " 구매오더테이블
  DELETE FROM zc103mmt0011.
  DELETE FROM zc103mmt0012.

  " 초기화
  PERFORM get_data.
  PERFORM set_data.
  PERFORM refresh_table USING go_top_grid.
  PERFORM refresh_table USING go_bottom_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form count_pr_po
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM count_pr_po .
*-- 구매요청, 구매오더 수를 세는 서브루틴
  " 정렬해서 중복값을 제거하는 방식으로
  DATA : lt_prheader TYPE TABLE OF zc103mmt0007,
         lt_po       TYPE TABLE OF zc103mmt0011,
         lt_poheader TYPE TABLE OF zc103mmt0011,
         lt_poahead  TYPE TABLE OF zc103mmt0011.

  lt_prheader = CORRESPONDING #( gt_prbody ).
  SORT lt_prheader BY prid.
  DELETE ADJACENT DUPLICATES FROM lt_prheader COMPARING prid.

  gv_prcnt = lines( lt_prheader ).

  lt_po = CORRESPONDING #( gt_pobody ).
  lt_poahead  = VALUE #( FOR ls IN lt_po WHERE ( order_status NE 'A' ) ( ls ) ).
  SORT lt_poahead BY prid.
  DELETE ADJACENT DUPLICATES FROM lt_poahead COMPARING prid.
  gv_poacnt = lines( lt_poahead ).

  lt_poheader = VALUE #( FOR ls IN lt_po WHERE ( order_status EQ 'A' ) ( ls ) ).
  SORT lt_poheader BY prid.
  DELETE ADJACENT DUPLICATES FROM lt_poheader COMPARING prid.
  gv_pocnt = lines( lt_poheader ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_user_command  USING    pv_ucomm
                                   po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.
    WHEN go_bottom_grid.
      PERFORM bottom_user_command USING pv_ucomm.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form bottom_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PV_UCOMM
*&---------------------------------------------------------------------*
FORM bottom_user_command  USING    pv_ucomm.

  CLEAR gt_pobody.
  CASE pv_ucomm.
    WHEN 'NAPR'.
      gt_pobody = VALUE #( FOR ls IN gt_poback WHERE ( order_status EQ 'A' ) ( ls ) ).
      PERFORM set_po_data.
      PERFORM refresh_table USING go_bottom_grid.

    WHEN 'APRV'.
      gt_pobody = VALUE #( FOR ls IN gt_poback WHERE ( order_status NE 'A' ) ( ls ) ).
      PERFORM set_po_data.
      PERFORM refresh_table USING go_bottom_grid.

    WHEN 'TOTA'.
      gt_pobody = CORRESPONDING #( gt_poback ).
      PERFORM set_po_data.
      PERFORM refresh_table USING go_bottom_grid.

  ENDCASE.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
