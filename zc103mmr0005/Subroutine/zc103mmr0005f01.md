```abap
*&---------------------------------------------------------------------*
*& Include          ZC103MMR0005F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_init_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_data .



ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_main_data .

  DATA : lv_start TYPE dats,
         lv_end   TYPE dats,
         lv_cnt   TYPE i.

*  lv_start = pa_year && '0101'.
*  lv_end = pa_year && '1231'.
  lv_start = '20250101'.
  lv_end = '20251231'.

  " 자재문서 헤더 데이터 가져오기
  SELECT matdocid plnid strid belnr mrpid movement_type matdocdate
    INTO CORRESPONDING FIELDS OF TABLE gt_mdhd_back
    FROM zc103mmt0016
   WHERE matdocdate BETWEEN lv_start AND lv_end.

  " 자재문서 아이템 데이터 가져오기
  SELECT a~matdocid matid smdocid quantity unit
    INTO CORRESPONDING FIELDS OF TABLE gt_mdit_back
    FROM zc103mmt0018 AS a
   INNER JOIN zc103mmt0016 AS b
    ON a~matdocid EQ b~matdocid
   WHERE matdocdate BETWEEN lv_start AND lv_end.

  gt_mdhd = CORRESPONDING #( gt_mdhd_back ).

  lv_cnt = LINES( gt_mdhd ).
  MESSAGE s008 WITH lv_cnt.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_master_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_master_data .

  " material master
  SELECT matid matname matgrp is_serial
    INTO CORRESPONDING FIELDS OF TABLE gt_mara
    FROM zc103mmt0001.

  " vendor master
  SELECT bpid name
    INTO CORRESPONDING FIELDS OF TABLE gt_vend
    FROM zc103mmt0002.

  " plant master
  SELECT plnid name
    INTO CORRESPONDING FIELDS OF TABLE gt_plnt
    FROM zc103mmt0003.

  " stlo master
  SELECT strid name
    INTO CORRESPONDING FIELDS OF TABLE gt_stlo
    FROM zc103mmt0004.

  " emp master
  SELECT empno empname
    INTO CORRESPONDING FIELDS OF TABLE gt_emp
    FROM zc103fit0011.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_tree_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_tree_data .

  " 트리에 필요한 데이터 가져오기
  DATA : lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_MOVEMENT_TYPE'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  LOOP AT lt_dd07v INTO ls_dd07v.

    CLEAR gs_tr_move.
    gs_tr_move-movement_type = ls_dd07v-domvalue_l.
    gs_tr_move-movement_type_text = ls_dd07v-ddtext.
    APPEND gs_tr_move TO gt_tr_move.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_main_data .

  DATA : lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v,
         lv_tabix TYPE sy-tabix.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_MOVEMENT_TYPE'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  LOOP AT gt_mdhd INTO gs_mdhd.

    lv_tabix = sy-tabix.

    CLEAR : gs_plnt, gs_stlo.

    " plnt name 세팅
    READ TABLE gt_plnt INTO gs_plnt WITH KEY plnid = gs_mdhd-plnid.
    gs_mdhd-pname = gs_plnt-name.

    " stlo name 세팅
    READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_mdhd-strid.
    gs_mdhd-sname = gs_stlo-name.

    " movement type text 세팅
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_mdhd-movement_type.
    gs_mdhd-movement_type_text = ls_dd07v-ddtext.

    MODIFY gt_mdhd FROM gs_mdhd INDEX lv_tabix TRANSPORTING pname sname movement_type_text.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form init_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_screen .

  IF go_main_cont IS NOT BOUND.

    CLEAR : gt_fcat.
    PERFORM set_fcat_and_layo.
    PERFORM set_item_fcat_and_layo.
    PERFORM create_object.

    PERFORM register_tree_events.
    PERFORM build_node.

    SET HANDLER : lcl_event_handler=>handle_node_double_click FOR go_tree.

    CALL METHOD go_tree->add_nodes
      EXPORTING
        table_structure_name = 'MTREESNODE'
        node_table           = node_table.

    SET HANDLER : lcl_event_handler=>handle_hotspot_click FOR go_main_grid,
                  lcl_event_handler=>handle_hotspot_click FOR go_item_grid.

    CALL METHOD go_main_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_vari
        i_save          = 'A'
        i_default       = abap_true
        is_layout       = gs_layo
      CHANGING
        it_outtab       = gt_mdhd
        it_fieldcatalog = gt_fcat.

    CALL METHOD go_item_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_ivari
        i_save          = 'A'
        i_default       = abap_true
        is_layout       = gs_ilayo
      CHANGING
        it_outtab       = gt_mdit
        it_fieldcatalog = gt_ifcat.


  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_fcat_and_layo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_fcat_and_layo .

  gt_fcat = VALUE #(
    ( key = abap_true fieldname = 'MATDOCID' ref_table = 'ZC103MMT0016' just = 'C' hotspot = abap_true )
                    ( fieldname = 'PLNID'    ref_table = 'ZC103MMT0003' just = 'C' emphasize = abap_true )
                    ( fieldname = 'PNAME'    coltext = '플랜트명'  )
                    ( fieldname = 'STRID'    ref_table = 'ZC103MMT0004' just = 'C' emphasize = abap_true )
                    ( fieldname = 'SNAME'    coltext = '플랜트명'  )
                    ( fieldname = 'BELNR'    coltext = '전표번호' just = 'C' emphasize = abap_true )
                    ( fieldname = 'MOVEMENT_TYPE' coltext = '이동유형번호' just = 'C' )
                    ( fieldname = 'MOVEMENT_TYPE_TEXT' coltext = '이동유형' emphasize = abap_true )
                    ( fieldname = 'MATDOCDATE' ref_table = 'ZC103MMT0016' coltext = '생성일자' just = 'C' ) ).

  gs_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'D' grid_title = '자재문서헤더 : 전체조회' ).

  gs_vari = VALUE #( report = sy-repid handle = 'ALV1' ).


ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_item_fcat_and_layo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_item_fcat_and_layo .

  gt_ifcat = VALUE #(
    ( key = abap_true fieldname = 'MATDOCID' ref_table = 'ZC103MMT0016' just = 'C' hotspot = abap_true )
                    ( fieldname = 'MATID'    ref_table = 'ZC103MMT0001' just = 'C' emphasize = abap_true )
                    ( fieldname = 'MATNAME'  coltext = '자재명' )
                    ( fieldname = 'MATGRP_TEXT' coltext = '자재그룹' )
                    ( fieldname = 'SMDOCID'  ref_table = 'ZC103MMT0018' just = 'C' emphasize = abap_true hotspot = abap_true )
                    ( fieldname = 'QUANTITY' ref_table = 'ZC103MMT0018' emphasize = abap_true qfieldname = 'UNIT'
                      coltext = '자재수량' )
                    ( fieldname = 'UNIT'     ref_table = 'ZC103MMT0018' coltext = '자재단위' just = 'C'  )
                    ( fieldname = 'IS_SERIAL' coltext = '시리얼자재여부' just = 'C' ) ).

  gs_ilayo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'D' grid_title = '자재문서아이템 : 미선택' ).
  gs_ivari = VALUE #( report = sy-repid handle = 'ALV2' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_main_cont
    EXPORTING
      container_name = 'MAIN_CONT'.

  CREATE OBJECT go_split_cont
    EXPORTING
      parent  = go_main_cont
      rows    = 1
      columns = 2.

  go_split_cont->get_container(
    EXPORTING row = 1 column = 1
    RECEIVING container = go_left_cont ).

  go_split_cont->get_container(
    EXPORTING row = 1 column = 2
    RECEIVING container = go_right_cont ).

  go_split_cont->set_column_width( EXPORTING id = 1 width = 30 ).

  CREATE OBJECT go_rsplit_cont
    EXPORTING
      parent  = go_right_cont
      rows    = 1
      columns = 2.

  go_rsplit_cont->get_container(
    EXPORTING row = 1 column = 1
    RECEIVING container = go_right_top_cont ).
  go_rsplit_cont->get_container(
    EXPORTING row = 1 column = 2
    RECEIVING container = go_right_bottom_cont ).

  CREATE OBJECT go_tree
    EXPORTING
      parent              = go_left_cont
      node_selection_mode = cl_gui_simple_tree=>node_sel_mode_single.

  CREATE OBJECT go_main_grid
    EXPORTING
      i_parent = go_right_top_cont.

  CREATE OBJECT go_item_grid
    EXPORTING
      i_parent = go_right_bottom_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_tree_events
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_tree_events .

  " 노드 이벤트
  events = VALUE #( ( eventid = cl_gui_simple_tree=>eventid_node_double_click
                      appl_event = abap_true ) ).

  CALL METHOD go_tree->set_registered_events
    EXPORTING
      events = events.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form build_node
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM build_node .

  " 데이터 만들기
  DATA : node        TYPE mtreesnode,
         lv_text(32).

  " 전체조회칸
  node = VALUE #( node_key = 'ROOT' text = TEXT-t02 isfolder = abap_true ).
  APPEND node TO node_table.
  CLEAR node.

  LOOP AT gt_tr_move INTO gs_tr_move.

    lv_text = |{ gs_tr_move-movement_type_text } ( { gs_tr_move-movement_type } )|.

    node = VALUE #( node_key = gs_tr_move-movement_type text = lv_text isfolder = abap_false
                    relatkey = 'ROOT' ).
    APPEND node TO node_table.
    CLEAR node.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_node_double_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> NODE_KEY
*&---------------------------------------------------------------------*
FORM handle_node_double_click  USING    pv_node_key.

  " 노드 더블클릭시 우측 alv에 보여줄 값 세팅
  DATA : lv_text(32),
         lv_cnt TYPE i.
  lv_text = '자재문서헤더 : '.

  CLEAR : gt_mdhd.

  CASE pv_node_key.
    WHEN 'ROOT'.  " 전체조회
      gt_mdhd = CORRESPONDING #( gt_mdhd_back ).
      lv_text = lv_text && '전체조회'.
    WHEN OTHERS.  " 이동유형별 조회
      READ TABLE gt_tr_move INTO gs_tr_move WITH KEY movement_type = pv_node_key.

      gt_mdhd = VALUE #( FOR ls_mdhd IN gt_mdhd_back WHERE
                           ( movement_type = pv_node_key ) ( ls_mdhd ) ).
      lv_text = lv_text && gs_tr_move-movement_type_text.

  ENDCASE.

  lv_cnt = LINES( gt_mdhd ).
  MESSAGE s008 WITH lv_cnt.

  PERFORM set_main_data.
  PERFORM refresh_layout USING go_main_grid lv_text.
  PERFORM refresh_grid   USING go_main_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GO_MAIN_GRID
*&---------------------------------------------------------------------*
FORM refresh_layout  USING    po_main_grid TYPE REF TO cl_gui_alv_grid
                              pv_text.

  gs_layo = VALUE #( BASE gs_layo grid_title = pv_text ).
  po_main_grid->set_frontend_layout( EXPORTING is_layout = gs_layo ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_grid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GO_MAIN_GRID
*&---------------------------------------------------------------------*
FORM refresh_grid  USING    po_main_grid TYPE REF TO cl_gui_alv_grid.

  DATA : ls_stbl TYPE lvc_s_stbl.
  ls_stbl = VALUE #( row = abap_true col = abap_true ).

  po_main_grid->refresh_table_display( EXPORTING is_stable = ls_stbl ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW_ID
*&      --> E_COLUMN_ID
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_hotspot_click  USING    ps_row_id TYPE lvc_s_row
                                    ps_column_id TYPE lvc_s_col
                                    po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.
    WHEN go_main_grid.
      PERFORM main_hotspot_click USING ps_row_id ps_column_id.
    WHEN go_item_grid.
      PERFORM item_hotspot_click USING ps_row_id ps_column_id.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form main_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PS_ROW_ID
*&      --> PS_COLUMN_ID
*&---------------------------------------------------------------------*
FORM main_hotspot_click  USING    ps_row_id TYPE lvc_s_row
                                  ps_column_id TYPE lvc_s_col.

  DATA : lv_text(32) VALUE '자재문서아이템 : ',
         lv_cnt TYPE i.

  " 자재문서 번호를 클릭하면 하단 alv에 보여질 데이터를 파싱하고 alv 타이틀 수정
  CASE ps_column_id-fieldname.
    WHEN 'MATDOCID'.
      CLEAR : gs_mdhd.
      READ TABLE gt_mdhd INTO gs_mdhd INDEX ps_row_id-index.
      gt_mdit = VALUE #( FOR ls_mdit IN gt_mdit_back
                           WHERE ( matdocid = gs_mdhd-matdocid ) ( ls_mdit ) ).
      lv_text = lv_text && |{ gs_mdhd-matdocid }|.
      PERFORM set_matdoc_item.
      PERFORM refresh_layout USING go_item_grid lv_text.
      PERFORM refresh_grid   USING go_item_grid.

      lv_cnt = LINES( gt_mdit ).
      MESSAGE s008 WITH lv_cnt.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form item_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PS_ROW_ID
*&      --> PS_COLUMN_ID
*&---------------------------------------------------------------------*
FORM item_hotspot_click  USING    ps_row_id TYPE lvc_s_row
                                  ps_column_id TYPE lvc_s_col.

  CASE ps_column_id-fieldname.
    WHEN 'SMDOCID'.
      CLEAR : gs_mdit.
      READ TABLE gt_mdit INTO gs_mdit INDEX ps_row_id-index.
      CALL FUNCTION 'ZC1FM03MM0003'
        EXPORTING
          iv_smdocid = gs_mdit-smdocid
          iv_select = abap_false.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_matdoc_item
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_matdoc_item .

  DATA : lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v,
         lv_tabix TYPE sy-tabix.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_MATGRP'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  CLEAR : gs_mdit.
  LOOP AT gt_mdit INTO gs_mdit.

    lv_tabix = sy-tabix.

    READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_mdit-matid.
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_mara-matgrp.

    gs_mdit-matname = gs_mara-matname.
    gs_mdit-matgrp = gs_mara-matgrp.
    gs_mdit-matgrp_text = ls_dd07v-ddtext.
    gs_mdit-is_serial = gs_mara-is_serial.

    MODIFY gt_mdit FROM gs_mdit INDEX lv_tabix TRANSPORTING matname matgrp matgrp_text is_serial.

  ENDLOOP.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
