```abap
*----------------------------------------------------------------------*
***INCLUDE LZC1FG03MM0001F02.
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form movement_101
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> IV_PLNID
*&      --> IV_STRID
*&      --> IV_MATID
*&      --> IV_QUANTITY
*&---------------------------------------------------------------------*
FORM movement_101  USING    pv_grid pv_total pv_tax pv_plnid pv_strid
                            pt_mat TYPE zc103mmtt0007
                            pv_bpid
                   CHANGING ps_header TYPE zc103mms0006
                            pt_item   TYPE zc103mmtt0006
                            pv_result pv_belnr.
  " 자재를 입고시키는 펑션모듈
  " 로직 순서
  " 1. 자재문서생성
  " 2. 자재수량정보 업데이트
  " 3. 시리얼-저장위치 생성
  " 4. 시리얼-자재문서 생성
  " 5. AP 전표 생성
  " 6. DB에 반영

  " 자재문서
  DATA : ls_matdochd TYPE zc103mmt0016, " 자재문서 헤더
         lt_matdocit TYPE TABLE OF zc103mmt0018, " 자재문서 아이템
         ls_matdocit TYPE zc103mmt0018, " 자재문서 아이템
         lv_matdocid TYPE zc103mmt0016-matdocid. " 자재문서 번호

  " 시리얼 (17번)
  DATA : lv_smdocid TYPE ZC103mmt0017-smdocid,  " 시리얼-자재번호
         lt_smdoc   TYPE TABLE OF zc103mmt0017, " 시리얼-자재번호 테이블
         ls_smdoc   TYPE zc103mmt0017.          " 시리얼-자재번호 스트럭쳐

  " 시리얼 (20번)
  DATA : lv_serial   TYPE zc103mmt0020-serial, " 시리얼 번호
         lt_serialsl TYPE TABLE OF zc103mmt0020, " 시리얼-저장위치 테이블
         ls_serialsl TYPE zc103mmt0020.          " 시리얼-저장위치 스토리지 로케이션

  " 제고수량 (06번)
  DATA : lt_matq  TYPE TABLE OF zc103mmt0006, " 재고수량 테이블
         ls_matq  TYPE zc103mmt0006,          " 재고수량 워크에이리어
         lv_stock TYPE zc103mmt0006-stoid.    " 재고수량 번호

  " SUBRC
  DATA : lv_subrc TYPE sy-subrc.

  " 회계전표 생성 메시지
  DATA : lv_belnr TYPE belnr_d,
         lv_msg   TYPE char100,
         lv_kind(10).

**********************************************************************
* 번호 채번
**********************************************************************

  " 1. 자재문서번호 채번
  CALL FUNCTION 'ZC103MMFG0002'
    EXPORTING
      iv_nro_no = '01'
      iv_object = 'ZC103MM05'
      iv_prefix = 'MD'
    IMPORTING
      ev_id     = lv_matdocid.

**********************************************************************
* 자재문서 생성
**********************************************************************

  " 1. 자재문서헤더값 세팅
  ls_matdochd = VALUE #( matdocid = lv_matdocid
                         plnid = pv_plnid strid = pv_strid belnr = pv_belnr
                         movement_type = '101' matdocdate = sy-datum
                         erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

  " 2. 자재문서아이템 값 세팅
  LOOP AT pt_mat INTO DATA(ls_mat).

    CLEAR : ls_matdocit, lv_smdocid.

    " 해당 자재가 시리얼로 관리되는 자재인지 확인하여 17번 테이블 번호 채번
    READ TABLE gt_mara INTO DATA(ls_mara) WITH KEY matid = ls_mat-matid.
    IF ls_mara-is_serial EQ 'Y'.

      CALL FUNCTION 'ZC103MMFG0002'
        EXPORTING
          iv_nro_no = '01'
          iv_object = 'ZC103MM07'
          iv_prefix = 'SM'
        IMPORTING
          ev_id     = lv_smdocid.

    ENDIF.

    ls_matdocit = VALUE #( matdocid = lv_matdocid
                           matid = ls_mat-matid smdocid = lv_smdocid
                           quantity = ls_mat-quantity unit = ls_mat-unit
                           erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

    APPEND ls_matdocit TO lt_matdocit.

    " 3. 재고수량 업데이트 (입고)
    " 수량정보 읽어오기
    CLEAR ls_matq.
    READ TABLE gt_matq INTO ls_matq WITH KEY plnid = ls_matdochd-plnid
                                             strid = ls_matdochd-strid
                                             matid = ls_matdocit-matid.

    " 재고수량 정보가 초기값인 경우
    " 새로 행을 채번해서 데이터 생성
    IF ls_matq IS INITIAL.

      " 재고수량 번호 채번
      CLEAR lv_stock.
      CALL FUNCTION 'ZC103MMFG0002'
        EXPORTING
          iv_nro_no = '01'
          iv_object = 'ZC103MM08'
          iv_prefix = 'SO'
        IMPORTING
          ev_id     = lv_stock.


      ls_matq = VALUE #( stoid = lv_stock matid = ls_matdocit-matid
                         plnid = ls_matdochd-plnid strid = ls_matdochd-strid
                         available_stock = ls_matdocit-quantity unit = ls_matdocit-unit
                         erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

    ELSE.

      ls_matq = VALUE #( BASE ls_matq
                         available_stock = ls_matq-available_stock + ls_matdocit-quantity
                         aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).

    ENDIF.

    APPEND ls_matq TO lt_matq.

  ENDLOOP.

  " 3. DB에 저장

***********************************************************************
** 시리얼 문서 생성
***********************************************************************
*
*  " 시리얼 문서인 경우에만 17번 테이블, 20번 테이블 값 생성
*  " smdocid 가 있으면 시리얼로 관리되는 자재임
  LOOP AT lt_matdocit INTO ls_matdocit WHERE smdocid IS NOT INITIAL.

    DO ls_matdocit-quantity TIMES.

      " 0. 시리얼 번호 채번
      CLEAR : lv_serial, ls_mara.
      CALL FUNCTION 'ZC103MMFG0002'
        EXPORTING
          iv_nro_no = '01'
          iv_object = 'ZC103MM06'
          iv_prefix = 'SR'
        IMPORTING
          ev_id     = lv_serial.

      " 자재명
      READ TABLE gt_mara INTO ls_mara WITH KEY matid = ls_matdocit-matid.

      " 1. 시리얼-저장위치 문서 생성
      ls_serialsl = VALUE #( serial = lv_serial
                             plnid = ls_matdochd-plnid strid = ls_matdochd-strid " 헤더에서 저장위치정보 설정
                             matid = ls_matdocit-matid
                             status = 'A' " 미사용상태로 저장
                             matdocid = lv_matdocid
                             matname = ls_mara-matname
                             erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

      APPEND ls_serialsl TO lt_serialsl.

      " 2. 시리얼-자재문서 데이터 생성
      ls_smdoc = VALUE #( smdocid = ls_matdocit-smdocid
                          serial  = lv_serial
                          erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

      APPEND ls_smdoc TO lt_smdoc.

    ENDDO.

  ENDLOOP.

**********************************************************************
* AP 전표 생성
**********************************************************************

DATA(lv_matid) = pt_mat[ 1 ]-matid.

CLEAR ls_mara.
READ TABLE gt_mara INTO ls_mara WITH KEY matid = lv_matid.

  CASE ls_mara-matgrp.
    WHEN 'BA' OR 'BD'.
      lv_kind = 'INV'.
    WHEN 'BB'.
      lv_kind = 'MER'.
    WHEN OTHERS.
      lv_kind = 'MAT'.
  ENDCASE.

  CALL FUNCTION 'ZC1FM03FI0004'
    EXPORTING
      iv_grid = pv_grid
      iv_gjahr = sy-datum(4)
      iv_bpid = pv_bpid
      iv_status = 'A'
      iv_total = pv_total
      iv_tax = pv_tax
      iv_waers = 'KRW'
      iv_uscode = 'ADMIN012'
      iv_kind = lv_kind
    IMPORTING
      ev_belnr = lv_belnr
      ev_message = lv_msg.

  ls_matdochd-belnr = lv_belnr.
  pv_belnr = lv_belnr.

**********************************************************************
* DB 저장
**********************************************************************


  MODIFY zc103mmt0016 FROM ls_matdochd. " 자재문서 헤더 저장
  lv_subrc += sy-subrc.
  MODIFY zc103mmt0018 FROM TABLE lt_matdocit. " 자재문서 아이템 저장
  lv_subrc += sy-subrc.
  MODIFY zc103mmt0006 FROM TABLE lt_matq. " 재고수량 업데이트
  lv_subrc += sy-subrc.

  IF lt_smdoc IS NOT INITIAL.
    MODIFY zc103mmt0017 FROM TABLE lt_smdoc. " 시리얼-자재문서 데이터 저장
    lv_subrc += sy-subrc.
  ENDIF.

  IF lt_serialsl IS NOT  INITIAL.
    MODIFY zc103mmt0020 FROM TABLE lt_serialsl. " 시리얼-저장위치 데이터 저장
    lv_subrc += sy-subrc.
  ENDIF.

  IF lv_subrc EQ 0.
    COMMIT WORK.
    pv_result = 0.
  ELSE.
    ROLLBACK WORK.
    pv_result = 1.
  ENDIF.

  ps_header = CORRESPONDING #( ls_matdochd ).
  pt_item   = CORRESPONDING #( lt_matdocit ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form movement_261
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> IV_PLNID
*&      --> IV_STRID
*&      --> IV_BELNR
*&      --> IT_MAT
*&      <-- ES_HEADER
*&      <-- ET_ITEM
*&      <-- EV_RESULT
*&---------------------------------------------------------------------*
FORM movement_261  USING    pv_plnid pv_strid
                            pt_mat    TYPE zc103mmtt0007
                            pt_serial TYPE zc103mmtt0008
                            pv_movementtype
                   CHANGING ps_header TYPE zc103mms0006
                            pt_item   TYPE zc103mmtt0006
                            pv_result pv_message.

  " 정비출고하는 서브루틴
  " 로직
  " 1. 출고 자재문서 생성
  "    - 생성시 재고수량 테이블에서 재고가 있는지 확인 (없으면 ev_result = 3 하고 종료, 16번 18번)
  " 2. 재고 수량 테이블에서 마이너스 (06번)
  " 3. 시리얼 자재라면 시리얼 테이블 업데이트
  "    - 시리얼-자재문서 테이블 업데이트 (17번)
  "    - 시리얼-저장위치 테이블에서 상태값 변경 (20번)

**********************************************************************
* 데이터 선언
**********************************************************************

  TABLES : ZC103MMt0020.

  " 자재문서
  DATA : ls_matdochd TYPE zc103mmt0016, " 자재문서 헤더
         lt_matdocit TYPE TABLE OF zc103mmt0018, " 자재문서 아이템
         ls_matdocit TYPE zc103mmt0018, " 자재문서 아이템
         lv_matdocid TYPE zc103mmt0016-matdocid. " 자재문서 번호

  " 시리얼 (17번)
  DATA : lv_smdocid TYPE ZC103mmt0017-smdocid,  " 시리얼-자재번호
         lt_smdoc   TYPE TABLE OF zc103mmt0017, " 시리얼-자재번호 테이블
         ls_smdoc   TYPE zc103mmt0017.          " 시리얼-자재번호 스트럭쳐

  " 시리얼 (20번)
  DATA : lv_serial     TYPE zc103mmt0020-serial, " 시리얼 번호
         lt_serialsl   TYPE TABLE OF zc103mmt0020, " 시리얼-저장위치 테이블
         ls_serialsl   TYPE zc103mmt0020,          " 시리얼-저장위치 스토리지 로케이션
         lt_serial     TYPE TABLE OF zc103mmt0020,
         ls_serials    TYPE zc103mmt0020,
         lt_serial_cnt TYPE TABLE OF zc103mmt0020.

  " 제고수량 (06번)
  DATA : lt_matq  TYPE TABLE OF zc103mmt0006, " 재고수량 테이블
         ls_matq  TYPE zc103mmt0006,          " 재고수량 워크에이리어
         lv_stock TYPE zc103mmt0006-stoid.    " 재고수량 번호

  " COMMON DATA
  DATA : lv_subrc TYPE sy-subrc,
         lv_tabix TYPE sy-tabix.

  DATA : lr_serial LIKE RANGE OF ZC103MMt0020-serial.

**********************************************************************
* 유효성검사 ( 저장위치에 재고 있는지 확인 )
**********************************************************************
  " 시리얼 데이터 가져오기
  " 시리얼 번호를 range 변수로 저장한다
  LOOP AT pt_serial INTO DATA(ls_serial).

    lr_serial = VALUE #( BASE lr_serial
                              ( low = ls_serial-serial sign = 'I' option = 'EQ' ) ).

  ENDLOOP.

  SELECT *
    FROM zc103mmt0020
    INTO CORRESPONDING FIELDS OF TABLE lt_serial
   WHERE serial IN lr_serial.

  " 요청 자재가 전부 있는지 확인 후 있으면 다음 로직을 실행한다
  LOOP AT pt_mat INTO DATA(ls_mat).

    READ TABLE gt_matq INTO ls_matq WITH KEY matid = ls_mat-matid
                                             plnid = pv_plnid
                                             strid = pv_strid.

    " 자재가 없거나 필요 자재보다 적은 경우
    " 오류를 리턴하고 펑션모듈 종료함
    IF ( ls_matq IS INITIAL ) OR
       ( ls_matq-available_stock < ls_mat-quantity ).

      pv_result = 3.
      pv_message = TEXT-e01.

      RETURN.

    ENDIF.

  ENDLOOP.

  " 시리얼 개수 확인


**********************************************************************
* 자재문서 번호 채번
**********************************************************************

  " 1. 자재문서번호 채번
  CALL FUNCTION 'ZC103MMFG0002'
    EXPORTING
      iv_nro_no = '01'
      iv_object = 'ZC103MM05'
      iv_prefix = 'MD'
    IMPORTING
      ev_id     = lv_matdocid.

**********************************************************************
* 자재문서 생성
**********************************************************************
  " 헤더 값 세팅
  ls_matdochd = VALUE #( matdocid = lv_matdocid
                       plnid = pv_plnid strid = pv_strid
                       movement_type = pv_movementtype matdocdate = sy-datum
                       erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

  " 아이템 값 세팅
  " 2. 자재문서아이템 값 세팅
  CLEAR ls_mat.
  LOOP AT pt_mat INTO ls_mat.

    CLEAR : ls_matdocit, lv_smdocid.

    " 해당 자재가 시리얼로 관리되는 자재인지 확인하여 17번 테이블 번호 채번
    READ TABLE gt_mara INTO DATA(ls_mara) WITH KEY matid = ls_mat-matid.
    IF ls_mara-is_serial EQ 'Y'.

      CALL FUNCTION 'ZC103MMFG0002'
        EXPORTING
          iv_nro_no = '01'
          iv_object = 'ZC103MM07'
          iv_prefix = 'SM'
        IMPORTING
          ev_id     = lv_smdocid.

    ENDIF.

    ls_matdocit = VALUE #( matdocid = lv_matdocid
                           matid = ls_mat-matid smdocid = lv_smdocid
                           quantity = ls_mat-quantity unit = ls_mat-unit
                           erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

    APPEND ls_matdocit TO lt_matdocit.

    " 3. 재고수량 업데이트 (입고)
    " 수량정보 읽어오기
    CLEAR ls_matq.
    READ TABLE gt_matq INTO ls_matq WITH KEY plnid = ls_matdochd-plnid
                                             strid = ls_matdochd-strid
                                             matid = ls_matdocit-matid.


    " 재고 수량에서 빼기
    ls_matq = VALUE #( BASE ls_matq
                            available_stock = ls_matq-available_stock - ls_matdocit-quantity
                            aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).

    APPEND ls_matq TO lt_matq.

  ENDLOOP.

***********************************************************************
** 시리얼 문서 생성 및 업데이트
***********************************************************************
*
*  " 시리얼 문서인 경우에만 17번 테이블, 20번 테이블 값 생성
*  " smdocid 가 있으면 시리얼로 관리되는 자재임
  " 재구현 필요
  " 시리얼-자재문서는 생성 필요함
  " 시리얼 번호는 기존 20번 테이블에 있는 데이터를 가지고 와야한다
  LOOP AT lt_matdocit INTO ls_matdocit WHERE smdocid IS NOT INITIAL.

    lt_serialsl = VALUE #( FOR ls_tempserial IN lt_serial
                           WHERE ( matid EQ ls_matdocit-matid ) ( ls_tempserial ) ).

    LOOP AT lt_serialsl INTO ls_serialsl.

      ls_smdoc = VALUE #( smdocid = ls_matdocit-smdocid serial = ls_serialsl-serial
                          erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).
      APPEND ls_smdoc TO lt_smdoc.

    ENDLOOP.

  ENDLOOP.

  LOOP AT pt_serial INTO DATA(ls_serialt).

    lv_tabix = sy-tabix.
    READ TABLE lt_serial INTO DATA(ls_tmpserial) WITH KEY serial = ls_serialt-serial.
    ls_tmpserial-status = 'B'.
    MODIFY lt_serial FROM ls_tmpserial INDEX lv_tabix TRANSPORTING status.

  ENDLOOP.

**********************************************************************
* DB에 반영
**********************************************************************

  " 자재문서 헤더
  MODIFY zc103mmt0016 FROM ls_matdochd.
  lv_subrc += sy-subrc.
  " 자재문서 아이템
  MODIFY zc103mmt0018 FROM TABLE lt_matdocit.
  lv_subrc += sy-subrc.
  " 재고수량
  MODIFY zc103mmt0006 FROM TABLE lt_matq.
  lv_subrc += sy-subrc.
  " 시리얼-자재문서
  MODIFY zc103mmt0017 FROM TABLE lt_smdoc.
  lv_subrc += sy-subrc.
  " 시리얼-저장위치
  MODIFY zc103mmt0020 FROM TABLE lt_serial.
  lv_subrc += sy-subrc.

  IF lv_subrc EQ 0.
    COMMIT WORK.
    pv_result = 0.
  ELSE.
    ROLLBACK WORK.
    pv_result = 1.
    pv_message = TEXT-e03.
  ENDIF.

  ps_header = CORRESPONDING #( ls_matdochd ).
  pt_item   = CORRESPONDING #( lt_matdocit ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form init_serial_display
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_serial_display .

  IF go_cont IS NOT BOUND.

    DATA : lt_fcat TYPE lvc_t_fcat,
           ls_layo TYPE lvc_s_layo,
           ls_vari TYPE disvariant.

    lt_fcat = VALUE #(
      ( key = abap_true fieldname = 'SERIAL' ref_table = 'ZC103MMT0020' just = 'C' )
                      ( fieldname = 'MATID'  coltext = '자재번호' just = 'C' emphasize = abap_true )
                      ( fieldname = 'MATNAME' coltext = '자재명' just = 'C' )
                      ( fieldname = 'PLNID'  coltext = '플랜트번호'  just = 'C' emphasize = abap_true )
                      ( fieldname = 'PNAME'  coltext = '플랜트명' just = 'C' )
                      ( fieldname = 'STRID'  coltext = '저장위치번호' just = 'C' emphasize = abap_true )
                      ( fieldname = 'SNAME'  coltext = '저장위치명' just = 'C' )
                      ( fieldname = 'STATUS'  coltext = '사용여부' just = 'C' ) ).
    ls_layo = VALUE #( zebra = abap_true sel_mode = 'D' cwidth_opt = 'A' ).
    ls_vari = VALUE #( report = sy-repid handle = 'FM03ALV1' ).

    " create object
    CREATE OBJECT go_cont
      EXPORTING
        container_name = 'SERIAL_CONT'.

    CREATE OBJECT go_grid
      EXPORTING
        i_parent = go_cont.

    SET HANDLER : lcl_event_handler=>handle_edit_toolbar FOR go_grid,
                  lcl_event_handler=>handle_user_command FOR go_grid.

    CALL METHOD go_grid->set_table_for_first_display
      EXPORTING
        is_variant                    = ls_vari
        i_save                        = 'A'
        i_default                     = 'X'
        is_layout                     = ls_layo
      CHANGING
        it_outtab                     = gt_serial
        it_fieldcatalog               = lt_fcat.

  ENDIF.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
