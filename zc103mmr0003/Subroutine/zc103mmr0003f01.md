```abap
*&---------------------------------------------------------------------*
*& Include          ZC103MMR0003F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form init_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_data .

  pa_year = 2025.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_subscreen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_subscreen .

  " 서브스크린 설정
  CASE gc_tab-activetab.
    WHEN 'TAB1'.
      gv_subscreen = '0101'.
    WHEN 'TAB2'.
      gv_subscreen = '0102'.
    WHEN OTHERS.
      gc_tab-activetab = 'TAB1'.
      gv_subscreen = '0101'.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_base_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_base_data .
  " 조회할 일 데이터 만들기
  DATA : lv_from(8), lv_to(8).
  lv_from = pa_year && '0101'.
  lv_to = pa_year && '1231'.

  " 승인 완료된 구매 오더만 가져온다.
  SELECT poid prid plnid strid mrpid order_date receive_date
         total_price currency order_status
         creator approval erdat erzet ernam aedat aezet aenam
    INTO CORRESPONDING FIELDS OF TABLE gt_pohdback
    FROM ZC103MMt0011
   WHERE order_date BETWEEN lv_from AND lv_to
     AND order_status NE 'A'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_master_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_master_data .
  " 마스터 데이터를 조회한다

  " 플랜트정보
  SELECT plnid name address
    INTO CORRESPONDING FIELDS OF TABLE gt_plant
    FROM ZC103MMt0003.

  " 저장위치 정보
  SELECT plnid strid name
    INTO CORRESPONDING FIELDS OF TABLE gt_stlo
    FROM ZC103MMt0004.

  " 자재 정보
  SELECT matid matname price weight_unit base_unit
    INTO CORRESPONDING FIELDS OF TABLE gt_mara
    FROM ZC103MMt0001.

  " 직원정보
  SELECT empno empname dptcode
    INTO CORRESPONDING FIELDS OF TABLE gt_emp
    FROM zc103fit0011.

  " 벤더정보
  SELECT bpid name
    INTO CORRESPONDING FIELDS OF TABLE gt_vend
    FROM zc103mmt0002.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_data .

  DATA : lv_tabix     TYPE sy-tabix,
         lt_emp_value LIKE gt_emp_value.

  " 사원정보 서치헬프 값 세팅
  lt_emp_value = CORRESPONDING #( gt_emp ).
  gt_emp_value = VALUE #( FOR ls_emp_value IN lt_emp_value
                          WHERE ( dptcode = 'MM' ) ( ls_emp_value ) ).

  " 도메인 값
  DATA : lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v.

  " 구매오더상태 텍스트
  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_ORDSTATUS'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  LOOP AT gt_pohd INTO DATA(ls_pohd).

    lv_tabix = sy-tabix.

    CLEAR : gs_plant, gs_stlo, gs_mara.

    " 플랜트 정보 세팅
    READ TABLE gt_plant INTO gs_plant WITH KEY plnid = ls_pohd-plnid.
    ls_pohd-pname = gs_plant-name.

    " 저장위치 정보 세팅
    READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = ls_pohd-strid.
    ls_pohd-sname = gs_stlo-name.

    " 생성자명 세팅
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = ls_pohd-creator.
    ls_pohd-creator_name = gs_emp-empname.

    " 승인자명 세팅
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = ls_pohd-approval.
    ls_pohd-approval_name = gs_emp-empname.

    " 입고 상태, 버튼 텍스트 세팅
    IF ls_pohd-order_status EQ 'B'.
      ls_pohd-icon = icon_led_red.
      ls_pohd-receive_status = '입고대기'.
      ls_pohd-receive_btn = '입고하기'.
    ELSEIF ls_pohd-order_status EQ 'C'.
      ls_pohd-icon = icon_led_yellow.
      ls_pohd-receive_status = '입고완료'.
      ls_pohd-receive_btn = '입고조회'.
    ELSEIF ls_pohd-order_status EQ 'D'.
      ls_pohd-icon = icon_led_green.
      ls_pohd-receive_status = '입고완료'.
      ls_pohd-receive_btn = '입고조회'.
    ENDIF.


    " 버튼
    ls_pohd-celltab = VALUE #( BASE ls_pohd-celltab
                               ( fieldname = 'RECEIVE_BTN' style = cl_gui_alv_grid=>mc_style_button ) ).

    " 구매오더상태 텍스트
    CLEAR ls_dd07v.
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = ls_pohd-order_status.
    ls_pohd-order_status_text = ls_dd07v-ddtext.

    MODIFY gt_pohd FROM ls_pohd INDEX lv_tabix TRANSPORTING pname sname creator_name approval_name icon
                                               receive_status receive_btn celltab order_status_text.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form count_num
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM count_num .
*-- 2025년
  DATA : lv_from     TYPE sy-datum,        " 시작일 데이터 ex) '20250101'
         lv_to       TYPE sy-datum,        " 종료일 데이터 ex) '20250131'
         lv_index    TYPE n LENGTH 2,
         ls_pohdtree LIKE gs_pohdtree,
         lt_pohdtree LIKE TABLE OF gs_pohdtree.

  " 1년치 데이터 한번에 가져옴
  lv_from = pa_year && '0101'.
  lv_to = pa_year && '1231'.

  " CDS 뷰에서 개수 미리 세옴
  SELECT FROM zc103mmcds_e_0002( start_date = @lv_from, end_date = @lv_to )
    FIELDS Order_month AS month , Not_received AS not_received, Received AS received
    INTO TABLE @lt_pohdtree.

  " CDS 뷰에서 Received와 Not_received가 다른 행에 나오므로 임시로 collect 하는 코드 추가함
  CLEAR : gt_pohdtree.
  LOOP AT lt_pohdtree INTO ls_pohdtree.

    COLLECT ls_pohdtree INTO gt_pohdtree.

  ENDLOOP.

  " 반복문을 돌면서 테이블에 값이 없으면 (n월, 0개, 0개)의 데이터를 넣는다
  CLEAR ls_pohdtree.
  DO 12 TIMES.

    " 월 문자 만들기
    IF sy-index < 10.
      lv_index = |0{ sy-index }|.
    ELSE.
      lv_index = sy-index.
    ENDIF.

    " gt_pohdtree에 데이터가 있는지 확인
    " 데이터가 있다면 넘어간다
    CLEAR ls_pohdtree.
    READ TABLE gt_pohdtree WITH KEY month = lv_index INTO ls_pohdtree.
    IF ls_pohdtree IS NOT INITIAL.
      CONTINUE.
    ENDIF.

    " 데이터가 없는 월 데이터 추가하고 인터널테이블에 추가함
    CLEAR ls_pohdtree.
    ls_pohdtree = VALUE #( month = lv_index not_received = 0 received = 0 ).
    APPEND ls_pohdtree TO gt_pohdtree.

  ENDDO.

  "인터널테이블을 월별로 정렬
  SORT gt_pohdtree BY month.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_receipt_tree
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_receipt_tree .

  IF go_receipt_tree IS NOT BOUND.

    PERFORM init_receipt_tree. " TREE에 사용되는 객체 생성
    PERFORM define_rec_hierarchy_header CHANGING  gs_receipt_hierhdr. " heirarchy_header 세팅 (맨 왼쪽)
    PERFORM build_receipt_comment USING gt_list_commentary gv_logo.       " 트리 상단 정보
    PERFORM define_field_catalog.
    PERFORM create_hierarchy.
    PERFORM fill_column_tree.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form init_receipt_tree
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_receipt_tree .

  CREATE OBJECT go_receipt_container
    EXPORTING
      container_name = 'RECEIPT_CONT'.

  CREATE OBJECT go_receipt_tree
    EXPORTING
      parent              = go_receipt_container
      node_selection_mode = cl_gui_column_tree=>node_sel_mode_single
      item_selection      = ' '.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form define_hierarchy_header
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM define_rec_hierarchy_header CHANGING ps_hierhdr TYPE treev_hhdr.

  ps_hierhdr = VALUE #( heading = '월'
                        tooltip = '구매오더 생성 월'
                        width = 15 width_pix = space ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form build_comment
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GT_LIST_COMMENTARY
*&      --> GV_LOGO
*&---------------------------------------------------------------------*
FORM build_receipt_comment  USING    pt_list_commentary TYPE slis_t_listheader
                                     pv_logo            TYPE sdydo_value.

  " 트리 상단
  pt_list_commentary = VALUE #(
    ( typ = 'H' info = '구매오더 생성일 기준' )
    ( typ = 'S' key = '조회년도 : ' info = pa_year )
    ( typ = 'S' key = '오늘날짜 : ' info = sy-datum )
    ( typ = 'S' key = '현재시간 : ' info = sy-uzeit ) ).

  pv_logo = 'SLOGO1'.
  gs_receipt_variant = sy-repid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form define_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM define_field_catalog .

  gt_receipt_fcat = VALUE #(
    ( fieldname = 'MONTH' coltext = '월' no_out = abap_true )
    ( fieldname = 'NOT_RECEIVED' coltext = '입고건수' outputlen = 20 )
    ( fieldname = 'RECEIVED' coltext = '미입고건수'  outputlen = 20 )
  ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_hierarchy
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_hierarchy .

  " 월별 트리 출력

  CALL METHOD go_receipt_tree->set_table_for_first_display
    EXPORTING
      is_variant          = gs_receipt_variant
      i_save              = 'A'
      i_default           = 'X'
      is_hierarchy_header = gs_receipt_hierhdr
      it_list_commentary  = gt_list_commentary
      i_logo              = gv_logo
      i_background_id     = 'SBACK1'
    CHANGING
      it_outtab           = gt_empohdtr
      it_fieldcatalog     = gt_receipt_fcat.

  " 이벤트 핸들러 세팅 및 등록
  SET HANDLER : lcl_event_handler=>on_item_ctx_menu_request  FOR go_receipt_tree,
                lcl_event_handler=>on_item_ctx_menu_selected FOR go_receipt_tree,
                lcl_event_handler=>on_node_double_click      FOR go_receipt_tree.

  CALL METHOD go_receipt_tree->get_registered_events
    IMPORTING
      events = gt_receipt_events.

  " node double click 이벤트
  gs_receipt_events-eventid = cl_gui_column_tree=>eventid_node_double_click.
  gs_receipt_events-appl_event = abap_true.
  APPEND gs_receipt_events TO gt_receipt_events.

  CALL METHOD go_receipt_tree->set_registered_events
    EXPORTING
      events = gt_receipt_events.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form change
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_0
*&      --> CL_GUI_COLUMN_TREE=>ITEM_CLASS
*&---------------------------------------------------------------------*
FORM change  USING  type TYPE i
                   value TYPE i.

  DATA: lt_selected_nodes   TYPE lvc_t_nkey,
        ls_selected_node    TYPE lvc_nkey,
        lt_item_layout      TYPE lvc_t_layi,
        ls_item_layout_wa   TYPE lvc_s_layi,
        lt_change_layout    TYPE lvc_t_laci,
        ls_change_layout_wa TYPE lvc_s_laci,
        l_fieldname         TYPE lvc_fname,
        ls_outtab           LIKE gt_pohdtree.

  CALL METHOD go_receipt_tree->get_selected_nodes
    CHANGING
      ct_selected_nodes = lt_selected_nodes.

  IF lt_selected_nodes IS INITIAL.
    CALL METHOD go_receipt_tree->get_selected_item
      IMPORTING
        e_selected_node = ls_selected_node
        e_fieldname     = l_fieldname.
    APPEND ls_selected_node TO lt_selected_nodes.
  ENDIF.

  LOOP AT lt_selected_nodes INTO ls_selected_node.
    CALL METHOD go_receipt_tree->get_outtab_line
      EXPORTING
        i_node_key     = ls_selected_node
      IMPORTING
        e_outtab_line  = ls_outtab
        et_item_layout = lt_item_layout.

    CLEAR lt_change_layout.
    LOOP AT lt_item_layout INTO ls_item_layout_wa.
      IF l_fieldname IS INITIAL OR
         ls_item_layout_wa-fieldname EQ l_fieldname.

        MOVE-CORRESPONDING ls_item_layout_wa TO ls_change_layout_wa.

        CASE type.
          WHEN 0.
            ls_change_layout_wa-class = value.
            ls_change_layout_wa-u_class = 'X'.
            IF value EQ cl_gui_column_tree=>item_class_checkbox.
              ls_change_layout_wa-editable = 'X'.
            ELSE.
              ls_change_layout_wa-editable = space.
            ENDIF.
            ls_change_layout_wa-u_editable = 'X'.
          WHEN 1.
            ls_change_layout_wa-font = value.
            ls_change_layout_wa-u_font = 'X'.
          WHEN 2.
            ls_change_layout_wa-style = value.
            ls_change_layout_wa-u_style = 'X'.
          WHEN 3.
            IF value EQ 0.
              ls_change_layout_wa-t_image = icon_okay.
            ELSE.
              ls_change_layout_wa-t_image = space.
            ENDIF.

            ls_change_layout_wa-u_t_image = 'X'.
        ENDCASE.
        APPEND ls_change_layout_wa TO lt_change_layout.
      ENDIF.
    ENDLOOP.

    CALL METHOD go_receipt_tree->change_node
      EXPORTING
        i_node_key     = ls_selected_node
        i_outtab_line  = ls_outtab
        it_item_layout = lt_change_layout.

  ENDLOOP.

  CALL METHOD go_receipt_tree->frontend_update.
  CALL METHOD cl_gui_cfw=>flush.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_column_tree
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM fill_column_tree .
  " ALV 트리에 정보를 넣는다
  DATA: lv_node_text   TYPE lvc_value,
        lv_month_key   TYPE lvc_nkey,
        lv_root_key    TYPE lvc_nkey,
        lv_partner_key TYPE lvc_nkey,
        lt_layout_item TYPE lvc_t_layi,
        ls_layout      TYPE lvc_s_layn.

  SORT gt_pohdtree BY month.

  LOOP AT gt_pohdtree INTO gs_pohdtree.

    ON CHANGE OF gs_pohdtree-month.
      CLEAR ls_layout.
      ls_layout = VALUE #( isfolder = abap_false ).
      lv_node_text = gs_pohdtree-month.

      CALL METHOD go_receipt_tree->add_node
        EXPORTING
          i_relat_node_key = lv_root_key
          i_relationship   = cl_gui_column_tree=>relat_last_child
          is_node_layout   = ls_layout
          i_node_text      = lv_node_text
          is_outtab_line   = gs_pohdtree   " 이걸 꼭 넣어야함
          it_item_layout   = lt_layout_item
        IMPORTING
          e_new_node_key   = lv_month_key.
    ENDON.

    PERFORM create_item_layouts CHANGING lt_layout_item.

  ENDLOOP.

  CALL METHOD : go_receipt_tree->update_calculations,
                go_receipt_tree->frontend_update,
                cl_gui_cfw=>flush.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_item_layouts
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LT_LAYOUT_ITEM
*&---------------------------------------------------------------------*
FORM create_item_layouts  CHANGING pt_layout_item TYPE lvc_t_layi.

  DATA : ls_item_layout TYPE lvc_s_layi.

  CLEAR pt_layout_item.
  LOOP AT gt_receipt_fcat INTO gs_receipt_fcat.
    CLEAR ls_item_layout.
    IF gs_receipt_fcat-no_out EQ space.
      ls_item_layout-fieldname = gs_receipt_fcat-fieldname.
      APPEND ls_item_layout TO pt_layout_item.
    ENDIF.
  ENDLOOP.

  CLEAR ls_item_layout.
  ls_item_layout-fieldname = go_receipt_tree->c_hierarchy_column_name.
  APPEND ls_item_layout TO pt_layout_item.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_on_node_double_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> NODE_KEY
*&---------------------------------------------------------------------*
FORM handle_on_node_double_click  USING  pv_node_key TYPE lvc_nkey  " pv_node_key가 선택된 인덱스
                                         po_sender   TYPE REF TO cl_gui_alv_tree.
  " 노드 더블클릭 이벤트 구현
  " 더블클릭시 우측 구매오더 alv에 사용될 itab 필터링
  " 우측 alv 리프레시
  gv_sel_node_key = pv_node_key.
  IF gv_sel_node_key IS NOT INITIAL.

    CASE po_sender.
      WHEN go_receipt_tree.
        PERFORM node_double_click_receipt USING pv_node_key.
      WHEN go_stlo_tree.
        PERFORM node_double_click_stlo    USING pv_node_key.
      WHEN OTHERS.
    ENDCASE.

  ENDIF.

  " 송장 alv 초기화
  CLEAR : gt_grhd.
  PERFORM refresh_layout USING go_gr_grid.
  PERFORM refresh_grid USING go_gr_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_po_grid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_po_grid .

  " 우측의 구매오더를 띄운다

  IF go_main_cont IS INITIAL.

    " top 설정
    CLEAR :  gt_po_fcat.
    PERFORM set_po_fcat.
    PERFORM set_po_layout.

    " bottom 설정
    CLEAR : gt_gr_fcat.
    PERFORM set_gr_fcat.
    PERFORM set_gr_layout.

    PERFORM create_main_object.

    SET HANDLER : lcl_event_handler=>on_cell_btn_click FOR go_po_grid,
                  lcl_event_handler=>on_edit_toolbar   FOR go_po_grid,
                  lcl_event_handler=>on_hotspot_click  FOR go_po_grid,
                  lcl_event_handler=>on_edit_toolbar   FOR go_gr_grid,
                  lcl_event_handler=>on_user_command   FOR go_gr_grid,
                  lcl_event_handler=>on_cell_btn_click FOR go_gr_grid,
                  lcl_event_handler=>on_hotspot_click  FOR go_gr_grid.

    " top 출력
    CALL METHOD go_po_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_po_vari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_po_layo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_pohd
        it_fieldcatalog      = gt_po_fcat.

    " bottom 출력
    CALL METHOD go_gr_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_gr_vari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_gr_layo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_grhd
        it_fieldcatalog      = gt_gr_fcat.

  ELSE.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_po_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_po_fcat .

  gt_po_fcat = VALUE #(
    ( key = abap_true fieldname = 'ICON' just = 'C' coltext = '상태' )
    ( key = abap_true fieldname = 'POID' ref_table = 'ZC103MMT0011' just = 'C' hotspot = abap_true )
                    ( fieldname = 'PRID' ref_table = 'ZC103MMT0011' just = 'C' )
                    ( fieldname = 'PLNID' ref_table = 'ZC103MMT0011' just = 'C' )
                    ( fieldname = 'PNAME' coltext = '플랜트명' emphasize = abap_true )
                    ( fieldname = 'STRID' ref_table = 'ZC103MMT0011' just = 'C' )
                    ( fieldname = 'SNAME' coltext = '저장위치명' emphasize = abap_true )
                    ( fieldname = 'ORDER_DATE' ref_table = 'ZC103MMT0011' coltext = '오더생성일'
                     emphasize = abap_true just = 'C' )
                    ( fieldname = 'RECEIVE_DATE' ref_table = 'ZC103MMT0011' coltext = '납품일'
                     emphasize = abap_true just = 'C' )
                    ( fieldname = 'TOTAL_PRICE' ref_table = 'ZC103MMT0011' cfieldname = 'CURRENCY' just = 'R'
                     coltext = '총 금액' emphasize = abap_true )
                    ( fieldname = 'CURRENCY' ref_table = 'ZC103MMT0011' coltext = '화폐' )
                    ( fieldname = 'ORDER_STATUS_TEXT' just = 'C' coltext = '구매오더상태' emphasize = abap_true  )
                    ( fieldname = 'CREATOR' coltext = '생성자 사번' just = 'C' emphasize = abap_true )
                    ( fieldname = 'CREATOR_NAME' coltext = '생성자 이름' just = 'C' )
                    ( fieldname = 'APPROVAL' coltext = '승인자 사번' just = 'C' emphasize = abap_true )
                    ( fieldname = 'APPROVAL_NAME' coltext = '승인자 이름' just = 'C' )
                    ( fieldname = 'RECEIVE_STATUS' coltext = '입고상태' just = 'C' emphasize = abap_true  )
                    ( fieldname = 'RECEIVE_BTN' coltext = '입고' just = 'C') ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_po_layout .

  " 레이아웃 세팅
  gs_po_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'D' stylefname = 'CELLTAB'
                        grid_title = '구매오더정보 : 미선택' ).

  " variant 세팅
  gs_po_vari = VALUE #( report = sy-repid handle = 'ALV1' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_po_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_main_object .

  CREATE OBJECT go_main_cont
    EXPORTING
      container_name = 'MAIN_CONT'.

  CREATE OBJECT go_split_cont
    EXPORTING
      parent  = go_main_cont
      rows    = 2
      columns = 1.

  " top container 가져오기
  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_top_cont.

  " bottom container 가져오기
  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 2
      column    = 1
    RECEIVING
      container = go_bottom_cont.

  CREATE OBJECT go_po_grid
    EXPORTING
      i_parent = go_top_cont.

  CREATE OBJECT go_gr_grid
    EXPORTING
      i_parent = go_bottom_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_grid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_grid USING po_alv_grid TYPE REF TO cl_gui_alv_grid .
  " 우측 alv 새로고침

  DATA : ls_stbl TYPE lvc_s_stbl.
  ls_stbl = VALUE #( row = abap_true col = abap_true ).

  CALL METHOD po_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stbl.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_cell_btn_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> ES_COL_ID
*&      --> ES_ROW_ID
*&---------------------------------------------------------------------*
FORM handle_cell_btn_click  USING    ps_col_id TYPE lvc_s_col
                                     ps_row_no TYPE lvc_s_roid
                                     po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.
    WHEN go_po_grid.
      PERFORM handle_po_cell_btn_click USING ps_col_id ps_row_no.
    WHEN go_gr_grid.
      PERFORM handle_gr_cell_btn_click USING ps_col_id ps_row_no.
    WHEN OTHERS.
  ENDCASE.



ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_pop_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_poid_screen .

  IF go_poid_cont IS INITIAL.

    CLEAR : gt_poid_fcat.
    PERFORM set_poid_fcat.
    PERFORM set_poid_layo.
    PERFORM create_poid_object.
    PERFORM exclude_toolbar.
    SET HANDLER : lcl_event_handler=>on_edit_toolbar FOR go_poid_grid.
    CALL METHOD go_poid_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_poid_vari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_poid_layo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_poid
        it_fieldcatalog      = gt_poid_fcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_poid_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_poid_fcat .

  gt_poid_fcat = VALUE #(
    ( key = abap_true fieldname = 'POID' ref_table = 'ZC103MMT0011'  )
                    ( fieldname = 'MATID' ref_table = 'ZC103MMT0001' )
                    ( fieldname = 'MATNAME' ref_table = 'ZC103mmt0001' coltext = '자재이름' emphasize = abap_true  )
                    ( fieldname = 'BPID' ref_table = 'ZC103MMT0002'  )
                    ( fieldname = 'BPNAME' coltext = '벤더 명' emphasize = abap_true )
                    ( fieldname = 'QUANTITY' ref_table = 'ZC103MMT0011' coltext = '수량' qfieldname = 'UNIT' )
                    ( fieldname = 'UNIT' ref_table = 'ZC103MMT0011' coltext = '단위' just = 'C'  )
                    ( fieldname = 'PRICE' ref_table = 'ZC103MMT0011' cfieldname = 'CURRENCY' coltext = '총 금액' just = 'R' )
                    ( fieldname = 'CURRENCY' ref_table = 'ZC103MMT0011' coltext = '통화' just = 'C' ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_poid_layo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_poid_layo .

  gs_poid_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' grid_title = '구매오더상세' ).
  gs_poid_vari = VALUE #( report = sy-repid handle = 'ALV2' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_poid_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_poid_object .

  CREATE OBJECT go_poid_cont
    EXPORTING
      container_name = 'POID_CONT'.

  CREATE OBJECT go_poid_grid
    EXPORTING
      i_parent = go_poid_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_rece_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_rece_screen .

  IF go_rece_cont IS NOT BOUND.

    CLEAR : gt_rece_fcat.
    PERFORM set_rece_fcat.
    PERFORM set_rece_layo.
    PERFORM create_rece_object.
    SET HANDLER : lcl_event_handler=>on_data_changed_finished FOR go_rece_grid,
                  lcl_event_handler=>on_edit_toolbar          FOR go_rece_grid,
                  lcl_event_handler=>on_user_command          FOR go_rece_grid.
    CALL METHOD go_rece_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_rece_vari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_rece_layo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_rece
        it_fieldcatalog      = gt_rece_fcat.
    PERFORM register_event.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_rece_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_rece_fcat .

  gt_rece_fcat = VALUE #(
    ( key = abap_true fieldname = 'IVID' ref_table = 'ZC103MMT0015' just = 'C' )
                    ( fieldname = 'POID' ref_table = 'ZC103MMT0015'  just = 'C' )
                    ( fieldname = 'MATID' ref_table = 'ZC103MMT0015' just = 'C' )
                    ( fieldname = 'MATNAME' ref_table = 'ZC103mmt0001' coltext = '자재이름' emphasize = abap_true  )
                    ( fieldname = 'BPID' ref_table = 'ZC103MMT0015'  just = 'C' )
                    ( fieldname = 'BPNAME' coltext = '벤더 명' emphasize = abap_true )
                    ( fieldname = 'QUANTITY' ref_table = 'ZC103MMT0015' just = 'C' coltext = '수량' qfieldname = 'UNIT' )
                    ( fieldname = 'UNIT' ref_table = 'ZC103MMT0015' just = 'C' coltext = '단위' )
                    ( fieldname = 'RECEIVE_DATE' ref_table = 'ZC103MMT0015' just = 'C' emphasize = abap_true coltext = '납품일' )
                    ( fieldname = 'PRICE' ref_table = 'ZC103MMT0015' emphasize = abap_true coltext = '금액'  cfieldname = 'CURRENCY'
                      just = 'R' )
                    ( fieldname = 'CURRENCY' ref_table = 'ZC103MMT0015' just = 'C' coltext = '통화' ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_rece_layo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_rece_layo .

  gs_rece_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'D' stylefname = 'CELLTAB'
                          grid_title = '입고문서' ).
  gs_rece_vari = VALUE #( report = sy-repid handle = 'ALV3' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_rece_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_rece_object .

  CREATE OBJECT go_rece_cont
    EXPORTING
      container_name = 'RECE_CONT'.

  CREATE OBJECT go_rece_grid
    EXPORTING
      i_parent = go_rece_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event .

  DATA : lv_mode TYPE i VALUE 1.
  IF gs_sel_pohd-order_status EQ 'C' OR
     gs_sel_pohd-order_status EQ 'D' OR
     gs_sel_pohd-order_status EQ 'E'.

    lv_mode = 0.

  ENDIF.

  CALL METHOD go_rece_grid->set_ready_for_input
    EXPORTING
      i_ready_for_input = lv_mode.

  CALL METHOD go_rece_grid->register_edit_event
    EXPORTING
      i_event_id = cl_gui_alv_grid=>mc_evt_modified.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pohd_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pohd_data .
*-- 구매오더 승인 정보 세팅

  " 오더 승인자 이름, 부서코드 가져와서 세팅하기
  CLEAR gs_emp.
  READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_sel_pohd-approval.
  gs_sel_pohd-approval_name = gs_emp-empname.
  gs_sel_pohd-approval_dept = gs_emp-dptcode.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_poid_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pop_data USING pv_table TYPE ANY TABLE .
  " 구매오더 아이템 데이터 세팅

  " 구매오더아이템, 입고 문서에서 공통으로 사용되는 텍스트를 한번에 세팅하기 위해 필드심볼 사용함
  FIELD-SYMBOLS : <fs_tab>     TYPE ANY TABLE,
                  <fs_str>     TYPE any, " 워크에이리어
                  <fs_field>   TYPE any, " 필드
                  <fs_celltab> TYPE lvc_t_styl. " celltab 할당용

  ASSIGN pv_table TO <fs_tab>.

  DATA : lv_tabix TYPE sy-tabix.

  " <fs_tab> 내의 워크에이리어를 <fs_str>에 할당하며 루프를 돌린다
  LOOP AT <fs_tab> ASSIGNING <fs_str>.

    lv_tabix = sy-tabix.
    CLEAR : gs_mara, gs_vend.

    " 자재 이름 세팅
    " <fs_str>에서 MATID를 읽어 <fs_field> 에 할당한다
    ASSIGN COMPONENT 'MATID' OF STRUCTURE <fs_str> TO <fs_field>.
    READ TABLE gt_mara INTO gs_mara WITH KEY matid = <fs_field>.
    " 값을 업데이트하기 위해 <fs_field>에 matname 필드 할당
    ASSIGN COMPONENT 'MATNAME' OF STRUCTURE <fs_str> TO <fs_field>.
    <fs_field> = gs_mara-matname.

    " 벤더 명 세팅
    ASSIGN COMPONENT 'BPID' OF STRUCTURE <fs_str> TO <fs_field>.
    READ TABLE gt_vend INTO gs_vend WITH KEY bpid = <fs_field>.
    ASSIGN COMPONENT 'BPNAME' OF STRUCTURE <fs_str> TO <fs_field>.
    <fs_field> = gs_vend-name.

    " 입고의 경우 style설정
    ASSIGN COMPONENT 'CELLTAB' OF STRUCTURE <fs_str> TO <fs_field>.
    CHECK sy-subrc EQ 0. " CELLTAB이 있을 때만 진행
    ASSIGN <fs_field> TO <fs_celltab>. " 인터널 테이블이므로 새로운 필드심볼 할당
    " 입고문서에서 수량의 경우에 수정이 가능하도록 수정
    <fs_celltab> = VALUE #( ( fieldname = 'QUANTITY' style = cl_gui_alv_grid=>mc_style_enabled ) ).

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_rece_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM make_rece_data .
  " 구매오더 아이템과 동일한 수의 자재수령문서를 생성한다
  DATA : lv_tabix   TYPE sy-tabix,
         lv_ivid(8). " 자재수령번호

  LOOP AT gt_poid INTO DATA(lv_poid).

    CLEAR gs_rece.

    " 자재수령번호 가져오기
    CALL FUNCTION 'ZC103MMFG0002'
      EXPORTING
        iv_prefix = 'IV'
        iv_nro_no = '01'
        iv_object = 'ZC103MM03'
      IMPORTING
        ev_id     = lv_ivid.

    gs_rece = CORRESPONDING #( lv_poid ).
    gs_rece-receive_date = gs_sel_pohd-receive_date.
    gs_rece-ivid = lv_ivid.

    APPEND gs_rece TO gt_rece.

  ENDLOOP.

ENDFORM.
*--------------------------------------------------------------*
*& Form handle_data_changed_finished
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_MODIFIED
*&      --> ET_GOOD_CELLS
*&---------------------------------------------------------------------*
FORM handle_data_changed_finished  USING    pv_modified
                                            pt_good_cells TYPE lvc_t_modi.
  " 수량이 변경될 때 마다 가격에 반영한다

  LOOP AT pt_good_cells INTO DATA(ls_good_cell).

    CASE ls_good_cell-fieldname.
      WHEN 'QUANTITY'. " 수량이 변경되는 경우에 가격을 다시 세팅한다

        CLEAR : gs_mara, gs_rece.
        " 변경된 행의 데이터를 읽는다
        READ TABLE gt_rece INTO gs_rece INDEX ls_good_cell-row_id.
        " 자재 가격 정보를 가져온다
        READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_rece-matid.
        " itab에 가격 정보 수정 및 반영
        gs_rece-price  = gs_rece-quantity * gs_mara-price.
        MODIFY gt_rece FROM gs_rece INDEX ls_good_cell-row_id TRANSPORTING price.
        " itab 새로고침
        PERFORM refresh_grid USING go_rece_grid.

    ENDCASE.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar  USING    po_object TYPE REF TO cl_alv_event_toolbar_set
                                   po_sender TYPE REF TO cl_gui_alv_grid.
  " 툴바 설정
  CASE po_sender.
    WHEN go_rece_grid.
      po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = 3 ) ).
    WHEN go_poid_grid.
      po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = 3 ) ).
    WHEN go_po_grid.
      po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = 3 ) ).
    WHEN go_gr_grid.
      CASE gv_checkgr_mode.
        WHEN 'E'.
          po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = 3 )
                               ( function = 'GRUP' icon = icon_xls text = ' 송장업로드' )
                               ( function = 'GRDT' icon = icon_delete_row )
                               ( function = 'GRSV' icon = icon_system_save )
                               ( butn_type = 3 )
                               ( function = 'FDWN' icon = icon_xls text = ' 폼다운로드' )
                               ( butn_type = 3 )
*                               ( function = 'MAIL' icon = icon_mail text = ' 메일전송' )
                               ).
        WHEN 'V'.
          po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = 3 )
                               ( function = 'GRUP' icon = icon_xls text = ' 송장업로드' disabled = abap_true )
                               ( function = 'GRDT' icon = icon_delete_row disabled = abap_true  )
                               ( function = 'GRSV' icon = icon_system_save disabled = abap_true  )
                               ( butn_type = 3 )
                               ( function = 'FDWN' icon = icon_xls text = ' 폼다운로드' )
                               ( butn_type = 3 )
*                               ( function = 'MAIL' icon = icon_mail text = ' 메일전송' )
                               ).
      ENDCASE.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_rece_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_rece_data .
  " 입고 정보를 DB에 저장하고 구매오더 상태를 변경한다.

  DATA : lt_rece      TYPE TABLE OF ZC103MMt0015, " 입고정보 저장용
         ls_pohd      TYPE ZC103MMt0011,          " 구매오더헤더 (상태 변경용)
         lv_answer(1),                            " 진행 답변 저장
         lv_tabix     TYPE sy-tabix.              " 인덱스

  DATA : ls_poid  LIKE gs_poid,
         lv_quality_equal TYPE abap_bool VALUE abap_false. " 수량이 같은지 확인을 위한 변수

  " 입고 저장 확인용 팝업창
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = TEXT-t02
    IMPORTING
      ev_answer = lv_answer.

  CHECK lv_answer EQ 1. " 저장 확인이 된 경우에만 진행

  CALL METHOD go_rece_grid->check_changed_data.


  " 입고 데이터가 구매오더 데이터랑 같은지 확인
  LOOP AT gt_rece INTO DATA(ls_rece_tmp).

    CLEAR ls_poid.
    READ TABLE gt_poid INTO ls_poid WITH KEY poid = ls_rece_tmp-poid
                                             matid = ls_rece_tmp-matid.

    " 구매오더 수량과 입고 수량이 같지 않다면 루프 종료
    IF ls_rece_tmp-quantity NE ls_poid-quantity.

      lv_quality_equal = abap_true.
      EXIT.

    ENDIF.

  ENDLOOP.

  " 구매오더 수량과 입고 수량이 같지 않다면 서브루틴 종료
  IF lv_quality_equal EQ abap_true.

    MESSAGE S078 DISPLAY LIKE 'E'.
    EXIT.

  ENDIF.

  IF gv_re_aprv_empno IS INITIAL. " 입고자 정보가 입력되지 않은 경우 오류를 출력하고 서브루틴 종료
    MESSAGE s017 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  " 저장용 itab로 데이터 이동
  lt_rece = CORRESPONDING #( gt_rece ).

  " lt_rece의 각 행 세팅
  LOOP AT lt_rece INTO DATA(ls_rece).

    lv_tabix = sy-tabix.

    ls_rece = VALUE #( BASE ls_rece
                        receiver = gv_re_aprv_empno
                        receive_date = sy-datum
                        aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).

    MODIFY lt_rece FROM ls_rece INDEX lv_tabix TRANSPORTING receiver receive_date
                                               erdat erzet ernam.

  ENDLOOP.

  " 구매오더헤더 데이터 세팅
  ls_pohd = CORRESPONDING #( gs_sel_pohd ).
  ls_pohd = VALUE #( BASE ls_pohd
                          order_status = 'C' " 승인 -> 자재수령완료 상태로 변경
                          aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).

  CHECK lt_rece IS NOT INITIAL.

  " DB에 저장
  INSERT zc103mmt0015 FROM TABLE lt_rece. " 입고문서 저장
  MODIFY ZC103MMt0011 FROM ls_pohd. " 구매오더헤더 수정

  IF sy-subrc EQ 0.
    COMMIT WORK.
  ELSE.
    ROLLBACK WORK.
  ENDIF.

*  " ALV 새로고침
*  gv_refresh_tree_needed = abap_true.
*
**  PERFORM refresh_all_alv.
*  CLEAR : gt_pohd, gt_pohdback. " 데이터를 다시 가져오므로 clear 한다
*  PERFORM get_base_data.
*  " 이전 선택되었던 노드를 보여주도록 한다 (해당 서브루틴 내에서 set_data 호출)
*  " 서브스크린이 0101 -> 입고 tree, 0102 -> 저장위치 tree
*  CASE gv_subscreen.
*    WHEN '0101'.
*      PERFORM handle_on_node_double_click USING gv_sel_node_key go_receipt_tree.
*      PERFORM count_num.
*    WHEN '0102'.
*      PERFORM handle_on_node_double_click USING gv_sel_node_key go_stlo_tree.
*      PERFORM count_stlo_num.
*  ENDCASE.
*  PERFORM refresh_tree.
*  PERFORM refresh_grid USING go_po_grid.
*
*  " 트리 리프레시
*  CASE gv_subscreen.
*    WHEN '0101'. " receipt tree refresh
*
*      " free tree object
*      CALL METHOD : go_receipt_tree->free, go_receipt_container->free.
*
*      FREE : go_receipt_tree, go_receipt_container.
*
*      PERFORM display_receipt_tree.
*
*    WHEN '0102'. " stlo tree refresh
*
*      " free tree object
*      CALL METHOD : go_stlo_tree->free, go_stlo_cont->free.
*
*      FREE : go_stlo_tree, go_stlo_cont.
*
*      PERFORM display_stlo_tree_screen.
*  ENDCASE.

  " free object
  CALL METHOD : go_rece_grid->free, go_rece_cont->free,
                go_poid_grid->free, go_poid_cont->free.

  FREE : go_rece_grid, go_rece_cont, go_poid_grid, go_poid_cont.

  REFRESH : gt_poid, gt_rece.


  LEAVE TO SCREEN 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_rece_emp_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_rece_emp_data .
  " 입고자 사원코드가 세팅되면 이름과 부서코드가 보이도록 한다

  CLEAR : gs_emp.

  IF gv_re_aprv_empno IS NOT INITIAL.

    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gv_re_aprv_empno.
    gv_re_aprv_name = gs_emp-empname.
    gv_re_aprv_dept = gs_emp-dptcode.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*
FORM handle_user_command  USING    pv_ucomm
                                   po_sender TYPE REF TO cl_gui_alv_grid.


  CASE po_sender.
    WHEN go_rece_grid.
      PERFORM rece_user_command USING pv_ucomm.
    WHEN go_gr_grid. " 송장
      PERFORM gr_user_command USING pv_ucomm.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form check_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM check_screen .
  " 입고조회시에는 입고자 정보가 수정 불가능하도록 변경한다.

  LOOP AT SCREEN.

    CASE screen-group1.
      WHEN 'REC'.
        IF gv_rece EQ 'V'. " 팝업 수정 모드가 조회모드이면 입력을 막음
          screen-input = 0.
          MODIFY SCREEN.
        ELSE.
          screen-input = 1. " 수정 모드이면 수정이 가능하도록 설장한다
          MODIFY SCREEN.
        ENDIF.
    ENDCASE.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form exclude_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM exclude_toolbar .

  DATA : ls_ui_functions TYPE ui_func.

  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_undo.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_cut.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_delete_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_insert_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_append_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste_new_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_refresh.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_auf.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_average.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_print.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_graph.
  APPEND ls_ui_functions TO gt_ui_functions.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_po_cell_btn_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PS_COL_ID
*&      --> PS_ROW_NO
*&---------------------------------------------------------------------*
FORM handle_po_cell_btn_click  USING ps_col_id TYPE lvc_s_col
                                     ps_row_no TYPE lvc_s_roid.

  DATA : ls_sel_rece LIKE gs_rece. " 입고문서

  " 기존 데이터 초기화
  CLEAR : gs_sel_pohd, gt_poid, gt_rece, gs_poid, gs_rece,
          gv_re_aprv_empno, gv_re_aprv_name, gv_re_aprv_dept.

  REFRESH : gt_poid, gt_rece.

*-- 선택된 해더 정보 가져오기
  READ TABLE gt_pohd INTO gs_sel_pohd INDEX ps_row_no-row_id.

*-- 구매오더 아이템 조회
  SELECT poid matid bpid quantity unit price currency
    INTO CORRESPONDING FIELDS OF TABLE gt_poid
    FROM ZC103MMt0012
   WHERE poid EQ gs_sel_pohd-poid.

*-- 입고문서 조회
  SELECT ivid poid bpid matid quantity unit receive_date receiver price currency
    INTO CORRESPONDING FIELDS OF TABLE gt_rece
    FROM ZC103MMt0015
   WHERE poid EQ gs_sel_pohd-poid.

*-- 입고일 세팅
  gs_sel_pohd-receive_date = sy-datum.

  " 입고 문서가 없으면 아직 입고 안된것이므로 구매오더와 동일한 수의 입고 문서를 생성한다.
  " 그리고 수정 가능한 모드로 설정한다
  IF gt_rece IS INITIAL.
    gv_rece = 'E'.
    PERFORM make_rece_data.
  ELSE. " 입고문서가 있다면 화면에 입고자 정보를 세팅한다
    " 입고문서 선택행 가져오기.
    gv_rece = 'V'.
    CLEAR gs_emp.
    READ TABLE gt_rece INTO ls_sel_rece WITH KEY poid = gs_sel_pohd-poid.
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = ls_sel_rece-receiver.
    gv_re_aprv_empno = ls_sel_rece-receiver.
    gv_re_aprv_name = gs_emp-empname.
    gv_re_aprv_dept = gs_emp-dptcode.
  ENDIF.

  PERFORM set_pohd_data. " 구매오더 헤더 텍스트 데이터 세팅
  PERFORM set_pop_data USING :  gt_poid, gt_rece. " 구매오더 아이템, 입고문서 텍스트 데이터 세팅

*-- 팝업창 띄우기
  CALL SCREEN 110 STARTING AT 10 10.

  PERFORM refresh_all_alv.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
