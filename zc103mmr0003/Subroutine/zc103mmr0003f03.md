```abap
*----------------------------------------------------------------------*
***INCLUDE ZC103MMR0003F03.
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form display_checkgr_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_checkgr_screen .

  " 송장 검증 팝업창 출력

  IF go_checkgr_cont IS NOT BOUND.

    CLEAR : gt_checkgr_fcat.
    PERFORM set_checkgr_fcat.
    PERFORM set_checkgr_layout.
    PERFORM create_checkgr_object.
    CALL METHOD go_checkgr_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_checkgr_vari
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_checkgr_layo
      CHANGING
        it_outtab       = gt_checkgr
        it_fieldcatalog = gt_checkgr_fcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_checkgr_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_checkgr_fcat .

  gt_checkgr_fcat = VALUE #(
    ( key = abap_true fieldname = 'STATUS'      ref_table = 'ZC103MMT0001' just = 'C' coltext = '검증상태' )
    ( key = abap_true fieldname = 'GRID'        ref_table = 'ZC103MMT0014' just = 'C' )
    ( key = abap_true fieldname = 'IVID'        ref_table = 'ZC103MMT0015' just = 'C' )
    ( key = abap_true fieldname = 'POID'        ref_table = 'ZC103MMT0011' just = 'C' )
                    ( fieldname = 'MATID'       ref_table = 'ZC103MMT0001' just = 'C'  )
                    ( fieldname = 'MATNAME'     ref_table = 'ZC103MMT0001' emphasize = abap_true  )
                    ( fieldname = 'PO_QUANTITY' emphasize = abap_true qfieldname = 'PO_UNIT' coltext = '구매오더수량' )
                    ( fieldname = 'PO_UNIT' just = 'C'  coltext = '구매오더단위'    )
                    ( fieldname = 'IV_QUANTITY' emphasize = abap_true qfieldname = 'IV_UNIT' coltext = '자재수령수량' )
                    ( fieldname = 'IV_UNIT' just = 'C' coltext = '자재수령단위'     )
                    ( fieldname = 'GR_QUANTITY' emphasize = abap_true qfieldname = 'GR_UNIT' coltext = '송장수량' )
                    ( fieldname = 'GR_UNIT' just = 'C'   coltext = '송장단위'   )
                    ( fieldname = 'PO_PRICE'    emphasize = abap_true cfieldname = 'PO_CURRENCY' coltext = '구매오더금액' just = 'R' )
                    ( fieldname = 'PO_CURRENCY'  coltext = '통화' )
                    ( fieldname = 'IV_PRICE'    emphasize = abap_true cfieldname = 'IV_CURRENCY' coltext = '자재수령금액' just = 'R' )
                    ( fieldname = 'IV_CURRENCY'  coltext = '통화' )
                    ( fieldname = 'GR_PRICE'    emphasize = abap_true cfieldname = 'GR_CURRENCY' coltext = '송장금액' just = 'R' )
                    ( fieldname = 'GR_CURRENCY'  coltext = '통화' ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_checkgr_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_checkgr_layout .

  gs_checkgr_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' ).
  gs_checkgr_vari = VALUE #( report = sy-repid handle = 'ALV6' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_checkgr_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_checkgr_object .

  CREATE OBJECT go_checkgr_cont
    EXPORTING
      container_name = 'CHECKGR_CONT'.

  CREATE OBJECT go_checkgr_grid
    EXPORTING
      i_parent = go_checkgr_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_check_gr
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_check_gr .

  DATA : lv_action(1),
         lv_answer(1),
         lt_checkgr LIKE gt_checkgr. " 개수 안맞는 경우 구매요청을 생성하기 위해 필요한 itab

  " DB 상태 변경용 변수
  DATA : ls_pohd  TYPE zc103mmt0011,          " 구매오더 헤더
         lt_grhd  TYPE TABLE OF zc103mmt0013, " 송장 헤더
         ls_grhd  TYPE zc103mmt0013,          " 송장 헤더
         lv_tabix TYPE sy-tabix.

  " 자재문서 생성용 변수
  DATA : lv_plnid  TYPE zc103mmt0003-plnid, " 플랜트번호
         lv_strid  TYPE zc103mmt0004-strid, " 저장위치번호
         lv_belnr  TYPE belnr_d,              " 회계 전표 번호
         lv_move   TYPE zc103mmt0016-movement_type, " 이동유형
         lt_mat    TYPE zc103mmtt0007,      " 자재 정보 (자재번호 수량 단위)
         ls_mat    TYPE zc103mms0008,       " 자재 정부
         lv_result TYPE i,                  " 펑션모듈 결과
         lv_tax    TYPE price.              " 세금

  " 송장 검증 완료 상태를 저장한다
  " 구매오더, 송장 상태 변경
  " 자재문서생성, 회계문서생성

  " 검증 정보가 입력되지 않으면 오류 출력
  IF gv_checker IS INITIAL.
    MESSAGE s017 DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  " 검증할지 사용자에게 확인
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = TEXT-t04
    IMPORTING
      ev_answer = lv_answer.

  CHECK lv_answer EQ 1.

  LOOP AT gt_checkgr INTO gs_checkgr.

    " 올바르지 않은 송장인 경우 오류 출력
    IF gs_checkgr-status EQ icon_led_red OR
       gs_checkgr-status EQ icon_led_yellow.
      MESSAGE i064 DISPLAY LIKE 'E'.
      RETURN.

    ENDIF.

  ENDLOOP.

  IF lt_checkgr IS NOT INITIAL.

    CLEAR : lv_answer.
    " 추가구매요청을 생성할지
    CALL FUNCTION 'ZC1F030001'
      EXPORTING
        iv_action = TEXT-t05
      IMPORTING
        ev_answer = lv_answer.

    " 사용자가 승인한 경우 신규 구매요청~오더까지 생성
    IF lv_answer EQ 1.
      PERFORM create_additional_pr USING lt_checkgr gv_checker.
    ENDIF.

  ENDIF.

  " 구매오더 상태 변경
  READ TABLE gt_pohd INTO gs_pohd WITH KEY poid = gs_sel_pohd-poid.
  ls_pohd = CORRESPONDING #( gs_pohd ).
  ls_pohd = VALUE #( BASE ls_pohd
                     order_status = 'D'
                     aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).

  " 송장 상태 변경
  lv_tax = gs_grhd-total_price / 10.
  gs_grhd = VALUE #( BASE gs_grhd
                   gr_status = 'B'
                   checker = gv_checker
                   tax_amount = lv_tax gjahr = sy-datum(4)
                   aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).

  ls_grhd = CORRESPONDING #( gs_grhd ).

  " 검증 완료 후 자재문서를 저장한다
  LOOP AT gt_sel_item_gr INTO gs_sel_item_gr.

    ls_mat = CORRESPONDING #( gs_sel_item_gr ).
    APPEND ls_mat TO lt_mat.

  ENDLOOP.

  CALL FUNCTION 'ZC1FM03MM0002'
    EXPORTING
      iv_grid         = gs_grhd-grid
      iv_total        = gs_grhd-total_price
      iv_tax          = gs_grhd-tax_amount
      iv_plnid        = gs_pohd-plnid
      iv_strid        = gs_pohd-strid
      iv_movementtype = '101'
      it_mat          = lt_mat
      iv_bpid         = gs_grhd-bpid
    IMPORTING
      ev_result       = lv_result
      ev_belnr        = lv_belnr.

  ls_grhd-belnr = lv_belnr.
  ls_grhd-document_issued = 'Y'.

  " DB에 반영
  MODIFY zc103mmt0011 FROM ls_pohd.
  MODIFY zc103mmt0013 FROM ls_grhd.

  " 구매오더 ALV, 송장 ALV 새로고침

  " 데이터 다시 가져오기
  PERFORM refresh_alv_grid.
  MESSAGE s074.

  IF lv_answer NE 1.
    LEAVE TO SCREEN 0.
  ENDIF.

  CALL METHOD : go_checkgr_grid->free, go_checkgr_cont->free.

  FREE : go_checkgr_grid, go_checkgr_cont.

  LEAVE TO SCREEN 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form append_gr_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM append_gr_data .



ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_gr_header
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_gr.
  " 송장 헤더, 아이템 데이터를 삭제한다
  DATA : lt_roid TYPE lvc_t_roid.

  CALL METHOD go_gr_grid->get_selected_rows
    IMPORTING
      et_row_no = lt_roid.

  " 삭제될 행이 입력되지 않았다면 종료
  IF lt_roid IS INITIAL.
    MESSAGE s026 DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  SORT lt_roid BY row_id DESCENDING.

  " 송장헤더 itab에서 데이터 삭제하고 삭제용 itab에 값 넣는다
  LOOP AT lt_roid INTO DATA(ls_roid).

    READ TABLE gt_grhd INTO gs_grhd INDEX ls_roid-row_id.
    APPEND gs_grhd TO gt_gr_delt.

    DELETE gt_grhd INDEX ls_roid-row_id.

  ENDLOOP.

  PERFORM refresh_grid USING go_gr_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_delete_gr
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_delete_gr .

  " 삭제된 송장헤더와 그와 연관된 송장 아이템을 모두 DB에서 삭제한다.
  DATA : lt_delt_head TYPE TABLE OF zc103mmt0013,
         ls_pohd      TYPE ZC103MMt0011,
         lv_cnt       TYPE i.

  lt_delt_head = CORRESPONDING #( gt_gr_delt ).

  " 송장 헤더에서 송장번호를 가져와서 아이템 삭제용 테이블에 담는다
  LOOP AT lt_delt_head INTO DATA(ls_delt_head).

    DELETE FROM zc103mmt0014 WHERE grid EQ ls_delt_head-grid.

  ENDLOOP.

  " 송장 헤더 삭제
  DELETE zc103mmt0013 FROM TABLE lt_delt_head.

  " 구매요청 상태 변경
  gs_sel_pohd-order_status = 'C'.
  ls_pohd = CORRESPONDING #( gs_sel_pohd ).
  MODIFY ZC103MMt0011 FROM ls_pohd.

  lv_cnt = lines( lt_delt_head ).

  IF sy-subrc EQ 0.
    MESSAGE s057 WITH lv_cnt.
    COMMIT WORK.
    PERFORM refresh_grid USING : go_po_grid, go_gr_grid.
  ELSE.
    MESSAGE s058 DISPLAY LIKE 'E'.
    ROLLBACK WORK.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_gr_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PV_ROW_ID
*&---------------------------------------------------------------------*
FORM handle_gr_hotspot_click  USING    pv_row_id TYPE lvc_s_row
                                       pv_column_id TYPE lvc_s_col.

  " 송장 alv의 핫스팟 이벤트를 처리하는 서브루틴
  DATA : lv_result TYPE i.

  CASE pv_column_id-fieldname.
    WHEN 'GRID'.

      " 송장 상세 정보를 출력함
      READ TABLE gt_grhd INTO gs_grhd INDEX pv_row_id-index.

      CALL FUNCTION 'ZC1FM03MM0001'
        EXPORTING
          iv_grid   = gs_grhd-grid
          it_vend   = gt_vend
          it_emp    = gt_emp
          it_mara   = gt_mara
        IMPORTING
          ev_result = lv_result.

    WHEN 'EXCEL'.

      PERFORM hotspot_click_gr_excel USING pv_row_id.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_checker_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_checker_data .

  CHECK gv_checker IS NOT INITIAL.

  READ TABLE gt_emp INTO gs_emp WITH KEY empno = gv_checker.
  gv_checker_dptcode = gs_emp-dptcode.
  gv_checker_name = gs_emp-empname.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form f4_empno
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM f4_empno .

  DATA : lt_return   LIKE TABLE OF ddshretval WITH HEADER LINE.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'EMPNO' " ALV 에 박히는 값
      dynpprog        = sy-repid
      dynpnr          = sy-dynnr
      dynprofield     = 'GV_USCODE'
      window_title    = 'Employee'
      value_org       = 'S'
    TABLES
      value_tab       = gt_emp_value
      return_tab      = lt_return
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  CHECK lt_return[] IS NOT INITIAL.

*-- Get employee name : New syntax
*  lt_return = VALUE #( lt_return[ 1 ] ).
*
*  CLEAR gs_emp_value.
*  gs_emp_value = VALUE #( gt_emp_value[ empno = lt_return-fieldval ].

*-- Original syntax
  READ TABLE lt_return INDEX 1.

  CLEAR gs_emp_value.
  READ TABLE gt_emp_value INTO gs_emp_value WITH KEY empno = lt_return-fieldval.

  gv_re_aprv_empno = lt_return-fieldval.
  gv_re_aprv_dept = gs_emp_value-dptcode.
  gv_re_aprv_name = gs_emp_value-empname.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_emp_sh
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_emp_sh .

  DATA : lt_dynp  TYPE TABLE OF dynpread WITH HEADER LINE.

  CHECK gv_re_aprv_name IS NOT INITIAL.

*-- Set employee info
  lt_dynp-fieldname   = 'GV_RE_APRV_DEPT'.
  lt_dynp-fieldvalue  = gv_re_aprv_dept.
  APPEND lt_dynp.

  lt_dynp-fieldname   = 'GV_RE_APRV_NAME'.
  lt_dynp-fieldvalue  = gv_re_aprv_name.
  APPEND lt_dynp.

  CALL FUNCTION 'DYNP_VALUES_UPDATE'
    EXPORTING
      dyname     = sy-repid
      dynumb     = sy-dynnr
    TABLES
      dynpfields = lt_dynp.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_additional_pr
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LT_CHECKGR
*&---------------------------------------------------------------------*
FORM create_additional_pr  USING    pt_checkgr TYPE tt_checkgr " 부족하게 받은 데이터만 받아옴
                                    pv_checker.

  " 송장수량이 구매오더 수량보다 적은 경우 자동으로 구매요청과 오더를 생성하는 서브루틴
  DATA : lv_quantity TYPE ts_checkgr-gr_quantity.

  DATA : ls_header   TYPE zc103mms0002,  " 구매요청 헤더
         lt_item     TYPE zc103mmtt0002, " 구매요청 아이템
         ls_item     TYPE zc103mmt0008,
         lv_tomorrow TYPE sy-datum,      " 내일 날짜
         lv_poid     TYPE zc103mmt0011-poid,
         lv_bpid     TYPE zc103mmt0012-bpid,
         lv_prid     TYPE zc103mmt0007-prid. " 생성된 구매요청 번호

  " 내일 날짜 받아오기
  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      date      = sy-datum
      days      = 1
      months    = 0
      years     = 0
    IMPORTING
      calc_date = lv_tomorrow.

  " 기존 구매오더 읽어오기
  CLEAR : gs_pohd, gs_poid.
  lv_poid = VALUE #( pt_checkgr[ 1 ]-poid ).
  READ TABLE gt_pohd INTO gs_pohd WITH KEY poid = lv_poid.

  SELECT SINGLE poid bpid
    INTO CORRESPONDING FIELDS OF gs_poid
    FROM zc103mmt0012
   WHERE poid EQ lv_poid.

  lv_bpid = gs_poid-bpid.

  " 헤더 데이터 생성
  ls_header = VALUE #( plnid = gs_pohd-plnid strid = gs_pohd-strid pr_status = 'C'
                       estkz = 'B' request_date = sy-datum receive_date = lv_tomorrow
                       creator = pv_checker approval = pv_checker ).

  " 아이템데이터 생성
  LOOP AT pt_checkgr INTO DATA(ls_checkgr).

    CLEAR ls_item.
    lv_quantity = ls_checkgr-po_quantity - ls_checkgr-gr_quantity.
    ls_item = VALUE #( matid = ls_checkgr-matid quantity = lv_quantity unit = ls_checkgr-po_unit
                       price = ls_checkgr-po_price currency = ls_checkgr-po_currency ).
    APPEND ls_item TO lt_item.

  ENDLOOP.

  " 구매요청 생성
  CALL FUNCTION 'ZC103MMFG0001'
    EXPORTING
      is_header = ls_header
      it_item   = lt_item
    IMPORTING
      ev_prid   = lv_prid.

  " 구매오더 전환
  CALL FUNCTION 'ZC103MMFG0003'
    EXPORTING
      iv_prid     = lv_prid
      iv_vendorid = lv_bpid
      iv_approval = pv_checker.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_alv_grid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_alv_grid .
  " 노드 선택정보, 핫스팟클릭 값을 유지한 채로 alv 리프레시

  DATA : ls_row TYPE lvc_s_row.

  PERFORM get_base_data.

  " 노드 더블클릭 설정
  CASE gv_subscreen.
    WHEN '0101'.
      PERFORM handle_on_node_double_click  USING gv_sel_node_key go_receipt_tree.
    WHEN '0102'.
      PERFORM handle_on_node_double_click  USING gv_sel_node_key go_stlo_tree.
  ENDCASE.

  " 핫스팟클릭 다시 실행
  READ TABLE gt_pohd INTO DATA(ls_pohd) WITH KEY poid = gs_sel_pohd-poid. " 인덱스만 확인
  ls_row-index = sy-tabix.
  IF ls_pohd-order_status NE 'B'.
    PERFORM handle_po_hotspot_click USING ls_row.
  ENDIF.

*  PERFORM set_data.
  PERFORM set_grhd_data.
  PERFORM refresh_grid USING : go_po_grid, go_gr_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form f4_empno_grch
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM f4_empno_grch .

  DATA : lt_return   LIKE TABLE OF ddshretval WITH HEADER LINE.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'EMPNO' " ALV 에 박히는 값
      dynpprog        = sy-repid
      dynpnr          = sy-dynnr
      dynprofield     = 'GV_CHECKER'
      window_title    = 'Employee'
      value_org       = 'S'
    TABLES
      value_tab       = gt_emp_value
      return_tab      = lt_return
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  CHECK lt_return[] IS NOT INITIAL.

*-- Get employee name : New syntax
*  lt_return = VALUE #( lt_return[ 1 ] ).
*
*  CLEAR gs_emp_value.
*  gs_emp_value = VALUE #( gt_emp_value[ empno = lt_return-fieldval ].

*-- Original syntax
  READ TABLE lt_return INDEX 1.

  CLEAR gs_emp_value.
  READ TABLE gt_emp_value INTO gs_emp_value WITH KEY empno = lt_return-fieldval.

  gv_checker = lt_return-fieldval.
  gv_checker_dptcode = gs_emp_value-dptcode.
  gv_checker_name = gs_emp_value-empname.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_emp_sh_grch
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_emp_sh_grch .

  DATA : lt_dynp  TYPE TABLE OF dynpread WITH HEADER LINE.

  CHECK gv_checker_name IS NOT INITIAL.

*-- Set employee info
  lt_dynp-fieldname   = 'GV_CHECKER_DPTCODE'.
  lt_dynp-fieldvalue  = gv_checker_dptcode.
  APPEND lt_dynp.

  lt_dynp-fieldname   = 'GV_CHECKER_NAME'.
  lt_dynp-fieldvalue  = gv_checker_name.
  APPEND lt_dynp.

  CALL FUNCTION 'DYNP_VALUES_UPDATE'
    EXPORTING
      dyname     = sy-repid
      dynumb     = sy-dynnr
    TABLES
      dynpfields = lt_dynp.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_all_alv
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_all_alv .

  PERFORM refresh_tree.

  " alv 리프레시
  PERFORM refresh_alv_grid.



ENDFORM.
*&---------------------------------------------------------------------*
*& Form hotspot_click_gr_excel
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM hotspot_click_gr_excel USING pv_row_id TYPE lvc_s_row.

  DATA : lv_answer(1).

  " 엑셀 버튼을 클릭한다

  " 클릭한 행의 데이터를 가져온다.
  " 사용자로부터 확인
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = '엑셀로 저장'
    IMPORTING
      ev_answer = lv_answer.

  CHECK lv_answer EQ 1.

  " 행의 헤더, 아이템정보 가져오기
  READ TABLE gt_grhd INTO gs_sel_grhd INDEX pv_row_id-index.

  " 미검증 송장은 다운로드 하지 못하도록 한다
  IF gs_sel_grhd-gr_status NE 'B'.
    MESSAGE s000 WITH TEXT-e02 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  SELECT grid matid quantity unit unit_price total_price currency
    INTO CORRESPONDING FIELDS OF TABLE gt_sel_grit
    FROM ZC103MMt0014
   WHERE grid = gs_sel_grhd-grid.

  IF objfile IS INITIAL.
    TRY.
        CREATE OBJECT objfile.
    ENDTRY.
  ENDIF.

  " 파일 저장 윈도우
  IF pfolder IS NOT INITIAL.
    initialfolder = pfolder.
  ELSE.
    objfile->get_temp_directory( CHANGING temp_dir = initialfolder
                                 EXCEPTIONS cntl_error = 1 error_no_gui = 2 not_supported_by_gui = 3 ).
  ENDIF.

  objfile->directory_browse( EXPORTING initial_folder = initialfolder
                              CHANGING  selected_folder = pickedfolder
                              EXCEPTIONS cntl_error = 1 error_no_gui = 2 not_supported_by_gui = 3 ).

  IF sy-subrc EQ 0.
    pfolder = pickedfolder.
  ELSE.
    MESSAGE 'An error has occured picking a folder' TYPE 'I' DISPLAY LIKE 'W'.
    EXIT.
  ENDIF.

  IF pfolder IS INITIAL.
    EXIT.
  ENDIF.

  CALL METHOD objfile->free.
  FREE objfile.

  PERFORM download_excel.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form form_download
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM form_download .

  DATA : wwwdata_item TYPE wwwdatatab,
         lv_filename  TYPE rlgrap-filename,
         lv_rc        TYPE i,
         lv_answer    TYPE c.

*-- 사용자에게 폴더 선택 요청
  IF objfile IS INITIAL.
    CREATE OBJECT objfile.
  ENDIF.

  objfile->get_temp_directory( CHANGING temp_dir = initialfolder ).
  objfile->directory_browse( EXPORTING initial_folder = initialfolder
                             CHANGING selected_folder = pickedfolder ).
  IF pickedfolder IS INITIAL.
    MESSAGE '폴더 선택이 취소되었습니다.' TYPE 'I'.
    RETURN.
  ENDIF.

*-- 파일명 정의
  CONCATENATE pickedfolder '\자재목록양식.XLSX' INTO lv_filename.

*-- 기존 파일 삭제 (있다면)
  CALL FUNCTION 'WS_FILE_DELETE'
    EXPORTING
      file   = lv_filename
    IMPORTING
      return = lv_rc.

*-- wwwdata에서 템플릿 가져오기
  SELECT SINGLE * FROM wwwdata
    INTO CORRESPONDING FIELDS OF wwwdata_item
    WHERE objid = 'ZINVOICE'.  " 등록된 템플릿 이름

  IF sy-subrc <> 0.
    MESSAGE '엑셀 템플릿이 시스템에 존재하지 않습니다.' TYPE 'E'.
    RETURN.
  ENDIF.

*-- 템플릿 다운로드
  CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'
    EXPORTING
      key         = wwwdata_item
      destination = lv_filename.

  MESSAGE '엑셀 템플릿이 다운로드되었습니다.' TYPE 'S'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_excel
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM download_excel .

  " 엑셀 다운로드

  DATA : lv_rc TYPE i.

  " filename
  CLEAR gv_temp_filename.
  gv_temp_filename = |{ pfolder }\\송장-{ gs_sel_grhd-grid }-{ sy-datum }-{ sy-uzeit }.XLSX|.

  gv_form = 'ZINVOICE'.
  PERFORM download_template USING gv_form gv_temp_filename.
  PERFORM open_excel_template USING gv_form.
  PERFORM fill_excel_line.

  " 기본적으로 1번 시트를 보여주도록 세팅
  CALL METHOD OF excel 'SHEETS' = sheet EXPORTING #1 = 1.
  CALL METHOD OF sheet 'SELECT' NO FLUSH.

  " 모두 출력 후 맨 뒷칸 커서로 이동
  CALL METHOD OF excel 'Cells' = cell
    EXPORTING
      #1 = 1
      #2 = 1.

  CALL METHOD OF cell 'Select'.

  SET PROPERTY OF excel 'VISIBLE' = 1.

  " 저장하기
  CALL METHOD OF workbook 'SaveAs' EXPORTING #1 = gv_temp_filename.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_template
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GV_FORM
*&      --> GV_TEMP_FILENAME
*&---------------------------------------------------------------------*
FORM download_template  USING    p_zform p_filename.

  DATA : wwwdata_item LIKE wwwdatatab,
         rc           TYPE i.

  gv_file = p_filename.

  CALL FUNCTION 'WS_FILE_DELETE'
    EXPORTING
      file   = gv_file
    IMPORTING
      return = rc.


  IF NOT ( rc EQ 0 OR rc EQ 1 ).
    MESSAGE s001 WITH TEXT-e01 DISPLAY LIKE 'E'.
  ENDIF.

  SELECT SINGLE * FROM wwwdata
    INTO CORRESPONDING FIELDS OF wwwdata_item
   WHERE objid = p_zform.

  CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'
    EXPORTING
      key         = wwwdata_item
      destination = gv_file.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form open_excel_template
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GV_FORM
*&---------------------------------------------------------------------*
FORM open_excel_template  USING    p_zform.

  IF excel IS INITIAL.

    CREATE OBJECT excel 'EXCEL.APPLICATION'.

  ENDIF.

  CALL METHOD OF excel 'WORKBOOKS' = workbook.
  SET PROPERTY OF excel 'VISIBLE' = 0.

  CALL METHOD OF workbook 'OPEN' EXPORTING #1 = gv_file.

  SET PROPERTY OF excel 'DisplayAlerts' = 0.

  GET PROPERTY OF : workbook 'APPLICATION' = application,
                    application 'ActiveSheet' = activesheet.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_excel_line
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM fill_excel_line .

  DATA : lv_receive_date(10),  " 납품일
         lv_document_date(10), " 문서발행일
         lv_tax_rate(5).       " 부가가치세 비율

  DATA : lv_index TYPE i. " 자재번호 입력 엑셀 행 번호

  DATA : lv_total_price TYPE zc103mmt0013-total_price, " 총 금액
         lv_tax_amount  TYPE zc103mmt0013-tax_amount,  " 부가가치세
         lv_unit_price  TYPE zc103mmt0014-unit_price.  " 단가

**********************************************************************
* 송장 헤더 정보 입력
**********************************************************************

  " 납품일
  lv_receive_date = |{ gs_sel_grhd-receive_date(4) }.| &
                    |{ gs_sel_grhd-receive_date+4(2) }.| &
                    |{ gs_sel_grhd-receive_date+6(2) }|.

  " 문서발행일
  lv_document_date = |{ sy-datum(4) }.| &
                     |{ sy-datum+4(2) }.| &
                     |{ sy-datum+6(2) }|.

  " 부가가치세 비율
  lv_tax_rate = TEXT-t06.

  " 벤더정보 가져오기
  READ TABLE gt_vend INTO gs_vend WITH KEY bpid = gs_sel_grhd-bpid.

  " 가격설정
  lv_total_price = gs_sel_grhd-total_price * 100.
  lv_tax_amount  = gs_sel_grhd-tax_amount * 100.

  PERFORM fill_cells USING : 8 1 gs_sel_grhd-grid,  " 송장 번호
                             8 2 gs_sel_grhd-belnr, " 회계 전표 번호
                             8 4 gs_sel_grhd-gjahr, " 회계연도
                             8 5 lv_document_date,  " 문서발행일
                             8 7 lv_total_price, " 총 금액
                             8 8 gs_sel_grhd-currency,    " 통화
                             10 1 gs_sel_grhd-bpid,       " 공급업체번호
                             10 2 gs_vend-name,           " 공급업체명
                             10 4 lv_receive_date,        " 납품일
                             10 5 gs_sel_grhd-checker_name,    " 문서발행인
                             10 7 lv_tax_amount, " 부가가치세
                             10 8 lv_tax_rate.            " 부가가치세 비율

**********************************************************************
* 송장 아이템 정보 입력
**********************************************************************
  lv_index = 12. " 아이템 시작 행 번호

  CLEAR : gs_sel_grit, lv_unit_price, lv_total_price.
  LOOP AT gt_sel_grit INTO gs_sel_grit.

    " 자재정보
    READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_sel_grit-matid.

    " 가격설정
    lv_unit_price = 100 * gs_sel_grit-unit_price.
    lv_total_price = 100 * gs_sel_grit-total_price.

    PERFORM fill_cells USING : lv_index 1 gs_sel_grit-grid, " 자재번호
                               lv_index 2 gs_mara-matname,  " 자재명
                               lv_index 4 gs_sel_grit-quantity, " 자재수량
                               lv_index 5 gs_sel_grit-unit,     " 자재 단위
                               lv_index 6 lv_unit_price, " 단가
                               lv_index 7 lv_total_price, " 총 금액
                               lv_index 8 gs_sel_grit-currency.    " 통화

    lv_index += 1.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_cells
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_8
*&      --> P_1
*&      --> GS_SEL_GRHD_GRID
*&---------------------------------------------------------------------*
FORM fill_cells  USING    i j val .

  " 셀에 값을 입력하는 서브루틴
  CALL METHOD OF excel 'CELLS' = cell
    EXPORTING
      #1 = i  " 행
      #2 = j. " 열

  SET PROPERTY OF cell 'VALUE' = val.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_tree
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_tree .

  CASE gv_subscreen.
    WHEN '0101'. " receipt tree refresh

      " free tree object
      CALL METHOD : go_receipt_tree->free, go_receipt_container->free.

      FREE : go_receipt_tree, go_receipt_container.

      PERFORM count_num.

      PERFORM display_receipt_tree.

    WHEN '0102'. " stlo tree refresh

      " free tree object
      CALL METHOD : go_stlo_tree->free, go_stlo_cont->free.

      FREE : go_stlo_tree, go_stlo_cont.

      PERFORM count_stlo_num.

      PERFORM display_stlo_tree_screen.
  ENDCASE.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
