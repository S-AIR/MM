```abap
*&---------------------------------------------------------------------*
*& Include          ZC103MMR0004F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form get_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_main_data .

  DATA : lv_tabix TYPE sy-tabix.

  " 재고 정보 가져오기
  SELECT stoid matid plnid strid available_stock unit
    INTO CORRESPONDING FIELDS OF TABLE gt_stock_backup
    FROM zc103mmt0006
   ORDER BY plnid strid matid ASCENDING.

  " 구매 오더 (아이템) 정보 가져오기
  SELECT matid plnid strid quantity unit order_status
    INTO CORRESPONDING FIELDS OF TABLE gt_po
    FROM zc103mmt0011 AS a
   INNER JOIN zc103mmt0012 AS b
      ON a~poid EQ b~poid
   WHERE order_status = 'B' OR
         order_status = 'C'.

  " 정비 출고 예정 수량
  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE gt_pm_bom
    FROM zc103pmt0002.

  " 승인상태인 구매오더 아이템의 수량을 더한다 (MATID, PLNID, STRID가 같은 경우만)
  " 곧 들어올 자재 수량
  LOOP AT gt_po INTO gs_po.


    READ TABLE gt_stock_backup INTO gs_stock WITH KEY matid = gs_po-matid
                                                      plnid = gs_po-plnid
                                                      strid = gs_po-strid.
        lv_tabix = sy-tabix. " gs_stock의 인덱스

    IF gs_stock IS INITIAL.
      CONTINUE.
    ELSEIF lv_tabix EQ 0.
      CONTINUE.
    ELSE.
      gs_stock-po_quantity += gs_po-quantity.
      MODIFY gt_stock_backup FROM gs_stock INDEX lv_tabix TRANSPORTING po_quantity.
    ENDIF.

  ENDLOOP.

  gt_stock = CORRESPONDING #( gt_stock_backup ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_master_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_master_data .

  " 플랜트정보
  SELECT plnid name
    INTO CORRESPONDING FIELDS OF TABLE gt_plnt
    FROM zc103mmt0003.

  " 스토리지로케이션 정보
  SELECT plnid strid name
    INTO CORRESPONDING FIELDS OF TABLE gt_stlo
    FROM zc103mmt0004
   ORDER BY plnid strid ASCENDING.

  " 자재 정보
  SELECT matid matname matgrp is_serial rpoint price currency
    INTO CORRESPONDING FIELDS OF TABLE gt_mara
    FROM zc103mmt0001.

  " 트리 정보 세팅
  LOOP AT gt_stlo INTO gs_stlo.

    READ TABLE gt_plnt INTO gs_plnt WITH KEY plnid = gs_stlo-plnid.

    gs_tr_plnt =  VALUE #( plnid = gs_plnt-plnid pname = gs_plnt-name
                           strid = gs_stlo-strid sname = gs_stlo-name ).

    APPEND gs_tr_plnt TO gt_tr_plnt.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form count_num
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM count_num .

  " 수량을 세는 서브루틴
  CLEAR : gv_total_cnt, gv_suppl_cnt, gv_ok_cnt, gv_warning_cnt, gv_error_cnt.

  " 조건에 따른 수량 세기
  gv_total_cnt = lines( gt_stock ).

  LOOP AT gt_stock INTO gs_stock.

    " 안전재고 수량

    IF gs_stock-available_stock + gs_stock-po_quantity > gs_stock-rpoint.

      gv_ok_cnt += 1.

    ELSEIF gs_stock-available_stock + gs_stock-po_quantity EQ gs_stock-rpoint.

      gv_warning_cnt += 1.

    ELSE.

      gv_error_cnt += 1.

    ENDIF.

  ENDLOOP.

  gv_suppl_cnt = gv_error_cnt.

  PERFORM set_screen.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_text_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_text_data .

  DATA : lv_tabix TYPE sy-tabix,
         lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v.

  " 자재그룹명
  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_MATGRP'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  " 조회정보 설정
  gv_today = sy-datum.
  gv_current_time = sy-uzeit.

  " 플랜트명, 스토리지로케이션 명 설정
  LOOP AT gt_stock_backup INTO gs_stock.

    lv_tabix = sy-tabix.

    " 플랜트명 설정
    READ TABLE gt_plnt INTO gs_plnt WITH KEY plnid = gs_stock-plnid.
    gs_stock-pname = gs_plnt-name.

    " 저장위치명 설정
    READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_stock-strid.
    gs_stock-sname = gs_stlo-name.

    " 자재명, 가용재고값 설정
    READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_stock-matid.
    gs_stock-matname = gs_mara-matname.
    gs_stock-is_serial = gs_mara-is_serial.
    gs_stock-rpoint = gs_mara-rpoint.
    gs_stock-matgrp = gs_mara-matgrp.

    " 자재그룹명 설정
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_mara-matgrp.
    gs_stock-matgrp_text = ls_dd07v-ddtext.

    CASE gs_stock-is_serial.
      WHEN 'Y'.
        gs_stock-celltab = VALUE #( ( fieldname = 'SERIAL' style = cl_gui_alv_grid=>mc_style_button ) ).
        gs_stock-serial = '조회하기'.
      WHEN 'N'.
        gs_stock-serial = '시리얼없음'.
    ENDCASE.

    " 가용재고 상태에 따른 색 설정, 상태 아이콘 설정
    IF gs_stock-available_stock + gs_stock-po_quantity < gs_stock-rpoint.

      gs_stock-color = VALUE #( BASE gs_stock-color
                                     ( fname = 'AVAILABLE_STOCK'
                                       color = VALUE #( col = 6 int = 1 ) )
                                     ( fname = 'PO_QUANTITY'
                                       color = VALUE #( col = 6 int = 1 ) )
                                     ( fname = 'EXPECTED_STOCK'
                                       color = VALUE #( col = 6 int = 1 ) )
                                     ( fname = 'RPOINT'
                                       color = VALUE #( col = 6 int = 1 ) )
                                     ( fname = 'OUT_QUANTITY'
                                       color = VALUE #( col = 6 int = 1 ) ) ).
      gs_stock-status_icon = icon_led_red.

    ELSEIF gs_stock-available_stock + gs_stock-po_quantity EQ gs_stock-rpoint.

      gs_stock-color = VALUE #( BASE gs_stock-color
                                     ( fname = 'AVAILABLE_STOCK'
                                       color = VALUE #( col = 3 int = 0 ) )
                                     ( fname = 'PO_QUANTITY'
                                       color = VALUE #( col = 3 int = 0 ) )
                                     ( fname = 'EXPECTED_STOCK'
                                       color = VALUE #( col = 3 int = 0 ) )
                                     ( fname = 'RPOINT'
                                       color = VALUE #( col = 3 int = 0 ) )
                                     ( fname = 'OUT_QUANTITY'
                                       color = VALUE #( col = 3 int = 0 ) ) ).
      gs_stock-status_icon = icon_led_yellow.

    ELSE.

      gs_stock-color = VALUE #( BASE gs_stock-color
                                     ( fname = 'AVAILABLE_STOCK'
                                       color = VALUE #( col = 5 int = 0 ) )
                                     ( fname = 'PO_QUANTITY'
                                       color = VALUE #( col = 5 int = 0 ) )
                                     ( fname = 'EXPECTED_STOCK'
                                       color = VALUE #( col = 5 int = 0 ) )
                                     ( fname = 'RPOINT'
                                       color = VALUE #( col = 5 int = 0 ) )
                                     ( fname = 'OUT_QUANTITY'
                                       color = VALUE #( col = 5 int = 0 ) ) ).
      gs_stock-status_icon = icon_led_green.

    ENDIF.

    " 출고예정수량 값 설정
    READ TABLE gt_pm_bom INTO gs_pm_bom WITH KEY matid = gs_stock-matid.
    IF gs_pm_bom IS NOT INITIAL.
      gs_stock-out_quantity = gs_pm_bom-qty.
    ENDIF.


    " 예상가용재고 값 설정
    gs_stock-expected_stock = gs_stock-available_stock + gs_stock-po_quantity - gs_stock-out_quantity.

    MODIFY gt_stock_backup FROM gs_stock INDEX lv_tabix TRANSPORTING pname sname matname matgrp is_serial rpoint status_icon
                                                                     celltab serial color matgrp_text expected_stock out_quantity.

  ENDLOOP.

  gt_stock = CORRESPONDING #( gt_stock_backup ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form init_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_screen .

  IF go_main_cont IS NOT BOUND.

    PERFORM create_object.
    PERFORM register_tree_events.
    SET HANDLER : lcl_event_handler=>handle_node_double_click FOR go_tree.
    PERFORM build_node.

    CALL METHOD go_tree->add_nodes
      EXPORTING
        table_structure_name = 'MTREESNODE'
        node_table           = node_table.

*    CALL METHOD go_tree->expand_node
*      EXPORTING
*        node_key = 'ROOT1'.

    PERFORM set_fcat_and_layo.

    SET HANDLER : lcl_event_handler=>handle_cell_btn_click FOR go_alv_grid,
                  lcl_event_handler=>handle_edit_toolbar   FOR go_alv_grid,
                  lcl_event_handler=>handle_user_command   FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_vari
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layo
      CHANGING
        it_outtab       = gt_stock
        it_fieldcatalog = gt_fcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_main_cont
    EXPORTING
      container_name = 'MAIN_CONT'.

  CREATE OBJECT go_split_cont
    EXPORTING
      parent  = go_main_cont
      rows    = 1
      columns = 2.

  go_split_cont->get_container(
    EXPORTING row = 1 column = 1
    RECEIVING container = go_left_cont ).

  go_split_cont->get_container(
    EXPORTING row = 1 column = 2
    RECEIVING container = go_right_cont ).

  go_split_cont->set_column_width( EXPORTING id = 1 width = 30 ).

  CREATE OBJECT go_tree
    EXPORTING
      parent              = go_left_cont
      node_selection_mode = cl_gui_simple_tree=>node_sel_mode_single.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_right_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_tree_events
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_tree_events .

  " 노드 이벤트 등록
  events = VALUE #( ( eventid = cl_gui_simple_tree=>eventid_node_double_click
                      appl_event = abap_true ) ).

  CALL METHOD go_tree->set_registered_events
    EXPORTING
      events = events.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form build_node
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM build_node .

  " 노드 데이터 생성
  DATA : node TYPE mtreesnode.

  " 전체조회칸
  node-node_key = 'RTTO'.
  node-text = TEXT-t01.
  node-isfolder = abap_false.
  APPEND node TO node_table.
  CLEAR node.

  " 저장위치별 조회
  node-node_key = 'RTPL'.
  node-text = TEXT-t02.
  node-isfolder = abap_true.
  APPEND node TO node_table.
  CLEAR node.

  LOOP AT gt_tr_plnt INTO gs_tr_plnt.

    ON CHANGE OF gs_tr_plnt-plnid.

      node-node_key = gs_tr_plnt-plnid.
      node-text = gs_tr_plnt-pname.
      node-relatkey = 'RTPL'.
      node-isfolder = abap_true.
      APPEND node TO node_table.
      CLEAR node.

    ENDON.

    node-node_key = gs_tr_plnt-strid.
    node-text = gs_tr_plnt-sname.
    node-relatkey = gs_tr_plnt-plnid.
    node-isfolder = abap_false.
    APPEND node TO node_table.
    CLEAR node.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_node_double_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_node_double_click USING node_key .

  " 더블클릭한 노드로 필터링하는 서브루틴

  DATA : lv_prefix(4),
         lv_cnt TYPE i.
  lv_prefix = node_key(4).

  CLEAR gt_stock.

  CASE lv_prefix.
    WHEN 'RTTO'. " 전체조회 더블클릭
      gt_stock = CORRESPONDING #( gt_stock_backup ).
      gv_title = |재고조회 : 전체조회|.
    WHEN 'RTPL'. " 저장위치별 조회 더블클릭
      gt_stock = CORRESPONDING #( gt_stock_backup ).
      gv_title = |재고조회 : 전체조회|.
    WHEN 'PLNT'. " 플랜트명
      READ TABLE gt_tr_plnt INTO gs_tr_plnt WITH KEY plnid = node_key.
      gt_stock = VALUE #( FOR ls_stock IN gt_stock_backup WHERE ( plnid EQ node_key ) ( ls_stock ) ).
      gv_title = |재고조회 : { gs_tr_plnt-pname }|.
    WHEN 'STLO'. " 스토리지로케이션명
      READ TABLE gt_tr_plnt INTO gs_tr_plnt WITH KEY strid = node_key.
      gt_stock = VALUE #( FOR ls_stock IN gt_stock_backup WHERE ( strid EQ node_key ) ( ls_stock ) ).
      gv_title = |재고조회 : { gs_tr_plnt-pname } - { gs_tr_plnt-sname }|.
  ENDCASE.

  lv_cnt = lines( gt_stock ).
  MESSAGE s008 WITH lv_cnt.

  PERFORM count_num.

  PERFORM refresh_table USING go_alv_grid.
  PERFORM refresh_layout USING go_alv_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_fcat_and_layo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_fcat_and_layo .

  " fcat
  gt_fcat = VALUE #(
    ( key = abap_true fieldname = 'STOID'   ref_table = 'ZC103MMT0006' just = 'C' )
    ( key = abap_true fieldname = 'STATUS_ICON'   coltext = '재고상태' just = 'C' )
                    ( fieldname = 'MATID'   ref_table = 'ZC103MMT0006' just = 'C' emphasize = abap_true )
                    ( fieldname = 'MATNAME' ref_table = 'ZC103MMT0001' )
                    ( fieldname = 'MATGRP_TEXT'  ref_table = 'ZC103MMT0001' coltext = '자재그룹' )
                    ( fieldname = 'PLNID'   ref_table = 'ZC103MMT0006' just = 'C' emphasize = abap_true )
                    ( fieldname = 'PNAME'   coltext = '플랜트명' )
                    ( fieldname = 'STRID'   ref_table = 'ZC103MMT0006' just = 'C' emphasize = abap_true )
                    ( fieldname = 'SNAME'   coltext = '저장위치명' )
                    ( fieldname = 'AVAILABLE_STOCK' ref_table = 'ZC103MMT0006'  coltext = '가용재고' qfieldname = 'UNIT' )
                    ( fieldname = 'PO_QUANTITY' coltext = '입고예정수량' qfieldname = 'UNIT' )
                    ( fieldname = 'EXPECTED_STOCK' coltext = '예상가용재고' qfieldname = 'UNIT' )
                    ( fieldname = 'RPOINT' coltext = '재주문점' qfieldname = 'UNIT' )
                    ( fieldname = 'OUT_QUANTITY' coltext = '출고예정수량' qfieldname = 'UNIT' decimals_o = '1' )
                    ( fieldname = 'UNIT'    coltext = '단위' emphasize = abap_true just = 'C'  )
                    ( fieldname = 'IS_SERIAL' coltext = '시리얼자재' just = 'C' )
                    ( fieldname = 'SERIAL'    coltext = '시리얼조회' just = 'C' ) ).

  " layo
  gs_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'D' grid_title = '재고조회 : 전체조회' stylefname = 'CELLTAB'
                     ctab_fname = 'COLOR' ).
  gs_vari = VALUE #( report = sy-repid handle = 'ALV1' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GO_ALV_GRID
*&---------------------------------------------------------------------*
FORM refresh_table  USING    po_alv_grid TYPE REF TO cl_gui_alv_grid.

  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable = VALUE #( row = abap_true col = abap_true ).

  CALL METHOD po_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GO_ALV_GRID
*&---------------------------------------------------------------------*
FORM refresh_layout  USING    po_alv_grid TYPE REF TO cl_gui_alv_grid.

  gs_layo = VALUE #( BASE gs_layo grid_title = gv_title ).
  po_alv_grid->set_frontend_layout( EXPORTING is_layout = gs_layo ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_cell_btn_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> ES_ROW_NO
*&---------------------------------------------------------------------*
FORM handle_cell_btn_click  USING ps_row_no TYPE lvc_s_roid.

  " 시리얼을 조회하는 버튼

  DATA : lv_result TYPE i.

  " 클릭된 행의 데이터 읽어오기
  READ TABLE gt_stock INTO gs_stock INDEX ps_row_no-row_id.

  " 시리얼 조회 페이지 호출
  CALL FUNCTION 'ZC1FM03MM0003'
    EXPORTING
      iv_matid  = gs_stock-matid
      iv_plnid  = gs_stock-plnid
      iv_strid  = gs_stock-strid
      iv_select = abap_false
    IMPORTING
      ev_result = lv_result.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar  USING    po_object TYPE REF TO cl_alv_event_toolbar_set
                                   po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.
    WHEN go_alv_grid.
      PERFORM stock_edit_toolbar USING po_object.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form stock_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PO_OBJECT
*&---------------------------------------------------------------------*
FORM stock_edit_toolbar  USING    po_object TYPE REF TO cl_alv_event_toolbar_set.

  po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar
                                   ( function = 'RPIV' icon = icon_change text = ' 재고보충' ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM handle_user_command  USING    pv_ucomm
                                   po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.
    WHEN go_alv_grid.
      PERFORM stock_user_command USING pv_ucomm.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form stock_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PV_UCOMM
*&---------------------------------------------------------------------*
FORM stock_user_command  USING    pv_ucomm.

  CASE pv_ucomm.
    WHEN 'RPIV'. " 재고보충버튼
      PERFORM reorder_stock.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form reorder_stock
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM reorder_stock .

  " 재주문점보다 낮은 가용재고를 가진 재고의 구매요청을 생성하는 서브루틴
  " 가용재고 + 입고예정수량 < 재주문점인 경우에 구매요청을 생성함
  " 정비부품, 기내유료상품, 어매니티, 기내식 별로 별도의 구매요청을 만듦
  " CBP이므로 생성되는 구매요청은 전부 오더까지 승인된 상태로 만든다.

  DATA : lt_roid     TYPE lvc_t_roid, " 선택 행 조회
         ls_roid     TYPE lvc_s_roid,
         lt_stock    LIKE TABLE OF gs_stock, " 재고 행 임시 저장
         lv_tabix    TYPE sy-tabix,
         lv_tomorrow TYPE sy-datum.  " 납품요청일 (내일날짜)

  DATA : lv_empno       TYPE zc103mmt0007-creator, " 구매요청 생성자
         ls_prhead_part TYPE zc103mmt0007. " 정비부품 구매요청 헤더

  DATA : lt_pritem_part TYPE TABLE OF zc103mmt0008, " 정비부품 구매요청 아이템
         ls_pritem_part TYPE zc103mmt0008,
         lv_quantity    TYPE zc103mmt0006-available_stock, " 필요 부품 계산용
         lv_prid        TYPE zc103mmt0007-prid. " 구매요청번호


  " 선택된 행을 가져온다
  CALL METHOD go_alv_grid->get_selected_rows( IMPORTING et_row_no = lt_roid ).

  " 선택된 재고를 lt_stock에 담기 (가용재고가 부족한 것만 담음)
  LOOP AT lt_roid INTO ls_roid.

    lv_tabix = sy-tabix.

    READ TABLE gt_stock INTO gs_stock INDEX ls_roid-row_id.
    IF ( gs_stock-expected_stock < gs_stock-rpoint ).

      APPEND gs_stock TO lt_stock.

    ENDIF.

  ENDLOOP.

  " 부족 재고가 없다면 종료
  IF lt_stock IS INITIAL.

    CLEAR lv_empno.
    MESSAGE s067 DISPLAY LIKE 'E'.
    RETURN.

  ENDIF.

  " 선택된 행이 없을 경우 오류 메시지를 출력한다
  IF lt_roid IS INITIAL.

    MESSAGE s004 DISPLAY LIKE 'E'.
    RETURN.

  ENDIF.

  " CBP 담당자 정보 입력
  CALL FUNCTION 'ZC103PMFG0001'
    IMPORTING
      empno = lv_empno.

  IF lv_empno IS INITIAL.

    CLEAR lv_empno.
    MESSAGE s017 DISPLAY LIKE 'E'.
    RETURN.

  ENDIF.

  " 내일 날짜 받아오기
  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      date      = sy-datum
      days      = 1
      months    = 0
      years     = 0
    IMPORTING
      calc_date = lv_tomorrow.

  " 헤더 정보 생성 (기내식, 기내유료상품, 어매니티는 CBP가 아니므로 뺐음)
  " 1. 정비부품 구매요청 헤더
  ls_prhead_part = VALUE #( plnid = 'PLNT0001' strid = 'STLO0001' pr_status = 'C'
                            creator = lv_empno approval = lv_empno
                            estkz = 'C' request_date = sy-datum   receive_date = lv_tomorrow ).

  " 구매요청 아이템 정보 생성
  CLEAR gs_stock.
  LOOP AT lt_stock INTO gs_stock.

    CASE gs_stock-matgrp.
      WHEN 'AA' OR 'AB' OR 'AC' OR 'AD'. " 부품인 경우

        CLEAR : ls_pritem_part, lv_quantity, gs_mara.

        lv_quantity = gs_stock-rpoint - gs_stock-expected_stock. " 현재 필요 수량
        lv_quantity = ceil( lv_quantity ). " 소숫점 있는경우 올림 처리


        " 자재마스터에서 금액, 통화 정보 가져온다
        READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_stock-matid.

        ls_pritem_part = VALUE #( matid = gs_stock-matid quantity = lv_quantity unit = gs_stock-unit
                                  price = gs_mara-price * lv_quantity currency = gs_mara-currency ).

        APPEND ls_pritem_part TO lt_pritem_part.

    ENDCASE.

  ENDLOOP.

  IF lt_pritem_part IS INITIAL.

    CLEAR lv_empno.
    MESSAGE s067 DISPLAY LIKE 'E'.
    RETURN.

  ENDIF.

  " 구매요청 생성
  CALL FUNCTION 'ZC103MMFG0001'
    EXPORTING
      is_header = ls_prhead_part
      it_item   = lt_pritem_part
    IMPORTING
      ev_prid   = lv_prid.

  " 구매오더 전환
  CALL FUNCTION 'ZC103MMFG0003'
    EXPORTING
      iv_approval = lv_empno
      iv_prid     = lv_prid
      iv_vendorid = 'VD000001'.


  " 리프레시
  PERFORM get_main_data.
  PERFORM set_text_data.
  PERFORM refresh_alv USING go_alv_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_alv
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GO_ALV_GRID
*&---------------------------------------------------------------------*
FORM refresh_alv  USING    po_alv_grid TYPE REF TO cl_gui_alv_grid.

  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable = VALUE #( row = abap_true col = abap_true ).

  CALL METHOD po_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form apply_number_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM apply_number_screen .

  " 수량 값 설정

  DATA : lt_dynp  TYPE TABLE OF dynpread WITH HEADER LINE.

  lt_dynp-fieldname   = 'GV_TOTAL_CNT'.
  lt_dynp-fieldvalue  = gv_total_cnt.
  APPEND lt_dynp.

  CALL FUNCTION 'DYNP_VALUES_UPDATE'
    EXPORTING
      dyname     = sy-repid
      dynumb     = sy-dynnr
    TABLES
      dynpfields = lt_dynp.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_screen .

  DATA : lt_dynp  TYPE TABLE OF dynpread WITH HEADER LINE.

*-- Set employee info
  lt_dynp-fieldname   = 'GV_TOTAL_CNT'.
  lt_dynp-fieldvalue  = gv_total_cnt.
  APPEND lt_dynp.

  lt_dynp-fieldname   = 'GV_SUPPL_CNT'.
  lt_dynp-fieldvalue  = gv_suppl_cnt.
  APPEND lt_dynp.

  CALL FUNCTION 'DYNP_VALUES_UPDATE'
    EXPORTING
      dyname     = sy-repid
      dynumb     = sy-dynnr
    TABLES
      dynpfields = lt_dynp.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
