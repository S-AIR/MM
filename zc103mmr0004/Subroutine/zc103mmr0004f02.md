```abap
*&---------------------------------------------------------------------*
*& Include          ZC103MMR0004F02
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form refresh_grid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_grid .

  PERFORM get_main_data.

  PERFORM set_text_data.

  PERFORM refresh_table USING go_alv_grid.

  PERFORM count_num.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_batch
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_batch .
  " 매일 자정에 실행되는 재고보충 CBP 프로그램
  " 재주문점보다 낮은 가용재고를 가진 재고의 구매요청을 생성하는 서브루틴
  " 가용재고 + 입고예정수량 < 재주문점인 경우에 구매요청을 생성함
  " 정비부품, 기내유료상품, 어매니티, 기내식 별로 별도의 구매요청을 만듦
  " CBP이므로 생성되는 구매요청은 전부 오더까지 승인된 상태로 만든다.

  DATA : lt_roid     TYPE lvc_t_roid, " 선택 행 조회
         ls_roid     TYPE lvc_s_roid,
         lt_stock    LIKE TABLE OF gs_stock, " 재고 행 임시 저장
         lv_tabix    TYPE sy-tabix,
         lv_tomorrow TYPE sy-datum.  " 납품요청일 (내일날짜)

  DATA : lv_empno       TYPE zc103mmt0007-creator, " 구매요청 생성자
         ls_prhead_part TYPE zc103mmt0007. " 정비부품 구매요청 헤더

  DATA : lt_pritem_part TYPE TABLE OF zc103mmt0008, " 정비부품 구매요청 아이템
         ls_pritem_part TYPE zc103mmt0008,
         lv_quantity    TYPE zc103mmt0006-available_stock, " 필요 부품 계산용
         lv_prid        TYPE zc103mmt0007-prid. " 구매요청번호

  " 재고가 부족한 자재를 lt_stock에 담는다
  LOOP AT gt_stock INTO gs_stock.

    IF gs_stock-expected_stock < gs_stock-rpoint.

      APPEND gs_stock TO lt_stock.

    ENDIF.

  ENDLOOP.

  " 부족 재고가 없다면 종료
  IF lt_stock IS INITIAL.

    CLEAR lv_empno.
    MESSAGE s067 DISPLAY LIKE 'E'.
    RETURN.

  ENDIF.

  " 내일 날짜 받아오기
  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      date      = sy-datum
      days      = 1
      months    = 0
      years     = 0
    IMPORTING
      calc_date = lv_tomorrow.

  " 헤더 정보 생성 (기내식, 기내유료상품, 어매니티는 CBP가 아니므로 뺐음)
  " 1. 정비부품 구매요청 헤더
  ls_prhead_part = VALUE #( plnid = 'PLNT0001' strid = 'STLO0001' pr_status = 'C'
                            creator = lv_empno approval = lv_empno
                            estkz = 'C' request_date = sy-datum   receive_date = lv_tomorrow ).

  " 구매요청 아이템 정보 생성
  CLEAR gs_stock.
  LOOP AT lt_stock INTO gs_stock.

    CASE gs_stock-matgrp.
      WHEN 'AA' OR 'AB' OR 'AC' OR 'AD'. " 부품인 경우

        CLEAR : ls_pritem_part, lv_quantity, gs_mara.

        lv_quantity = gs_stock-rpoint - gs_stock-expected_stock. " 현재 필요 수량
        lv_quantity = ceil( lv_quantity ). " 소숫점 있는경우 올림 처리


        " 자재마스터에서 금액, 통화 정보 가져온다
        READ TABLE gt_mara INTO gs_mara WITH KEY matid = gs_stock-matid.

        ls_pritem_part = VALUE #( matid = gs_stock-matid quantity = lv_quantity unit = gs_stock-unit
                                  price = gs_mara-price * lv_quantity currency = gs_mara-currency ).

        APPEND ls_pritem_part TO lt_pritem_part.

    ENDCASE.

  ENDLOOP.

  IF lt_pritem_part IS INITIAL.

    RETURN.

  ENDIF.

  " 구매요청 생성
  CALL FUNCTION 'ZC103MMFG0001'
    EXPORTING
      is_header = ls_prhead_part
      it_item   = lt_pritem_part
    IMPORTING
      ev_prid   = lv_prid.

  " 구매오더 전환
  CALL FUNCTION 'ZC103MMFG0003'
    EXPORTING
      iv_approval = lv_empno
      iv_prid     = lv_prid
      iv_vendorid = 'VD000001'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_plane_delivery
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_plane_delivery .

  " 자정에 항공기 판매출고를 진행한다

  DATA : lt_sche TYPE TABLE OF zc103sdt0008,
         ls_sche TYPE zc103sdt0008.

  "
  DATA : BEGIN OF ls_poit.
           INCLUDE STRUCTURE zc103mmt0008.
  DATA :     plnid TYPE zc103mmt0007-plnid,
           strid TYPE zc103mmt0007-strid,
         END OF ls_poit,
         lt_sche_poit LIKE TABLE OF ls_poit.

  " 출고용
  DATA : lt_norm_mat TYPE zc103mmtt0007, " 일반창고
         lt_frsh_mat TYPE zc103mmtt0007, " 신선창고
         ls_mat      TYPE zc103mms0008,
         lv_plnid    TYPE zc103mmt0003-plnid,
         lv_strid    TYPE zc103mmt0004-strid.

  DATA : lv_msg(30).

  " 오늘 날짜의 항공기 스케줄 가져오기
  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE lt_sche
    FROM zc103sdt0008
   WHERE departdate = sy-datum.    " 임시로 22일꺼, 실제 프로그램에서는 sy-datum으로 변경

  LOOP AT lt_sche INTO ls_sche.

    " 해당 스케줄 아이디로 구매요청 아이템 가져오기
    CLEAR : lt_sche_poit.

    SELECT a~prid matid quantity unit price currency b~plnid b~strid
    INTO CORRESPONDING FIELDS OF TABLE lt_sche_poit
    FROM zc103mmt0008 AS a
   INNER JOIN zc103mmt0007 AS b
      ON a~prid EQ b~prid
   WHERE b~scheduleid = ls_sche-scheduleid.

    " 구매요청 스토리지별 분류
    CLEAR : ls_poit.
    LOOP AT lt_sche_poit INTO ls_poit.

      CLEAR : ls_mat.
      ls_mat = VALUE #( matid = ls_poit-matid
                     quantity = ls_poit-quantity
                     unit = ls_poit-unit ).

      CASE ls_poit-strid.
        WHEN 'STLO0002'.
          APPEND ls_mat TO lt_norm_mat.
        WHEN 'STLO0003'.
          APPEND ls_mat TO lt_frsh_mat.
      ENDCASE.

    ENDLOOP.

  ENDLOOP.

  " 일반창고 판매출고 진행
  CALL FUNCTION 'ZC1FM03MM0002'
    EXPORTING
      iv_plnid        = 'PLNT0002'
      iv_strid        = 'STLO0002'
      iv_movementtype = '601'
      it_mat          = lt_norm_mat
    IMPORTING
      ev_message      = lv_msg.

  " 신선창고 판매충고 진행
  CALL FUNCTION 'ZC1FM03MM0002'
    EXPORTING
      iv_plnid        = 'PLNT0002'
      iv_strid        = 'STLO0003'
      iv_movementtype = '601'
      it_mat          = lt_frsh_mat
    IMPORTING
      ev_message      = lv_msg.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
