```abap

*&---------------------------------------------------------------------*
*& Include          ZC103MMR0001F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form get_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
**********************************************************************
* 데이터 조회
**********************************************************************
FORM get_data .

*-- 구매요청 헤더 + 아이템 데이터 가져오기
  CLEAR : gt_body.

  SELECT a~prid plnid strid mrpid pr_status estkz
         request_date receive_date b~matid quantity
         b~unit b~price currency scheduleid
         creator create_time create_date
         approval approve_date approve_time pritemid
         b~erdat b~erzet b~ernam b~aedat b~aezet b~aenam
    INTO CORRESPONDING FIELDS OF TABLE gt_back
    FROM zc103mmt0007 AS a
   INNER JOIN zc103mmt0008 AS b
      ON a~prid EQ b~prid
   WHERE a~prid IN so_prid
     AND a~scheduleid IN so_schid
     AND request_date IN so_reqdt
     AND receive_date IN so_recdt.

  gt_body = CORRESPONDING #( gt_back ).
  SORT gt_body BY pr_status ASCENDING.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_textdata_and_style
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
**********************************************************************
* 자재이름, 스타일필드 세팅
**********************************************************************
FORM set_textdata.

*-- 루프를 돌면서 세팅해야하는 데이터 세팅
  " 1. 자재 이름, 플랜트 이름, 저장위치 이름 세팅
  " 2. 구매요청 상태 텍스트 세팅

  DATA : lv_tabix     TYPE sy-tabix,        " 루프용 인덱스
         lt_dd07v     TYPE TABLE OF dd07v,  " 구매요청상태 도메인 텍스트 값 저장 itab
         ls_dd07v     TYPE dd07v,
         lt_dd07vm    TYPE TABLE OF dd07v,  " 생성지시자 도메인 텍스트 값
         ls_dd07vm    TYPE dd07v,
         lt_emp_value LIKE gt_emp_value.

  PERFORM get_text_data.

  " 구매요청상태 텍스트 (도메인 값) 가져오기
  PERFORM get_domain_text USING 'ZC103D_MM_PRSTATUS' 'X' CHANGING lt_dd07v.
  PERFORM get_domain_text USING 'ZC103D_MM_ESTKZ'    'X' CHANGING lt_dd07vm.

  " 서치헬프값 세팅
  lt_emp_value = CORRESPONDING #( gt_emp ).
  gt_emp_value = VALUE #( FOR ls_emp_value IN lt_emp_value
                          WHERE ( dptcode = 'MM' ) ( ls_emp_value ) ).

  LOOP AT gt_body INTO gs_body.

    lv_tabix = sy-tabix.

    " 자재이름, 플랜트이름  세팅
    READ TABLE gt_text INTO gs_text WITH KEY matid = gs_body-matid.

    IF gs_text IS NOT INITIAL.
      gs_body-matname = gs_text-matname.
      gs_body-pname   = gs_text-pname.
    ENDIF.
    CLEAR gs_text.

    " 항공스케줄아이디가 없으면 해당없음으로 세팅
    IF gs_body-scheduleid IS INITIAL.
      gs_body-scheduleid = '해당없음'.
    ENDIF.

*     저장위치명 세팅
    READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_body-strid.
    IF gs_stlo IS NOT INITIAL.
      gs_body-sname = gs_stlo-name.
    ENDIF.
    CLEAR gs_stlo.

    " 구매요청상태 텍스트 세팅
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_body-pr_status.
    gs_body-status_text = ls_dd07v-ddtext.
    CLEAR ls_dd07v.

    " 생성지시자 텍스트 세팅
    READ TABLE lt_dd07vm INTO ls_dd07vm WITH KEY domvalue_l = gs_body-estkz.
    gs_body-estkz_text = ls_dd07vm-ddtext.
    CLEAR ls_dd07vm.

    " 직원 정보 세팅
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_body-creator.
    gs_body-creator_name = gs_emp-empname.
    CLEAR gs_emp.
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_body-approval.
    gs_body-approve_name = gs_emp-empname.
    CLEAR gs_emp.

    " 스타일필드 세팅 (승인 대기 상태일때만)
    " 결재 버튼, 수정 버튼 생성
    IF gs_body-pr_status EQ 'A'.
      gs_body-celltab = VALUE #(
        ( fieldname = 'APPROVE' style = cl_gui_alv_grid=>mc_style_button )
        ( fieldname = 'UPDATE ' style = cl_gui_alv_grid=>mc_style_button ) ).
      gs_body-approve = '결재하기'.
      gs_body-update  = '수정하기'.
      gs_body-icon    = icon_led_yellow.  " 신호등 파란색
    ELSE.
      gs_body-celltab = VALUE #( ( fieldname = 'APPROVE' style = cl_gui_alv_grid=>mc_style_disabled ) ).
      gs_body-icon    = icon_led_green. " 신호등 주황색
      gs_body-approve = ''.
      gs_body-update  = ''.
    ENDIF.

    MODIFY gt_body FROM gs_body INDEX lv_tabix TRANSPORTING matname status_text icon scheduleid
                                pname sname celltab approve creator_name approve_name estkz_text update.

  ENDLOOP.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_text_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_text_data .

  DATA : lv_cnt TYPE i.

  CLEAR : gt_prhdt.
  SELECT prid pr_status
    INTO CORRESPONDING FIELDS OF TABLE gt_prhdt
    FROM zc103mmt0007
   WHERE prid IN so_prid
     AND scheduleid IN so_schid
     AND request_date IN so_reqdt
     AND receive_date IN so_recdt.

  lv_cnt = lines( gt_prhdt ).
  MESSAGE s008 WITH lv_cnt.

  " 자재이름 가져오기
  CLEAR : gt_text, gs_body.
  SELECT matid matname price currency base_unit b~plnid b~name AS pname lotsize
    INTO CORRESPONDING FIELDS OF TABLE gt_text
    FROM zc103mmt0001 AS a INNER JOIN zc103mmt0003 AS b
    ON a~plnid EQ b~plnid.

*  " 스토리지 로케이션 이름 가져오기
  CLEAR : gs_stlo, gt_stlo.
  SELECT plnid strid name
    INTO CORRESPONDING FIELDS OF TABLE gt_stlo
    FROM zc103mmt0004.

  " 직원 정보 가져오기
  CLEAR : gs_emp, gt_emp.
  SELECT empno empname dptcode
    INTO CORRESPONDING FIELDS OF TABLE gt_emp
    FROM zc103fit0011.
  SORT gt_emp BY empno.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_search_help_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_search_help_data .
*-- 서치헬프 테이블에 데이터를 넣는다
  CLEAR : gt_sh_matid, gs_text.
  LOOP AT gt_text INTO gs_text.

    gs_sh_matid = VALUE #( matid = gs_text-matid
                           matname = gs_text-matname ).

    APPEND gs_sh_matid TO gt_sh_matid.

    CLEAR : gs_sh_matid.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
**********************************************************************`
* 메인 화면 (100번 스크린)
**********************************************************************
FORM display_screen .

  IF go_main_cont IS NOT BOUND.

    " 필드 카탈로그 설정
    CLEAR : gs_fcat, gt_fcat.
    PERFORM set_field_catalog.
    " 레이아웃, variant 설정
    PERFORM set_layout_and_vari_and_sort.
    " 오브젝트 생성
    PERFORM create_object.
    PERFORM exclude_toolbar.
    PERFORM set_handler.
    SET HANDLER : lcl_event_handler=>top_of_page FOR go_alv_grid.

    CALL METHOD go_alv_grid->list_processing_events " 특정 이벤트의 처리 시점을 지정하는 메소드
      EXPORTING
        i_event_name = 'TOP_OF_PAGE'
        i_dyndoc_id  = go_dyndoc_id.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_vari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_layo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_body
        it_fieldcatalog      = gt_fcat
        it_sort              = gt_sort.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog.

  "" 필드카탈로그 설정
  gt_fcat = VALUE #(
              ( fieldname = 'ICON'         coltext = '상태' icon = abap_true just = 'C' )
    ( key = 'X' fieldname = 'PRID'            ref_table = 'ZC103MMT0007' just = 'C' )
              ( fieldname = 'SCHEDULEID'      ref_table = 'ZC103MMT0007' just = 'C' )
              ( fieldname = 'PLNID'           ref_table = 'ZC103MMT0007' just = 'C' )
              ( fieldname = 'STRID'           ref_table = 'ZC103MMT0007' just = 'C' )
              ( fieldname = 'PNAME'           ref_table = 'ZC103MMT0003' coltext = '플랜트명'  emphasize = abap_true )
              ( fieldname = 'SNAME'           ref_table = 'ZC103MMT0004' coltext = '저장위치명' emphasize = abap_true  )
              ( fieldname = 'ESTKZ_TEXT'      coltext = '생성지시자' )
              ( fieldname = 'REQUEST_DATE'    ref_table = 'ZC103MMT0007' coltext = '구매요청일' just = 'C' emphasize = abap_true )
              ( fieldname = 'RECEIVE_DATE'    ref_table = 'ZC103MMT0007' coltext = '납품요청일' just = 'C' emphasize = abap_true )
              ( fieldname = 'MATID'           ref_table = 'ZC103MMT0008' just = 'C'  )
              ( fieldname = 'MATNAME'         ref_table = 'ZC103MMT0001' emphasize = abap_true )
              ( fieldname = 'QUANTITY'        ref_table = 'ZC103MMT0008' qfieldname = 'UNIT' coltext = '수량'
                do_sum = abap_true emphasize = abap_true )
              ( fieldname = 'UNIT'            ref_table = 'ZC103MMT0008' coltext = '단위')
              ( fieldname = 'PRICE'           ref_table = 'ZC103MMT0008' cfieldname = 'CURRENCY' coltext = '금액'
                do_sum = abap_true emphasize = abap_true )
              ( fieldname = 'CURRENCY'        ref_table = 'ZC103MMT0008' coltext = '통화' )
              ( fieldname = 'CREATOR'         ref_table = 'ZC103MMT0003' coltext = '생성자 사번' just = 'C' )
              ( fieldname = 'CREATOR_NAME'    ref_table = 'ZC103FIT0011' coltext = '생성자 이름' just = 'C' )
              ( fieldname = 'APPROVAL'        ref_table = 'ZC103MMT0004' coltext = '승인자 사번' just = 'C' )
              ( fieldname = 'APPROVE_NAME'   ref_table = 'ZC103FIT0011' coltext = '승인자 이름' just = 'C' )
              ( fieldname = 'STATUS_TEXT'  coltext = '구매요청상태' just = 'C' emphasize = abap_true )
              ( fieldname = 'APPROVE'      coltext = '결재' )
              ( fieldname = 'UPDATE'       coltext = '조회' )
  ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout_and_variant
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout_and_vari_and_sort .

*-- 레이아웃 설정
  gs_layo = VALUE #( cwidth_opt = 'A' zebra = abap_true sel_mode = 'D'
                     stylefname = 'CELLTAB' totals_bef = abap_true ).
*-- variant 설정
  gs_vari = VALUE #( report = sy-repid handle = 'ALV1' ).

*-- sort 설정
  gt_sort = VALUE #(
*    ( spos = 1 fieldname = 'REQUEST_DATE' up = abap_true subtot = abap_true )
    ( spos = 2 fieldname = 'PRID' up = abap_true subtot = abap_true )
  ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_main_cont
    EXPORTING
      container_name = 'MAIN_CONT'.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_main_cont.

  " TOP-OF-PAGE 객체 생성
  CREATE OBJECT go_dyndoc_id
    EXPORTING
      style = 'ALV_GRID'.

  CREATE OBJECT go_topofp_cont
    EXPORTING
      side      = cl_gui_docking_container=>dock_at_top
      extension = 90.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> E_INTERACTIVE
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar  USING    po_object TYPE REF TO cl_alv_event_toolbar_set
                                   pv_interactive
                                   po_sender TYPE REF TO cl_gui_alv_grid.

*-- 버튼 텍스트 데이터
  DATA : lv_aprd(30),
         lv_aprv(30),
         lv_totp(30).

*-- 버튼에 건수 설정
  lv_aprd = | 승인대기 : { gv_aprdp }건|.
  lv_aprv = | 승인완료 : { gv_aprvp }건|.
  lv_totp = | 전체조회 : { gv_totpr }건|.

*-- ALV에 따라 다른 로직을 실행하도록
*-- sender 활용함
  CASE po_sender.
    WHEN go_alv_grid.
      po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar
        ( butn_type = 3 )
        ( function = 'CREA' icon = icon_create text = ' 구매요청생성' )
        ( function = 'APRD' icon = icon_led_yellow text = lv_aprd )
        ( function = 'APRV' icon = icon_led_green  text = lv_aprv )
        ( function = 'SALL' icon = icon_display text = lv_totp ) ).
    WHEN go_pop_grid.
      CASE gv_mode.
        WHEN 'E'.
          po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar
            ( butn_type = 3 )
            ( function = 'TOGL' icon = icon_toggle_display_change quickinfo = 'Display <-> Change' )
            ( function = 'IROW' icon = icon_insert_row     quickinfo = 'Insert Row' )
            ( function = 'DROW' icon = icon_delete_row     quickinfo = 'Delete Row' )
          ).
        WHEN OTHERS.
          po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar
            ( butn_type = 3 )
            ( function = 'TOGL' icon = icon_toggle_display_change quickinfo = 'Display <-> Change' )
            ( function = 'IROW' icon = icon_insert_row     quickinfo = 'Insert Row' disabled = abap_true )
            ( function = 'DROW' icon = icon_delete_row     quickinfo = 'Delete Row' disabled = abap_true )
          ).
      ENDCASE.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_handler
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_handler .

  SET HANDLER : lcl_event_handler=>edit_toolbar     FOR go_alv_grid,
                lcl_event_handler=>user_command     FOR go_alv_grid,
                lcl_event_handler=>handle_btn_click FOR go_alv_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*
FORM handle_user_command  USING pv_ucomm
                                pv_sender TYPE REF TO cl_gui_alv_grid.

*-- 유저 커맨드 처리 (sendor 오브젝트 활용하여 여러 alv 처리함)
  CASE pv_sender.
    WHEN go_alv_grid.
      PERFORM handle_main_user_command USING pv_ucomm.
    WHEN go_pop_grid.
      PERFORM handle_pop_user_command USING pv_ucomm.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_pop_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_pop_screen .

  IF go_pop_cont IS NOT BOUND.

    PERFORM set_pop_fcat.
    PERFORM set_pop_layout_and_variant.
    PERFORM create_pop_object.
    PERFORM set_pop_handler.
    CALL METHOD go_pop_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_pvari
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_playo
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_prcr
        it_fieldcatalog      = gt_pfcat.
    PERFORM register_pop_event.
    PERFORM register_f4_fields.

  ELSE.
    PERFORM refresh_pop_table.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pop_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pop_fcat .

*-- 메인화면 필드카탈로그 설정

  gt_pfcat = VALUE #(
    ( key = 'X' fieldname = 'MATID'        ref_table = 'ZC103MMT0008' f4availabl = abap_true edit = abap_true )
              ( fieldname = 'MATNAME'      ref_table = 'ZC103MMT0001' )
              ( fieldname = 'QUANTITY'     ref_table = 'ZC103MMT0008' qfieldname = 'UNIT' coltext = '수량' )
              ( fieldname = 'UNIT'         ref_table = 'ZC103MMT0008' coltext = '단위')
              ( fieldname = 'PRICE'        ref_table = 'ZC103MMT0008' cfieldname = 'CURRENCY' coltext = '금액' do_sum = abap_true )
              ( fieldname = 'CURRENCY'     ref_table = 'ZC103MMT0008' coltext = '통화' )
  ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pop_layout_and_variant
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pop_layout_and_variant .

*-- 메인화면 레이아웃, 배리언트 설정

  gs_playo = VALUE #( cwidth_opt = 'A' zebra = abap_true sel_mode = 'D' stylefname = 'cell_tab' totals_bef = abap_true ).
  gs_pvari = VALUE #( report = sy-repid handle = 'ALV2' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_pop_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_pop_object .

  CREATE OBJECT go_pop_cont
    EXPORTING
      container_name = 'POP_CONT'.

  CREATE OBJECT go_pop_grid
    EXPORTING
      i_parent = go_pop_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pop_handler
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pop_handler .

  SET HANDLER : lcl_event_handler=>edit_toolbar       FOR go_pop_grid,
                lcl_event_handler=>modify_value       FOR go_pop_grid,
                lcl_event_handler=>user_command       FOR go_pop_grid,
                lcl_event_handler=>handle_search_help FOR go_pop_grid.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_main_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_main_user_command USING pv_ucomm .

*-- 메인 ALV 유저 커맨드 처리

  CASE pv_ucomm.
    WHEN 'CREA'.
      gv_pmode = 'C'. " 팝업창 모드 설정
      CLEAR : gt_prcr, gv_empno, gv_empname, gv_dptcode, gs_prhd, gs_prcr, gt_prhd, gt_prdelt.
      CALL SCREEN 110 STARTING AT 10 10.
    WHEN 'APRD'. " 승인대기조회
      PERFORM filter_body USING 'A'.

    WHEN 'APRV'. " 승인완료조회
      PERFORM filter_body USING 'B'.
    WHEN 'SALL'. " 전체조회
      gt_body = CORRESPONDING #( gt_back ).
      PERFORM set_textdata.
      PERFORM refresh_table.
  ENDCASE.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_pop_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PV_UCOMM
*&---------------------------------------------------------------------*
FORM handle_pop_user_command  USING pv_ucomm.

*-- 구매 요청 생성 커맨드 처리

  CASE pv_ucomm.
    WHEN 'TOGL'.
      PERFORM toggle_mode.
    WHEN 'IROW'.
      PERFORM insert_row.
    WHEN 'DROW'.
      PERFORM delete_row.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_pop_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_pop_event .

  CALL METHOD : go_pop_grid->set_ready_for_input( EXPORTING i_ready_for_input = 1 ),
                go_pop_grid->register_edit_event( EXPORTING i_event_id = go_pop_grid->mc_evt_modified ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form toggle_mode
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM toggle_mode .

  CASE gv_mode.
    WHEN 'E'.
      gv_mode = 'D'.
      go_pop_grid->set_ready_for_input( EXPORTING i_ready_for_input = 0 ).
      PERFORM refresh_pop_table.
    WHEN 'D'.
      gv_mode = 'E'.
      go_pop_grid->set_ready_for_input( EXPORTING i_ready_for_input = 1 ).
      PERFORM refresh_pop_table.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_modify_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_MODIFIED
*&      --> ET_GOOD_CELLS
*&---------------------------------------------------------------------*
FORM handle_modify_value  USING    pv_modified
                                   pt_good_cells TYPE lvc_t_modi.

  DATA : ls_good_cells TYPE lvc_s_modi,
         lv_price      TYPE price.
  CHECK pv_modified IS NOT INITIAL.
  LOOP AT pt_good_cells INTO ls_good_cells.

    " 수정되었을 때 modi_yn 값 설정
    CLEAR : gs_text, gs_prcr.
    READ TABLE gt_prcr INTO gs_prcr INDEX ls_good_cells-row_id.
    gs_prcr-modi_yn = abap_true.

    CASE ls_good_cells-fieldname.
      WHEN 'MATID'.
        " 자재코드와 수량이 입력되면 단위와 통화키가 세팅된다
        READ TABLE gt_text INTO gs_text WITH KEY matid = ls_good_cells-value.
        gs_prcr = VALUE #( BASE gs_prcr
                           unit = gs_text-base_unit
                           matname = gs_text-matname
                           currency = gs_text-currency
                           quantity = gs_text-lotsize
                           price = gs_text-price ).
        gs_prcr-price = gs_prcr-price * gs_prcr-quantity.
        MODIFY gt_prcr FROM gs_prcr INDEX ls_good_cells-row_id TRANSPORTING currency unit quantity price matname modi_yn price.
      WHEN 'QUANTITY'.
        " 수량이 입력되면 총 금액이 세팅됨
        READ TABLE gt_text INTO gs_text WITH KEY matid = gs_prcr-matid.
        gs_prcr-price = gs_text-price * gs_prcr-quantity.
        MODIFY gt_prcr FROM gs_prcr INDEX ls_good_cells-row_id TRANSPORTING price modi_yn.
    ENDCASE.

  ENDLOOP.

  PERFORM refresh_pop_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form insert_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM insert_row .
*-- 구매요청 생성 ALV에 행을 추가하고 리프레시한다
*-- 스타일 객체 추가하고 구매요청생성 itab에 행 추가
  CLEAR gs_prcr.
  gs_prcr-cell_tab = VALUE #(
   ( fieldname = 'MATID'     style = cl_gui_alv_grid=>mc_style_enabled )
   ( fieldname = 'QUANTITY'  style = cl_gui_alv_grid=>mc_style_enabled ) ).
  APPEND gs_prcr TO gt_prcr.
  PERFORM refresh_pop_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_pop_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_pop_table .
*-- 구매오더 생성 ALV 새로고침 (테이블 -> ALV)
  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable-row = abap_true.
  ls_stable-col = abap_true.

  CALL METHOD go_pop_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_row .

*-- 구매요청 생성 팝업창에서 행을 삭제하기 위한 서브루틴

*-- 사용자가 선택한 행 정보를 가져온다
  DATA : lt_roid TYPE lvc_t_roid,
         ls_roid TYPE lvc_s_roid.

  CALL METHOD go_pop_grid->get_selected_rows
    IMPORTING
      et_row_no = lt_roid.

  IF lt_roid IS INITIAL.
    MESSAGE s000 WITH TEXT-e01 DISPLAY LIKE 'E'.
  ENDIF.

*-- 역순으로 정렬하여 행의 순서가 밀리는 현상 방지
  SORT lt_roid BY row_id DESCENDING.

  LOOP AT lt_roid INTO ls_roid.

    READ TABLE gt_prcr INTO gs_prcr INDEX ls_roid-row_id.

    IF gv_pmode EQ 'U'. " 팝업창이 업데이트 모드일 때는 수정을 위한 itab에 append

      APPEND gs_prcr TO gt_prdelt.

    ENDIF.

    DELETE gt_prcr INDEX ls_roid-row_id.

  ENDLOOP.

  PERFORM refresh_pop_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_pop_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_pop_data .
*-- 구매요청을 생성, 수정, 삭제한다.

  CASE gv_pmode.
    WHEN 'C'.
      PERFORM create_data.
    WHEN 'U'.
      PERFORM update_and_delete_data.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form popup_to_confirm
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LV_ANSWER
*&---------------------------------------------------------------------*
FORM popup_to_confirm  CHANGING pv_answer.
*-- 저장 확인용 팝업
  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = '구매요청 저장'
      text_question         = '생성된 구매요청을 저장하시겠습니까?'
      text_button_1         = '저장하기'
      default_button        = '1'
      display_cancel_button = 'X'
    IMPORTING
      answer                = pv_answer.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form after_save_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM clear_all .

*-- 저장 후 전부 초기 상태로 돌리기 위한 메소드

*-- 구매요청 메인 ALV 초기화
  PERFORM get_data.
  PERFORM set_textdata.
  PERFORM calc_total_pr.
  PERFORM refresh_table.

*-- 구매요청 생성 ALV 초기화
  CLEAR : gt_prcr, gs_prcr, gs_prhd.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_table .

*-- 구매요청 ALV 새로고침 (테이블 -> ALV)

  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable-row = abap_true.
  ls_stable-col = abap_true.

  CALL METHOD go_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_all_data_for_test
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_all_data_for_test .

  DATA : lv_text(1).

  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = '데이터를 삭제'
    IMPORTING
      ev_answer = lv_text.

  CHECK lv_text EQ 1.

  DELETE FROM zc103mmt0007.
  DELETE FROM zc103mmt0008.
  COMMIT WORK.

  PERFORM clear_all.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_domain_text
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      <-- LT_DD07V
*&---------------------------------------------------------------------*
FORM get_domain_text  USING pv_domain pv_text
                      CHANGING pt_dd07v TYPE STANDARD TABLE.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = pv_domain
      text       = pv_text
    TABLES
      values_tab = pt_dd07v.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_header_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_header_data .
*-- 팝업창에서 구매요청 헤더 데이터 세팅

  CHECK gv_pmode EQ 'C'. " 팝업창이 생성모드일 때만 아래 로직을 실행함

  DATA : lv_prid(8).

*-- 구매요청번호 세팅
  CALL FUNCTION 'NUMBER_GET_NEXT'
    EXPORTING
      nr_range_nr = '01'
      object      = 'ZC103MM01'
    IMPORTING
      number      = lv_prid.

  lv_prid = lv_prid+2(6).
  lv_prid = 'PR' && lv_prid.
  gs_prhd-prid = lv_prid.

*-- 요청생성일 세팅
  gs_prhd-request_date = sy-datum.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_btn_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> ES_COL_ID
*&      --> ES_ROW_NO
*&---------------------------------------------------------------------*
FORM handle_btn_click  USING ps_row_no TYPE lvc_s_roid
                             ps_col_id TYPE lvc_s_col.

  " 결재, 수정 버튼 처리
  CASE ps_col_id-fieldname.
    WHEN 'APPROVE'.
      PERFORM handle_approve_button USING ps_row_no.
    WHEN 'UPDATE'.
      PERFORM handle_update_button USING ps_row_no.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_empinfo
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_empinfo .
  " 팝업창에서 사원코드를 입력하면
  " 구매요청을 생성하는 사람의 이름과 부서 정보가 보이도록 세팅

  CHECK gv_empno IS NOT INITIAL.
  CLEAR gs_emp.
  READ TABLE gt_emp INTO gs_emp WITH KEY empno = gv_empno.
  gv_empname = gs_emp-empname.
  gv_dptcode = gs_emp-dptcode.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form onf4
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_FIELDNAME
*&      --> E_FIELDVALUE
*&      --> ES_ROW_NO
*&      --> ER_EVENT_DATA
*&      --> ET_BAD_CELLS
*&      --> E_DISPLAY
*&---------------------------------------------------------------------*
FORM onf4  USING    p_fieldname   TYPE lvc_fname
                    p_fieldvalue  TYPE lvc_value
                    ps_row_no     TYPE lvc_s_roid
                    pi_event_data TYPE REF TO cl_alv_event_data
                    pt_bad_cells  TYPE lvc_t_modi
                    p_display     TYPE char01.

  " 팝업창에서 자재번호를 서치헬프를 통해 입력하도록 한다
  DATA: dynprog          LIKE sy-repid,
        dynnr            LIKE sy-dynnr,
        window_title(30) TYPE c,
        l_row            TYPE p.

  DATA : ls_body  LIKE gs_body,
         lv_field TYPE dfies-fieldname,
         lv_text  TYPE help_info-dynprofld,  " 필드명
         lv_flag.

  " 만약 팝업창이 조회모드라면 아래 이벤트를 실행하지 않는다.
  CHECK gv_mode EQ 'E'. " 입력 모드가 'E'인지 확인

  READ TABLE gt_body INTO ls_body INDEX ps_row_no-row_id.

  DATA : lt_return LIKE TABLE OF ddshretval WITH HEADER LINE.
  CLEAR : dynprog, dynnr, window_title.", ls_itab.

  dynprog  = sy-repid.
  dynnr    = sy-dynnr.
  lv_field = 'MATID'.
  lv_text  = 'MATNAME'.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield     = lv_field
      dynpprog     = dynprog
      dynpnr       = dynnr
      dynprofield  = lv_text
      window_title = window_title
      value_org    = 'S'
    TABLES
      value_tab    = gt_sh_matid
      return_tab   = lt_return.

  pi_event_data->m_event_handled = abap_true.

  FIELD-SYMBOLS: <fs> TYPE lvc_t_modi.

  ASSIGN pi_event_data->m_data->* TO <fs>.

  READ TABLE lt_return INDEX 1.
  <fs> = VALUE #( ( row_id = ps_row_no-row_id
                    fieldname = p_fieldname
                    value = lt_return-fieldval ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_f4_fields
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_f4_fields .

  DATA : lt_f4 TYPE lvc_t_f4.

  lt_f4 = VALUE #( ( fieldname   = 'MATID'
                     register    = abap_true
                     getbefore   = abap_true
                     chngeafter  = abap_true ) ).

  CALL METHOD go_pop_grid->register_f4_for_fields
    EXPORTING
      it_f4 = lt_f4.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form filter_body
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&---------------------------------------------------------------------*
FORM filter_body  USING  pv_status.
*-- 메인 alv 필터링을 위한 서브루틴
*-- gt_back과 gt_body의 개수가 다르다면 이미 필터링이 한번 수행된 것 이므로
*-- gt_back을 gt_body로 복사한다

  IF lines( gt_back ) NE lines( gt_body ).
    gt_body = CORRESPONDING #( gt_back ).
  ENDIF.

*-- 필터링용 로컬 테이블 선언
  DATA : lt_body LIKE TABLE OF gs_body.
  lt_body = CORRESPONDING #( gt_body ).

*-- 필터링 수행
  IF pv_status EQ 'A'. " 승인대기
    gt_body = VALUE #( FOR ls_body IN lt_body
                          WHERE ( pr_status = pv_status ) ( ls_body ) ).
  ELSE.
    gt_body = VALUE #( FOR ls_body IN lt_body
                          WHERE ( pr_status NE 'A' ) ( ls_body ) ).

  ENDIF.

*-- 메인 ALV 새로고침 및 결재버튼 설정
  PERFORM set_textdata.
  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form calc_total_pr
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM calc_total_pr .
*-- 구매요청 건 수를 계산한다

  DATA : lt_body LIKE TABLE OF zc103mmt0007.

  lt_body = VALUE #( FOR ls_body IN gt_prhdt WHERE ( prid NE '' ) ( ls_body ) ).
  gv_totpr = lines( lt_body ).

  " 승인대기건수
  CLEAR lt_body.
  lt_body = VALUE #( FOR ls_body IN gt_prhdt WHERE ( pr_status = 'A' ) ( ls_body ) ).
  gv_aprdp = lines( lt_body ).

  " 승인완료건수
  CLEAR lt_body.
  lt_body = VALUE #( FOR ls_body IN gt_prhdt WHERE ( pr_status NE 'A' AND pr_status NE '' ) ( ls_body ) ).
  gv_aprvp = lines( lt_body ).

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
