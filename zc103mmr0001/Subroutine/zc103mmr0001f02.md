```abap
*----------------------------------------------------------------------*
***INCLUDE ZC103MMR0001F02.
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form handle_top_of_page
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_top_of_page .

  " TOP-OF-PAGE를 만드는 이벤트

  DATA : lr_dd_table        TYPE REF TO cl_dd_table_element, " 테이블 전체 구조를 선언하는 테이블
         col_field          TYPE REF TO cl_dd_area,          " 왼쪽 열 ( 항목 이름 )
         col_value          TYPE REF TO cl_dd_area,          " 오른쪽 열 ( 항목 값 )
         lv_pr_text         TYPE sdydo_text_element,         " 구매요청번호 텍스트
         lv_sc_text         TYPE sdydo_text_element,
         lv_reqdt_low_text  TYPE sdydo_text_element,         " 구
         lv_reqdt_high_text TYPE sdydo_text_element,
         lv_reqdt_text      TYPE sdydo_text_element,
         lv_recdt_low_text  TYPE sdydo_text_element,
         lv_recdt_high_text TYPE sdydo_text_element,
         lv_recdt_text      TYPE sdydo_text_element,
         lv_text            TYPE sdydo_text_element.

  " HEAD 추가
*  CALL METHOD go_dyndoc_id->add_text
*    EXPORTING
*      text         = TEXT-t03
*      sap_style    = cl_dd_area=>heading
*      sap_fontsize = cl_dd_area=>large
*      sap_color    = cl_dd_area=>list_heading_inv.
*
*  CALL METHOD go_dyndoc_id->new_line.
*  CALL METHOD go_dyndoc_id->new_line.

  " 테이블 추가
  CALL METHOD go_dyndoc_id->add_table
    EXPORTING
      no_of_columns = 2
      border        = '0'
    IMPORTING
      table         = lr_dd_table.

  " 열 추가
  CALL METHOD lr_dd_table->add_column
    IMPORTING
      column = col_field.

  CALL METHOD lr_dd_table->add_column
    IMPORTING
      column = col_value.

  " 구매요청번호 텍스트 생성
  IF so_prid IS NOT INITIAL.
    lv_pr_text = |{ so_prid-low }|. " 구매요청번호
  ELSE.
    lv_pr_text = TEXT-t02.          " 전체조회
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '구매요청번호' lv_pr_text.

  " 구매요청번호 텍스트 생성
  IF so_schid IS NOT INITIAL.
    lv_sc_text = |{ so_schid-low }|. " 구매요청번호
  ELSE.
    lv_sc_text = TEXT-t02.          " 전체조회
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '항공기 스케줄 ID' lv_sc_text.

  " 구매요청일 텍스트 생성
  IF so_recdt IS NOT INITIAL.
    lv_recdt_low_text = |{ so_recdt-low+0(4) }년 { so_recdt-low+4(2) }월 { so_recdt-low+6(2) }일|.
    lv_recdt_high_text = |{ so_recdt-high+0(4) }년 { so_recdt-high+4(2) }월 { so_recdt-high+6(2) }일|.
    IF so_recdt-high IS INITIAL.
      lv_recdt_text = |{ lv_recdt_low_text }|.
    ELSE.
      lv_recdt_text = |{ lv_recdt_low_text } ~ { lv_recdt_high_text }|.
    ENDIF.
  ELSE.
    lv_recdt_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '구매요청일' lv_recdt_text.

  " 납품요청일 텍스트 생성
  IF so_reqdt IS NOT INITIAL.
    lv_reqdt_low_text = |{ so_reqdt-low+0(4) }년 { so_reqdt-low+4(2) }월 { so_reqdt-low+6(2) }일|.
    lv_reqdt_high_text = |{ so_reqdt-high+0(4) }년 { so_reqdt-high+4(2) }월 { so_reqdt-high+6(2) }일|.
    IF so_reqdt-high IS INITIAL.
      lv_reqdt_text = |{ lv_reqdt_low_text }|.
    ELSE.
      lv_reqdt_text = |{ lv_reqdt_low_text } ~ { lv_reqdt_high_text }|.
    ENDIF.

  ELSE.
    lv_reqdt_text = TEXT-t02.
  ENDIF.
  PERFORM add_row USING lr_dd_table col_field col_value '납품요청일' lv_reqdt_text.

  " 승인대기
  PERFORM add_row USING lr_dd_table col_field col_value '승인대기' 'YELLOW'.

  " 승인완료
  PERFORM add_row USING lr_dd_table col_field col_value '승인완료' 'GREEN'.

  PERFORM set_topof_page.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form add_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LR_DD_TABLE
*&      --> COL_FIELD
*&      --> COL_VALUE
*&      --> P_
*&      --> LV_TEXT
*&---------------------------------------------------------------------*
FORM add_row  USING    pr_dd_table  TYPE REF TO cl_dd_table_element " 테이블
                       po_col_field TYPE REF TO cl_dd_area          " 행
                       po_col_value TYPE REF TO cl_dd_area          " 행
                       pv_field                                     " 필드명
                       pv_text.                                     " 필드값


  " 테이블에 행을 추가한다.
  " col_field 세팅
  CALL METHOD po_col_field->add_text " 테이블 행에 텍스트 등록
    EXPORTING
      text         = pv_field
      sap_emphasis = cl_dd_document=>strong
      sap_color    = cl_dd_document=>list_heading_inv.

  CALL METHOD po_col_field->add_gap EXPORTING width = 1.

  CASE pv_text.
    WHEN 'GREEN'.
      CALL METHOD po_col_value->add_icon
        EXPORTING
          sap_icon = 'ICON_LED_GREEN'.
    WHEN 'YELLOW'.
      CALL METHOD po_col_value->add_icon
        EXPORTING
          sap_icon = 'ICON_LED_YELLOW'.
    WHEN OTHERS.
      " col_value 세팅
      CALL METHOD po_col_value->add_text " 테이블 행에 텍스트 등록
        EXPORTING
          text         = pv_text
          sap_emphasis = cl_dd_document=>heading
          sap_color    = cl_dd_document=>list_negative_inv.
  ENDCASE.

  CALL METHOD po_col_value->add_gap EXPORTING width = 1.
  " 테이블에 새 행 추가
  CALL METHOD pr_dd_table->new_row.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_topof_page
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_topof_page .

  " html viewer 생성
  IF go_html_cntrl IS INITIAL.

    CREATE OBJECT go_html_cntrl
      EXPORTING
        parent = go_topofp_cont.

  ENDIF.

  CALL METHOD go_dyndoc_id->merge_document.
  go_dyndoc_id->html_control = go_html_cntrl.

  CALL METHOD go_dyndoc_id->display_document
    EXPORTING
      reuse_control = abap_true
      parent        = go_topofp_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form exclude_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM exclude_toolbar .

  DATA : ls_ui_functions TYPE ui_func.

  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_undo.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_cut.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_delete_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_insert_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_append_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste_new_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_refresh.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_auf.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_average.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_print.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_graph.
  APPEND ls_ui_functions TO gt_ui_functions.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_approve_button
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_approve_button USING ps_row_no TYPE lvc_s_roid.

*-- 미결재 항목을 결재한다

  DATA : ls_empno  TYPE zc103e_fi_empno,       " 결재자 사번
         ls_update TYPE zc103mmt0007, " 저장용 헤더 테이블
         lt_update TYPE TABLE OF zc103mmt0007,
         lv_prid   TYPE zc103mmt0007-prid,
         lv_tabix  TYPE sy-tabix.

  CLEAR gs_body.

  READ TABLE gt_body INTO gs_body INDEX ps_row_no-row_id.

  " 구매요청번호 저장
  lv_prid = gs_body-prid.

  " 결재 정보 가져오기
  CALL FUNCTION 'ZC103PMFG0001' IMPORTING empno = ls_empno.

  " 결재 정보가 입력되지 않으면 출력하지 않음
  CHECK ls_empno IS NOT INITIAL.

  " 같은 구매오더인 행 수정
  LOOP AT gt_body INTO gs_body WHERE prid EQ lv_prid.

    lv_tabix = sy-tabix.

    " 데이터 업데이트
    gs_body = VALUE #( BASE gs_body
                       pr_status = 'B'
                       status_text = '승인'
                       approval = ls_empno
                       approve_date = sy-datum
                       approve_time = sy-uzeit ).

    " itab 업데이트
    MODIFY gt_body FROM gs_body INDEX lv_tabix
      TRANSPORTING pr_status status_text approval approve_date approve_time.

  ENDLOOP.

  " DB 저장용
  ls_update = CORRESPONDING #( gs_body ).
  ls_update = VALUE #( BASE ls_update
                        erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).

  " DB 업데이트
  MODIFY zc103mmt0007 FROM ls_update.
  IF sy-subrc EQ 0.
    MESSAGE s009.
    COMMIT WORK.
  ELSE.
    ROLLBACK WORK.
  ENDIF.

  " 테이블 업데이트
  PERFORM set_textdata.
  PERFORM calc_total_pr.
  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_update_button
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PS_ROW_NO
*&---------------------------------------------------------------------*
FORM handle_update_button  USING    ps_row_no TYPE lvc_s_roid.

  CLEAR : gt_prcr, gv_empno, gv_empname, gv_dptcode, gs_prhd, gs_prcr, gt_prhd, gt_prdelt.

  " 수정하기 버튼을 눌렀을 때 팝업창에 수정 버튼이 나오도록 한다

  DATA : lt_body  LIKE gt_body,
         lv_tabix TYPE sy-tabix.

  " 수정하기 버튼을 누른 행의 헤더 데이터를 세팅한다
  CLEAR : gs_body, gs_prhd, gt_prcr.
  READ TABLE gt_body INTO gs_body INDEX ps_row_no-row_id.
  gs_prhd = CORRESPONDING #( gs_body ).

  gv_pmode = 'U'. " 팝업창 수정 모드 설정'

  " 아이템 데이터 세팅
  lt_body = VALUE #( FOR ls_body IN gt_body WHERE ( prid = gs_body-prid ) ( ls_body ) ).

  gt_prcr = CORRESPONDING #( lt_body ).
  SORT gt_prcr BY pritemid ASCENDING.

  " 사원정보세팅
  gv_empno = gs_body-creator.
  PERFORM set_empinfo.

  " 수정모드 세팅
  LOOP AT gt_prcr INTO gs_prcr.

    lv_tabix = sy-tabix.

    gs_prcr-cell_tab = VALUE #( ( fieldname = 'QUANTITY'  style = cl_gui_alv_grid=>mc_style_enabled )
                                ( fieldname = 'MATID'     style = cl_gui_alv_grid=>mc_style_enabled ) ).

    MODIFY gt_prcr FROM gs_prcr INDEX lv_tabix TRANSPORTING cell_tab.


  ENDLOOP.

  CALL SCREEN 110 STARTING AT 10 10.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form check_pop_mode
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM check_pop_mode .

  " 팝업 모드를 체크해서 스크린 입력 값을 막는다

  LOOP AT SCREEN.

    CASE screen-group1.
      WHEN 'PMD'.
        IF gv_pmode = 'C'.
          screen-input = 1.
        ELSE.
          screen-input = 0.
        ENDIF.
    ENDCASE.

    MODIFY SCREEN.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_data .

*-- 저장용 데이터
  DATA : ls_svhd      TYPE zc103mmt0007,          " 구매요청 헤더 데이터 저장용
         ls_svit      TYPE zc103mmt0008,          " 구매요청 아이템 데이터 저장용
         lt_svit      TYPE TABLE OF zc103mmt0008, " 구매요청 아이템 데이터 저장용
         lv_answer(1),                            " 저장 팝업 결과
         lv_tabix     TYPE sy-tabix,              " 루프용 인덱스
         lv_return    TYPE i.                     " 유효성검사 결과값 (0 : 정상, 1 : 오류)


*-- ALV -> itab 데이터 동기화
  CALL METHOD go_pop_grid->check_changed_data.

*-- 데이터 저장 팝업
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = TEXT-t11
    IMPORTING
      ev_answer = lv_answer.

  CHECK lv_answer EQ '1'.

  " 유효성 검사
  PERFORM check_input_data CHANGING lv_return.

  CHECK lv_return EQ 0.

*-- 헤더 데이터
  ls_svhd = CORRESPONDING #( gs_prhd ).
  " 생성 타임스탬스 및 구매요청 상태 세팅
  ls_svhd = VALUE #( BASE ls_svhd
                     erdat = sy-datum
                     ernam = sy-uname
                     erzet = sy-uzeit
                     creator = gv_empno
                     create_date = sy-datum
                     create_time = sy-uzeit
                     pr_status = 'A' ).

*-- 아이템 데이터
  lt_svit = CORRESPONDING #( gt_prcr ).
  LOOP AT lt_svit INTO ls_svit.

    lv_tabix = sy-tabix.

    " 아이템 데이터가 미입력된 행이 있으면 서브루틴을 종료한다
    IF ls_svit-matid IS INITIAL OR
       ls_svit-quantity IS INITIAL.
      MESSAGE i010 DISPLAY LIKE 'E' WITH TEXT-t10.
      RETURN.
    ENDIF.

    " 아이템에 입력된 자재번호값이 잘못되었으면 오류를 출력하고 서브루틴을 종료한다
    CLEAR gs_text.
    READ TABLE gt_text INTO gs_text WITH KEY matid = ls_svit-matid.
    IF gs_text IS INITIAL.

      MESSAGE i068 DISPLAY LIKE 'E' WITH TEXT-t17.
      RETURN.

    ENDIF.


    " 수량이 0인 것이 있다면 서브루틴 종료
    IF ls_svit-quantity = 0.
      MESSAGE i052 WITH TEXT-t04 DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 금액이 0인 것이 있다면 서브루틴 종료
    IF ls_svit-price = 0.
      MESSAGE i052 WITH TEXT-t05 DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 생성 타입스탬프 및 구매요청 번호 세팅
    ls_svit = VALUE #( BASE ls_svit
                    pritemid = lv_tabix
                    erdat = sy-datum
                    ernam = sy-uname
                    erzet = sy-uzeit
                    prid  = ls_svhd-prid ).

    MODIFY lt_svit FROM ls_svit INDEX lv_tabix TRANSPORTING erdat ernam erzet prid pritemid.

  ENDLOOP.

*-- 헤더 데이터 저장
  MODIFY zc103mmt0007 FROM ls_svhd.
*-- 아이템 데이터 저장
  MODIFY zc103mmt0008 FROM TABLE lt_svit.
  IF sy-subrc EQ 0.
    COMMIT WORK.
    PERFORM clear_all.
    MESSAGE s022.
  ELSE.
    ROLLBACK WORK.
    MESSAGE i023 DISPLAY LIKE 'E'.
  ENDIF.

*-- 팝업창 닫기
  LEAVE TO SCREEN 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form update_and_delete_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM update_and_delete_data .

*-- 저장용 데이터
  DATA : ls_svhd       TYPE zc103mmt0007,          " 구매요청 헤더 데이터 저장용
         ls_svit       TYPE zc103mmt0008,          " 구매요청 아이템 데이터 저장용
         lt_svit       TYPE TABLE OF zc103mmt0008, " 구매요청 아이템 데이터 저장용\
         lt_delt       TYPE TABLE OF zc103mmt0008, " 구매요청 아이템 데이터 삭제용
         lv_answer(1),                            " 저장 팝업 결과
         lv_tabix      TYPE sy-tabix,              " 루프용 인덱스
         lv_lastitemid TYPE char8,                " 아이템아이디 저장용
         lv_prid       TYPE zc103mmt0008-prid.    " 아이템 저장용 prid


*-- ALV -> itab 데이터 동기화
  CALL METHOD go_pop_grid->check_changed_data.

*-- 데이터 저장 팝업
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = TEXT-t12
    IMPORTING
      ev_answer = lv_answer.

  CHECK lv_answer EQ '1'.

  " 아이템 데이터가 없으면 수정 불가하도록 함
  IF gt_prcr IS INITIAL.
    MESSAGE i032 DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  " 아이템 데이터 itab를 저장용 itab에 넣음
  lt_svit = CORRESPONDING #( gt_prcr ).

  READ TABLE lt_svit INTO ls_svit INDEX 1.
  lv_prid = ls_svit-prid.

  " 추가될 데이터의 아이템 아이디를 세팅하기 위해 설정
  LOOP AT lt_svit INTO ls_svit.

    " 수량이 0인 것이 있다면 서브루틴 종료
    IF ls_svit-quantity = 0.
      MESSAGE i052 WITH TEXT-t04 DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 금액이 0인 것이 있다면 서브루틴 종료
    IF ls_svit-price = 0.
      MESSAGE i052 WITH TEXT-t05 DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    IF ls_svit-pritemid > lv_lastitemid.
      lv_lastitemid = ls_svit-pritemid.
    ENDIF.

  ENDLOOP.

  LOOP AT gt_prdelt INTO DATA(ls_prdelt).

    IF ls_prdelt-pritemid > lv_lastitemid.
      lv_lastitemid = ls_prdelt-pritemid.
    ENDIF.

  ENDLOOP.

  " 수정된 데이터 타임스탬스 설정
  LOOP AT lt_svit INTO ls_svit.

    lv_tabix = sy-tabix.

    IF ls_svit-erdat IS INITIAL.
      ls_svit = VALUE #( BASE ls_svit
                              erdat = sy-datum erzet = sy-uzeit ernam = sy-uname ).
    ELSE.
      ls_svit = VALUE #( BASE ls_svit
                              aedat = sy-datum aezet = sy-uzeit aenam = sy-uname ).
    ENDIF.

    IF ls_svit-pritemid IS INITIAL.
      lv_lastitemid += 1.
      ls_svit = VALUE #( BASE ls_svit pritemid = lv_lastitemid prid = lv_prid ).
    ENDIF.

    MODIFY lt_svit FROM ls_svit INDEX lv_tabix TRANSPORTING erdat erzet ernam aedat aezet aenam pritemid prid.

  ENDLOOP.

  " 삭제 데이터
  lt_delt = CORRESPONDING #( gt_prdelt ).

  " 데이터 삭제
  IF lt_delt IS NOT INITIAL.
    DELETE zc103mmt0008 FROM TABLE lt_delt.
  ENDIF.


  " 데이터 수정
  MODIFY zc103mmt0008 FROM TABLE lt_svit.

  IF sy-subrc EQ 0.
    COMMIT WORK.
    PERFORM clear_all.
    MESSAGE s022.
  ELSE.
    ROLLBACK WORK.
    MESSAGE i023 DISPLAY LIKE 'E'.
  ENDIF.

*-- 팝업창 닫기
  LEAVE TO SCREEN 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form strid_f4_control
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM strid_f4_control .
  " 선택된 플랜트에 맞는 스토리지로케이션만 조회되도록 하는 서치헬프

  DATA : BEGIN OF ls_stlof4, " 서치헬프에 사용될 테이블
           strid TYPE zc103mmt0004-strid,
           plnid TYPE zc103mmt0004-plnid,
           name  TYPE  zc103mmt0004-name,
         END OF   ls_stlof4,
         lt_stlof4 LIKE TABLE OF ls_stlof4,
         lt_stlo   LIKE TABLE OF ls_stlof4,
         ls_stlo   LIKE ls_stlof4.

  DATA : lt_return  TYPE TABLE OF ddshretval,
         lt_dynp    TYPE TABLE OF dynpread,     " 화면 값 저장
         lt_mapping TYPE TABLE OF dselc.        " 매핑

  " 화면 값 읽어오기
  CALL FUNCTION 'DYNP_VALUES_READ'
    EXPORTING
      dyname     = sy-cprog
      dynumb     = sy-dynnr
      request    = 'A'
    TABLES
      dynpfields = lt_dynp.

  READ TABLE lt_dynp INTO DATA(ls_plnid) WITH KEY fieldname = 'GS_PRHD-PLNID'.

  " 조건에 맞게 테이블을 세팅
  LOOP AT gt_stlo INTO gs_stlo.

    ls_stlo = VALUE #( plnid = gs_stlo-plnid
                       strid = gs_stlo-strid
                       name  = gs_stlo-name ).
    APPEND ls_stlo TO lt_stlo.

  ENDLOOP.
  lt_stlof4 = VALUE #( FOR ls_stlo_tmp IN lt_stlo WHERE ( plnid EQ ls_plnid-fieldvalue ) ( ls_stlo_tmp ) ).

  IF lt_stlof4 IS INITIAL.
    MESSAGE i000 WITH TEXT-e02 DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  " 화면 매핑 값 설정
  lt_mapping = VALUE #( ( fldname = 'PLNID' dyfldname = '플랜트번호' )
                        ( fldname = 'STRID' dyfldname = '스토리지로케이션번호' )
                        ( fldname = 'NAME'  dyfldname = '스토리지로케이션명' ) ).

  " 서치헬프 호출
  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'STRID'
      dynpprog        = sy-repid
      dynpnr          = sy-dynnr
      dynprofield     = 'GS_PRHD-STRID'
      value_org       = 'S'
    TABLES
      value_tab       = lt_stlof4
      return_tab      = lt_return
      dynpfld_mapping = lt_mapping
    EXCEPTIONS
      OTHERS          = 1.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form check_prid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM check_prid .

  " 구매오더번호 유효성 검사 확인
  DATA(lv_prid) = so_prid-low.

  " 빈 값인 경우 전체조회이므로 통과
  IF lv_prid IS INITIAL.
    RETURN.
  ENDIF.

  " 길이 확인 (PR + 6자리 = 총 8자리)
  IF strlen( lv_prid ) <> 8.
    MESSAGE |'{ lv_prid }': 형식이 잘못되었습니다. (총 8자리)| TYPE 'E'.
  ENDIF.

  " 'PR' 접두사 확인
  IF lv_prid+0(2) <> 'PR'.
    MESSAGE |'{ lv_prid }': 'PR'로 시작해야 합니다.| TYPE 'E'.
  ENDIF.

  " 숫자 6자리 확인
  DATA(lv_suffix) = lv_prid+2(6).

  IF cl_abap_matcher=>create( pattern = '^\d{6}$' text = lv_suffix )->match( ) = abap_false.
    MESSAGE |'{ lv_prid }': 뒤 6자리는 숫자여야 합니다.| TYPE 'E'.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form check_schid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM check_schid .

  " 항공스케줄번번호 유효성 검사 확인
  DATA(lv_schid) = so_schid-low.

  " 빈 값인 경우 전체조회이므로 통과
  IF lv_schid IS INITIAL.
    RETURN.
  ENDIF.

  " 길이 확인 (PR + 6자리 = 총 8자리)
  IF strlen( lv_schid ) <> 8.
    MESSAGE |'{ lv_schid }': 형식이 잘못되었습니다. (총 8자리)| TYPE 'E'.
  ENDIF.

  " 'PR' 접두사 확인
  IF lv_schid+0(3) <> 'KSA'.
    MESSAGE |'{ lv_schid }': 'KSA'로 시작해야 합니다.| TYPE 'E'.
  ENDIF.

  " 숫자 6자리 확인
  DATA(lv_suffix) = lv_schid+3(5).

  IF cl_abap_matcher=>create( pattern = '^\d{5}$' text = lv_suffix )->match( ) = abap_false.
    MESSAGE |'{ lv_schid }': 뒤 6자리는 숫자여야 합니다.| TYPE 'E'.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_new_code
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_new_code .

  CALL METHOD cl_gui_cfw=>set_new_ok_code
    EXPORTING
      new_code = 'ENTER'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form check_input_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM check_input_data CHANGING pv_result.

  DATA : ls_plnt TYPE zc103mmt0004.

*-- 납품일이 오늘 날짜랑 같거나 더 이전이면 서브루틴 종료
  IF gs_prhd-receive_date <= sy-datum.
    MESSAGE i013 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.
  ENDIF.

*-- 플랜트 미입력시 저장 불가
  IF gs_prhd-plnid IS INITIAL.
    MESSAGE i032 DISPLAY LIKE 'E' WITH TEXT-t07.
    pv_result = 1.
    RETURN.
  ENDIF.

*-- 스토리지로케이션 미입력시 저장 불가
  IF gs_prhd-strid IS INITIAL.
    MESSAGE i032 DISPLAY LIKE 'E' WITH TEXT-t08.
    pv_result = 1.
    RETURN.
  ENDIF.

*-- 생성지시자 미입력시 저장 불가
  IF gs_prhd-estkz IS INITIAL.
    MESSAGE i032 DISPLAY LIKE 'E' WITH TEXT-t09.
    pv_result = 1.
    RETURN.
  ENDIF.

*-- 아이템 미입력시 저장 불가
  IF gt_prcr IS INITIAL.
    MESSAGE i010 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.
  ENDIF.

*-- 입력값 검증
  " 플랜트값 검증
  CLEAR : gs_stlo.
  READ TABLE gt_stlo INTO gs_stlo WITH KEY plnid = gs_prhd-plnid.
  IF gs_stlo IS INITIAL.

    MESSAGE i068 WITH TEXT-t13 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.

  ENDIF.

  " 스토리지로케이션값 검증
  CLEAR : gs_stlo.
  READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_prhd-strid.
  IF gs_stlo IS INITIAL.

    MESSAGE i068 WITH TEXT-t14 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.

  ENDIF.


  " 스토리지로케이션이 맞지 않는 플랜트랑 입력된 경우 검증
  CLEAR : gs_stlo.
  READ TABLE gt_stlo INTO ls_plnt WITH KEY plnid = gs_prhd-plnid.
  READ TABLE gt_stlo INTO gs_stlo WITH KEY strid = gs_prhd-strid.

  IF gs_stlo-plnid NE ls_plnt-plnid.

    MESSAGE i068 WITH TEXT-t14 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.

  ENDIF.

  " 생성지시자 값 검증
  IF gs_prhd-estkz NE 'A' AND
     gs_prhd-estkz NE 'B' AND
     gs_prhd-estkz NE 'C'.

    MESSAGE i068 WITH TEXT-t15 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.

  ENDIF.

  " 사원번호값 검증
  CLEAR : gs_emp.
  READ TABLE gt_emp INTO gs_emp WITH KEY empno = gv_empno.
  IF gs_emp IS INITIAL.

    MESSAGE i068 WITH TEXT-T16 DISPLAY LIKE 'E'.
    pv_result = 1.
    RETURN.

  ENDIF.

  pv_result = 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form f4_empno
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM f4_empno .

  DATA : lt_return   LIKE TABLE OF ddshretval WITH HEADER LINE.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'EMPNO' " ALV 에 박히는 값
      dynpprog        = sy-repid
      dynpnr          = sy-dynnr
      dynprofield     = 'GV_USCODE'
      window_title    = 'Employee'
      value_org       = 'S'
    TABLES
      value_tab       = gt_emp_value
      return_tab      = lt_return
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  CHECK lt_return[] IS NOT INITIAL.

*-- Get employee name : New syntax
*  lt_return = VALUE #( lt_return[ 1 ] ).
*
*  CLEAR gs_emp_value.
*  gs_emp_value = VALUE #( gt_emp_value[ empno = lt_return-fieldval ].

*-- Original syntax
  READ TABLE lt_return INDEX 1.

  CLEAR gs_emp_value.
  READ TABLE gt_emp_value INTO gs_emp_value WITH KEY empno = lt_return-fieldval.

  gv_empno = lt_return-fieldval.
  gv_dptcode = gs_emp_value-dptcode.
  gv_empname = gs_emp_value-empname.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_emp_sh
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_emp_sh .

  DATA : lt_dynp  TYPE TABLE OF dynpread WITH HEADER LINE.

  CHECK gv_empname IS NOT INITIAL.

*-- Set employee info
  lt_dynp-fieldname   = 'GV_DPTCODE'.
  lt_dynp-fieldvalue  = gv_dptcode.
  APPEND lt_dynp.

  lt_dynp-fieldname   = 'GV_EMPNAME'.
  lt_dynp-fieldvalue  = gv_empname.
  APPEND lt_dynp.

  CALL FUNCTION 'DYNP_VALUES_UPDATE'
    EXPORTING
      dyname     = sy-repid
      dynumb     = sy-dynnr
    TABLES
      dynpfields = lt_dynp.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 758
```
