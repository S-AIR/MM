```abap
*&---------------------------------------------------------------------*
*& Include          SAPMZC103MM0001F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Module INIT_SCREEN OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE init_screen OUTPUT.

  " 메인 데이터 (송장)
  IF gt_grhd IS INITIAL.
    PERFORM get_main_data.
  ENDIF.

  " 마스터데이터는 처음 한번만 가져온다
  IF gs_vend IS INITIAL.

    PERFORM get_master_data.
    PERFORM set_main_data.

  ENDIF.

  PERFORM display_screen.

ENDMODULE.
*&---------------------------------------------------------------------*
*& Form get_master_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_master_data .

  " 벤더 마스터
  SELECT bpid name
    INTO CORRESPONDING FIELDS OF TABLE gt_vend
    FROM zc103mmt0002.

  " 직원 마스터
  SELECT empno empname dptcode
    INTO CORRESPONDING FIELDS OF TABLE gt_emp
    FROM zc103fit0011.

  " 자재 마스터
  SELECT matid matname
    INTO CORRESPONDING FIELDS OF TABLE gt_mara
    FROM zc103mmt0001.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_main_data .

  DATA : lv_cnt TYPE i.

  " 송장 관련 전체 데이터를 가져온다.
  SELECT grid poid bpid receive_date total_price currency gr_status document_issued
         checker belnr gjahr tax_amount
    INTO CORRESPONDING FIELDS OF TABLE gt_grhd_back
    FROM zc103mmt0013.

  gt_grhd = VALUE #( FOR ls_grhd IN gt_grhd_back WHERE ( gjahr EQ '2025' ) ( ls_grhd ) ).

  lv_cnt = lines( gt_grhd ).
  MESSAGE s008 WITH lv_cnt.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_main_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_main_data .

  DATA : lv_tabix TYPE sy-tabix.

  DATA : lt_dd07v TYPE TABLE OF dd07v,
         ls_dd07v TYPE dd07v.

  " 날짜 설정
  gv_today = sy-datum.
  gv_current_time = sy-uzeit.
  PERFORM count_num.

  CALL FUNCTION 'GET_DOMAIN_VALUES'
    EXPORTING
      domname    = 'ZC103D_MM_GRSTATUS'
      text       = abap_true
    TABLES
      values_tab = lt_dd07v.

  " 텍스트데이터 세팅
  LOOP AT gt_grhd INTO gs_grhd.

    CLEAR : gs_vend, gs_emp, ls_dd07v.

    lv_tabix = sy-tabix.

    " 벤더명 세팅
    READ TABLE gt_vend INTO gs_vend WITH KEY bpid = gs_grhd-bpid.
    gs_grhd-bpname = gs_vend-name.

    " 검증자명 세팅
    READ TABLE gt_emp INTO gs_emp WITH KEY empno = gs_grhd-checker.
    gs_grhd-checker_name = gs_emp-empname.

    " 상태 아이콘 세팅
    IF gs_grhd-gr_status EQ 'A'.

      gs_grhd-icon = icon_led_yellow.
      gs_grhd-color = VALUE #( BASE gs_grhd-color
                               ( fname = 'STATUS_TEXT'
                                 color = VALUE #( col = 3 int = 0 ) ) ).
      gs_grhd-btn1 = '검증하기'.
      gs_grhd-celltab = VALUE #( BASE gs_grhd-celltab
                                 ( fieldname = 'BTN1' style = cl_gui_alv_grid=>mc_style_button ) ).

    ELSE.

      gs_grhd-icon = icon_led_green.
      gs_grhd-color = VALUE #( BASE gs_grhd-color
                         ( fname = 'STATUS_TEXT'
                           color = VALUE #( col = 5 int = 0 ) ) ).

    ENDIF.

    " 상태 명 세팅
    READ TABLE lt_dd07v INTO ls_dd07v WITH KEY domvalue_l = gs_grhd-gr_status.
    gs_grhd-status_text = ls_dd07v-ddtext.

    MODIFY gt_grhd FROM gs_grhd INDEX lv_tabix TRANSPORTING bpname checker_name icon status_text color btn1 celltab.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen .

  IF go_cont IS NOT BOUND.

    CLEAR : gt_fcat.
    PERFORM set_fcat.
    PERFORM set_layout.
    PERFORM create_object.

    SET HANDLER : lcl_event_handler=>handle_hotspot_click FOR go_grid.

    CALL METHOD go_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_vari
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layo
      CHANGING
        it_outtab       = gt_grhd
        it_fieldcatalog = gt_fcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_fcat .

  gt_fcat = VALUE #(
    ( key = abap_true fieldname = 'GRID' ref_table = 'ZC103MMT0013' just = 'C' hotspot = abap_true )
    ( key = abap_true fieldname = 'ICON' coltext = '상태' just = 'C' )
                    ( fieldname = 'POID' ref_table = 'ZC103MMT0011' just = 'C' emphasize = abap_true )
                    ( fieldname = 'BPID' ref_table = 'ZC103MMT0002' just = 'C' emphasize = abap_true )
                    ( fieldname = 'BPNAME' coltext = '공급업체명' )
                    ( fieldname = 'RECEIVE_DATE' coltext = '납품일' just = 'C' emphasize = abap_true )
                    ( fieldname = 'TOTAL_PRICE' coltext = '총 금액' cfieldname = 'CURRENCY' )
                    ( fieldname = 'CURRENCY' coltext = '통화' just = 'C' )
                    ( fieldname = 'CHECKER' ref_table = 'ZC103MMT0013' just = 'C' emphasize = abap_true )
                    ( fieldname = 'CHECKER_NAME' coltext = '검증자' )
                    ( fieldname = 'BELNR'   coltext = '화계전표번호' just = 'C' )
                    ( fieldname = 'GJAHR'   coltext = '전기기간' just = 'C' emphasize = abap_true )
                    ( fieldname = 'TAX_AMOUNT' ref_table = 'ZC103MMT0013' cfieldname = 'CURRENCY' )
                    ( fieldname = 'STATUS_TEXT' coltext = '송장상태' just = 'C')
                    ( fieldname = 'BTN1' coltext = '검증' just = 'C'  ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

  gs_layo = VALUE #( zebra = abap_true cwidth_opt = 'A' sel_mode = 'D' ctab_fname = 'COLOR' stylefname = 'CELLTAB'
                     grid_title = '송장' ).
  gs_vari = VALUE #( report = sy-repid handle = 'ALV1' ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_cont
    EXPORTING
      container_name = 'MAIN_CONT'.

  CREATE OBJECT go_grid
    EXPORTING
      i_parent = go_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW_ID
*&---------------------------------------------------------------------*
FORM handle_hotspot_click  USING    ps_row_id TYPE lvc_s_row.

  " 송장번호 클릭시 송장 상세조회
  CLEAR : gs_grhd.
  READ TABLE gt_grhd INTO gs_grhd INDEX ps_row_id-index.
  CALL FUNCTION 'ZC1FM03MM0001'
    EXPORTING
      iv_grid = gs_grhd-grid
      it_emp  = gt_emp
      it_vend = gt_vend
      it_mara = gt_mara.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form search_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM search_data .
  " 검색 조건에 따른 데이터 조회

  DATA : lv_tabix TYPE sy-tabix,
         lv_cnt   TYPE i,
         lv_return TYPE i VALUE 0. " 유효성검사 리턴값

  PERFORM check_input CHANGING lv_return. " 조회 전 유효성검사

  CHECK lv_return EQ 0.

  CLEAR : gs_grhd, gt_grhd.

  " 전기기간 ranges 세팅
  IF gr_year IS NOT INITIAL AND
     gr_year-low IS NOT INITIAL.

    gr_year-option = 'EQ'.
    gr_year-sign   = 'I'.
    APPEND gr_year.

  ENDIF.

  IF gr_year-low IS INITIAL.

    CLEAR : gr_year, gr_year[].

  ENDIF.

  " 송장번호 값 세팅
  IF gr_grid IS NOT INITIAL AND
     gr_grid-low IS NOT INITIAL.

    gr_grid-option = 'EQ'.
    gr_grid-sign   = 'I'.
    APPEND gr_grid.

  ENDIF.

  IF gr_grid-low IS INITIAL.

    CLEAR : gr_grid, gr_grid[].

  ENDIF.

  " 납품일 세팅
  IF gr_receive_date IS NOT INITIAL AND
     gr_receive_date-low IS NOT INITIAL.

    IF gr_receive_date-high IS INITIAL.

      gr_grid-option = 'EQ'.
      gr_grid-sign   = 'I'.

    ELSE.

      gr_grid-option = 'BT'.
      gr_grid-sign   = 'I'.

    ENDIF.

    APPEND gr_receive_date.

  ENDIF.

  IF gr_receive_date-low IS INITIAL.

    CLEAR : gr_receive_date, gr_receive_date[].

  ENDIF.

  LOOP AT gt_grhd_back INTO gs_grhd.

    IF ( gs_grhd-gjahr IN gr_year ) AND
       ( gs_grhd-grid  IN gr_grid ) AND
       ( gs_grhd-receive_date IN gr_receive_date ).

      APPEND gs_grhd TO gt_grhd.

    ENDIF.

  ENDLOOP.

  lv_cnt = lines( gt_grhd ).
  MESSAGE s008 WITH lv_cnt.

  PERFORM set_main_data.
  PERFORM refresh_grid USING go_grid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_grid
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GO_GRID
*&---------------------------------------------------------------------*
FORM refresh_grid  USING    po_grid TYPE REF TO cl_gui_alv_grid.

  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable = VALUE #( row = abap_true col = abap_true ).

  po_grid->refresh_table_display( EXPORTING is_stable = ls_stable ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form check_input
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM check_input CHANGING pv_return.

  " 년도 유효성검사
  IF gr_year-low IS INITIAL
   OR gr_year-low CP '####'.
    " 통과 (유효한 값)
  ELSE.
    MESSAGE s068 WITH '전기기간 년도' DISPLAY LIKE 'E'.
    pv_return = 1.
  ENDIF.

  " 송장번호 유효성검사
  IF gr_grid-low IS INITIAL
   OR gr_grid-low CP 'GR######'.
    " 통과 (정상적인 형식)
  ELSE.
    MESSAGE s068 WITH '송장번호' DISPLAY LIKE 'E'.
    pv_return = 1.
  ENDIF.

  " 납품일 유효성검사
  IF gr_receive_date-low IS INITIAL AND gr_receive_date-high IS INITIAL.
    " ✅ 둘 다 비어 있는 경우 → 통과

  ELSEIF gr_receive_date-low IS NOT INITIAL AND gr_receive_date-high IS INITIAL.
    " ✅ low만 입력된 경우 → 통과

  ELSEIF gr_receive_date-low IS NOT INITIAL AND gr_receive_date-high IS NOT INITIAL.
    IF gr_receive_date-high < gr_receive_date-low.
      " ❌ high가 low보다 빠르면 오류
      MESSAGE s000 WITH TEXT-E01 DISPLAY LIKE 'E'.
      pv_return = 1.
    ENDIF.

  ELSE.
    " ❌ high만 있고 low는 없음 → 비정상 상황 (필요 시 처리)
    MESSAGE s000 WITH TEXT-E01 DISPLAY LIKE 'E'.
    pv_return = 1.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form init_search_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_search_data .

  " 조회조건 초기화
  CLEAR : gr_year, gr_year[], gr_grid, gr_grid[], gr_receive_date, gr_receive_date[].

ENDFORM.
*&---------------------------------------------------------------------*
*& Form count_num
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM count_num .

  gv_total_cnt = LINES( gt_grhd ).

  CLEAR : gs_grhd, gv_ok_cnt, gv_warning_cnt.
  LOOP AT gt_grhd INTO gs_grhd.

    CASE gs_grhd-gr_status.
      WHEN 'A'.
        gv_warning_cnt += 1.
      WHEN 'B'.
        gv_ok_cnt += 1.
    ENDCASE.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_cell_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> ES_ROW_NO
*&---------------------------------------------------------------------*
FORM handle_cell_click  USING    ps_row_no TYPE LVC_S_ROID.

  CALL TRANSACTION 'ZC103MMR0003'.

ENDFORM.
```